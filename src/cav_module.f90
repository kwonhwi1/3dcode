MODULE CAV_MODULE
  USE CONFIG_MODULE
  USE EOS_MODULE
  IMPLICIT NONE
  PRIVATE
  PUBLIC :: T_CAV,T_MERKLE,T_KUNZ,T_SINGHAL,T_CAV_RESULT
  
  TYPE T_CAV_RESULT
     REAL(8) :: CAVSOURCE,ICAV(4)
  END TYPE
  
  TYPE, ABSTRACT :: T_CAV
    PRIVATE
    REAL(8) :: PREF
    REAL(8) :: UREF,C_C,C_V,DP_REF,T_REF
    REAL(8), POINTER, PUBLIC :: PV(:),DV(:),GRD(:)
    CONTAINS
      PROCEDURE :: CONSTRUCT
      PROCEDURE :: DESTRUCT
      PROCEDURE(P_CAVSOURCE), DEFERRED :: CAVSOURCE
  END TYPE T_CAV
  
  TYPE, EXTENDS(T_CAV) :: T_MERKLE
    CONTAINS
      PROCEDURE :: CAVSOURCE => MERKLE
  END TYPE T_MERKLE
  
  TYPE, EXTENDS(T_CAV) :: T_KUNZ
    CONTAINS
      PROCEDURE :: CAVSOURCE => KUNZ
  END TYPE T_KUNZ
  
  TYPE, EXTENDS(T_CAV) :: T_SINGHAL
    CONTAINS
      PROCEDURE :: CAVSOURCE => SINGHAL
  END TYPE T_SINGHAL
  
  ABSTRACT INTERFACE
    FUNCTION P_CAVSOURCE(CAV,EOS) RESULT(CAV_RESULT)
      IMPORT T_CAV_RESULT
      IMPORT T_CAV
      IMPORT T_EOS
      IMPLICIT NONE
      CLASS(T_CAV), INTENT(IN) :: CAV
      TYPE(T_EOS),INTENT(IN) :: EOS
      TYPE(T_CAV_RESULT) :: CAV_RESULT
    END FUNCTION P_CAVSOURCE
  END INTERFACE
  
  CONTAINS
    !CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
    SUBROUTINE CONSTRUCT(CAV,CONFIG)
      IMPLICIT NONE
      CLASS(T_CAV), INTENT(OUT) :: CAV
      TYPE(T_CONFIG), INTENT(IN) :: CONFIG
        
      CAV%PREF         = CONFIG%GETPREF()
      CAV%UREF         = CONFIG%GETUREF()
      CAV%C_C          = CONFIG%GETC_C()
      CAV%C_V          = CONFIG%GETC_V()
      CAV%DP_REF = 2.D0/(CONFIG%GETRHOREF()*CAV%UREF**2)
      CAV%T_REF = CAV%UREF/CONFIG%GETL_CHORD()
         
    END SUBROUTINE CONSTRUCT
    !CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
    SUBROUTINE DESTRUCT(CAV)
      IMPLICIT NONE
      CLASS(T_CAV), INTENT(INOUT) :: CAV
      
      IF(ASSOCIATED(CAV%GRD))  NULLIFY(CAV%GRD) 
      IF(ASSOCIATED(CAV%PV))   NULLIFY(CAV%PV) 
      IF(ASSOCIATED(CAV%DV))   NULLIFY(CAV%DV)

    END SUBROUTINE DESTRUCT
    !CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
    FUNCTION MERKLE(CAV,EOS) RESULT(CAV_RESULT)
      IMPLICIT NONE
      CLASS(T_MERKLE), INTENT(IN) :: CAV
      TYPE(T_EOS), INTENT(IN) :: EOS
      TYPE(T_CAV_RESULT) :: CAV_RESULT
      REAL(8) :: R_C,R_V,PWW,LAM,RHO1
      REAL(8), PARAMETER :: PHI = 1.D0
      
      CAV_RESULT = T_CAV_RESULT(0.D0,(/0.D0,0.D0,0.D0,0.D0/))
      PWW = EOS%GET_PWW(CAV%PV(5))
      RHO1 = 1.D0/CAV%DV(1)
      
      R_C = CAV%C_C*CAV%DV(1)*CAV%PV(6)*DMAX1(CAV%PV(1)+CAV%PREF-PWW,0.D0)*CAV%DP_REF*CAV%T_REF
      R_V = CAV%C_V*CAV%DV(1)*(1.D0-CAV%PV(6)-CAV%PV(7))*DMAX1(PWW-CAV%PV(1)-CAV%PREF,0.D0)*CAV%DP_REF*CAV%T_REF
           
      CAV_RESULT%CAVSOURCE = (R_V - R_C)*CAV%GRD(1)
      
      IF(R_C.NE.0.D0) THEN
        CAV_RESULT%ICAV(1) = - R_C*(1.D0/(CAV%PV(1)+CAV%PREF-PWW)+CAV%DV(7)*RHO1)
        CAV_RESULT%ICAV(2) = - R_C*CAV%DV(8)*RHO1
        CAV_RESULT%ICAV(3) = - R_C*(1.D0/CAV%PV(6)+CAV%DV(9)*RHO1)
        CAV_RESULT%ICAV(4) = - R_C*CAV%DV(10)*RHO1
      END IF
      IF(R_V.NE.0.D0) THEN
        CAV_RESULT%ICAV(1) = R_V*(-1.D0/(PWW-CAV%PV(1)-CAV%PREF)+CAV%DV(7)*RHO1)
        CAV_RESULT%ICAV(2) = R_V*CAV%DV(8)*RHO1
        CAV_RESULT%ICAV(3) = R_V*(CAV%DV(9)*RHO1-1.D0/(1.D0-CAV%PV(6)-CAV%PV(7)))
        CAV_RESULT%ICAV(4) = R_V*(CAV%DV(10)*RHO1-1.D0/(1.D0-CAV%PV(6)-CAV%PV(7)))
      END IF
      LAM = DSIGN(1.D0,CAV_RESULT%ICAV(3))
      CAV_RESULT%ICAV(:) = CAV_RESULT%ICAV(:)*(PHI*(1.D0-0.5D0*(1.D0-LAM))-0.5D0*(1.D0-LAM))
    END FUNCTION MERKLE
    !CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
    FUNCTION KUNZ(CAV,EOS) RESULT(CAV_RESULT)
      IMPLICIT NONE
      CLASS(T_KUNZ), INTENT(IN) :: CAV
      TYPE(T_EOS), INTENT(IN) :: EOS
      TYPE(T_CAV_RESULT) :: CAV_RESULT
      REAL(8) :: R_C,R_V,AL,AV,AG,PWW,AV1,LAM,RHO1
      REAL(8), PARAMETER :: PHI = 1.D0

      CAV_RESULT = T_CAV_RESULT(0.D0,(/0.D0,0.D0,0.D0,0.D0/))
      PWW = EOS%GET_PWW(CAV%PV(5))
      RHO1 = 1.D0/CAV%DV(1)
      
      AV = CAV%DV(1)*CAV%PV(6)/CAV%DV(4)
      AG = CAV%DV(1)*CAV%PV(7)/CAV%DV(5)
      AL = 1.D0 - AV - AG

      R_C = CAV%C_C*CAV%DV(4)*AV*(AL+AG)**2*CAV%T_REF
      R_V = CAV%C_V*CAV%DV(1)*(1.D0-CAV%PV(6)-CAV%PV(7))*DMAX1(PWW-CAV%PV(1)-CAV%PREF,0.D0)*CAV%DP_REF*CAV%T_REF

      CAV_RESULT%CAVSOURCE = (R_V - R_C)*CAV%GRD(1)
      
      AV1 = 1.D0-4.D0*AV+3.D0*AV**2
      IF(R_C.NE.0.D0 ) THEN
        CAV_RESULT%ICAV(1) = - CAV%C_C*CAV%T_REF*(CAV%DV(7)*CAV%PV(6)*AV1+2.D0*AV**2*CAV%DV(15)*(AL+AG))
        CAV_RESULT%ICAV(2) = - CAV%C_C*CAV%T_REF*(CAV%DV(8)*CAV%PV(6)*AV1+2.D0*AV**2*CAV%DV(16)*(AL+AG))
        CAV_RESULT%ICAV(3) = - CAV%C_C*CAV%T_REF*(CAV%DV(9)*CAV%PV(6)+CAV%DV(1))*AV1
        CAV_RESULT%ICAV(4) = - CAV%C_C*CAV%T_REF*CAV%DV(10)*CAV%PV(6)*AV1
      END IF
      IF(R_V.NE.0.D0) THEN
        CAV_RESULT%ICAV(1) = R_V*(-1.D0/(PWW-CAV%PV(1)-CAV%PREF)+CAV%DV(7)*RHO1)
        CAV_RESULT%ICAV(2) = R_V*CAV%DV(8)*RHO1
        CAV_RESULT%ICAV(3) = R_V*(CAV%DV(9)*RHO1-1.D0/(1.D0-CAV%PV(6)-CAV%PV(7)))
        CAV_RESULT%ICAV(4) = R_V*(CAV%DV(10)*RHO1-1.D0/(1.D0-CAV%PV(6)-CAV%PV(7)))
      END IF
      LAM = DSIGN(1.D0,CAV_RESULT%ICAV(3))
      CAV_RESULT%ICAV(:) = CAV_RESULT%ICAV(:)*(PHI*(1.D0-0.5D0*(1.D0-LAM))-0.5D0*(1.D0-LAM))
    END FUNCTION KUNZ  
    !CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
    FUNCTION SINGHAL(CAV,EOS) RESULT(CAV_RESULT)
      IMPLICIT NONE
      CLASS(T_SINGHAL), INTENT(IN) :: CAV
      TYPE(T_EOS), INTENT(IN) :: EOS
      TYPE(T_CAV_RESULT) :: CAV_RESULT
      REAL(8) :: R_C,R_V,PWW,SIGMA,LAM,RHO1,RHO2
      REAL(8), PARAMETER :: PHI = 1.D0
      
      CAV_RESULT = T_CAV_RESULT(0.D0,(/0.D0,0.D0,0.D0,0.D0/))
      PWW = EOS%GET_PWW(CAV%PV(5))
      SIGMA = EOS%GET_SIGMA(CAV%PV(5))
      RHO1 = 1.D0/CAV%DV(3)
      RHO2 = 1.D0/CAV%DV(4)
      
      R_C = CAV%C_C*CAV%UREF/SIGMA*CAV%DV(3)*CAV%DV(4) &
          *DSQRT(2.D0/3.D0*DMAX1(CAV%PV(1)+CAV%PREF-PWW,0.D0)*RHO1)*CAV%PV(6)
      R_V = CAV%C_V*CAV%UREF/SIGMA*CAV%DV(3)*CAV%DV(4) &
          *DSQRT(2.D0/3.D0*DMAX1(PWW-CAV%PV(1)-CAV%PREF,0.D0)*RHO1)*(1.D0-CAV%PV(6)-CAV%PV(7))

      CAV_RESULT%CAVSOURCE = (R_V - R_C)*CAV%GRD(1)
      
      IF(R_C.NE.0.D0 ) THEN
        CAV_RESULT%ICAV(1) = - R_C*(CAV%DV(15)*RHO2+0.5D0*CAV%DV(17)*RHO1+0.5D0/(CAV%PV(1)+CAV%PREF-PWW))
        CAV_RESULT%ICAV(2) = - R_C*(CAV%DV(16)*RHO2+0.5D0*CAV%DV(18)*RHO1)
        CAV_RESULT%ICAV(3) = - R_C/CAV%PV(6)
        CAV_RESULT%ICAV(4) = 0.D0
      END IF
      IF(R_V.NE.0.D0 ) THEN
        CAV_RESULT%ICAV(1) = R_V*(CAV%DV(15)*RHO2+0.5D0*CAV%DV(17)*RHO1-0.5D0/(PWW-CAV%PV(1)-CAV%PREF))
        CAV_RESULT%ICAV(2) = R_V*(CAV%DV(16)*RHO2+0.5D0*CAV%DV(18)*RHO1)
        CAV_RESULT%ICAV(3) = - R_V/(1.D0-CAV%PV(6)-CAV%PV(7))
        CAV_RESULT%ICAV(4) = - R_V/(1.D0-CAV%PV(6)-CAV%PV(7))
      END IF
      LAM = DSIGN(1.D0,CAV_RESULT%ICAV(3))
      CAV_RESULT%ICAV(:) = CAV_RESULT%ICAV(:)*(PHI*(1.D0-0.5D0*(1.D0-LAM))-0.5D0*(1.D0-LAM))
    END FUNCTION SINGHAL
    !CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
END MODULE CAV_MODULE
    