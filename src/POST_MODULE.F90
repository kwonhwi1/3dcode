MODULE POST_MODULE
  USE CONFIG_MODULE
  USE GRID_MODULE
  USE VARIABLE_MODULE
  IMPLICIT NONE
  PRIVATE
  PUBLIC :: T_POST
  
  TYPE T_POST
    PRIVATE
    REAL(8), DIMENSION(:,:,:), ALLOCATABLE :: PV,TV
    REAL(8), DIMENSION(:,:,:,:), ALLOCATABLE :: QQ
    REAL(8) :: PREF
    INTEGER :: SIZE,RANK,NSTEADY
    INTEGER :: IMAX,JMAX
    INTEGER :: NPV,NTV,NQQ
    CONTAINS
      PROCEDURE :: CONSTRUCT
      PROCEDURE :: DESTRUCT
      PROCEDURE :: EXPORT_VARIABLE
  END TYPE T_POST
  
  CONTAINS
    !CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
    SUBROUTINE CONSTRUCT(POST,CONFIG,GRID,VARIABLE)
      IMPLICIT NONE
      CLASS(T_POST), INTENT(OUT) :: POST
      TYPE(T_CONFIG), INTENT(IN) :: CONFIG
      TYPE(T_GRID), INTENT(IN) :: GRID
      TYPE(T_VARIABLE), INTENT(IN) :: VARIABLE
      
      POST%SIZE = CONFIG%GETSIZE()
      POST%RANK = CONFIG%GETRANK()
      POST%NSTEADY = CONFIG%GETNSTEADY()
      POST%PREF = CONFIG%GETPREF()
      POST%IMAX = GRID%GETIMAX()
      POST%JMAX = GRID%GETJMAX()
      POST%NPV = VARIABLE%GETNPV()
      POST%NTV = VARIABLE%GETNTV()
      POST%NQQ = VARIABLE%GETNQQ() 

      ALLOCATE(POST%PV(POST%NPV,POST%IMAX,POST%JMAX))
      ALLOCATE(POST%TV(POST%NTV,POST%IMAX,POST%JMAX))
      ALLOCATE(POST%QQ(POST%NQQ,VARIABLE%GETNPV(),POST%IMAX,POST%JMAX))
      
    END SUBROUTINE CONSTRUCT
    !CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
    SUBROUTINE DESTRUCT(POST)
      IMPLICIT NONE
      CLASS(T_POST), INTENT(INOUT) :: POST
      
      DEALLOCATE(POST%PV,POST%TV,POST%QQ)
      
    END SUBROUTINE DESTRUCT
    !CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
    SUBROUTINE EXPORT_VARIABLE(POST,VARIABLE,NT_PHY,NT)
      IMPLICIT NONE
      INCLUDE 'mpif.h'
      CLASS(T_POST), INTENT(INOUT) :: POST
      TYPE(T_VARIABLE), INTENT(IN) :: VARIABLE
      INTEGER, INTENT(IN) :: NT_PHY,NT
      INTEGER :: I,J,L,N,M,IER,IO
      CHARACTER(7) :: ITER_TAG
    
      IF(POST%NSTEADY.EQ.1) THEN
        WRITE(ITER_TAG,'(I4.4)') NT_PHY
      ELSE
        WRITE(ITER_TAG,'(I7.7)') NT
      END IF
      
      DO M=0,POST%SIZE-1
        IF(M.EQ.POST%RANK) THEN
          IF(M.EQ.0) THEN
            OPEN(NEWUNIT=IO,FILE='./OUT_'//TRIM(ITER_TAG)//'.DAT',STATUS='UNKNOWN',ACTION='WRITE',FORM='UNFORMATTED')
            WRITE(IO) POST%SIZE,NT_PHY,NT,POST%NQQ
          ELSE
            OPEN(NEWUNIT=IO,FILE='./OUT_'//TRIM(ITER_TAG)//'.DAT',STATUS='OLD',ACTION='WRITE',POSITION='APPEND',FORM='UNFORMATTED')          
          END IF
          WRITE(IO) POST%RANK,POST%IMAX,POST%JMAX
          DO J=2,POST%JMAX
            DO I=2,POST%IMAX
              POST%PV(:,I,J) = VARIABLE%GETPV(I,J)
              POST%PV(1,I,J) = POST%PV(1,I,J)+POST%PREF 
              POST%TV(:,I,J) = VARIABLE%GETTV(I,J)
              DO N=1,POST%NQQ
                POST%QQ(1,:,I,J) = VARIABLE%GETQQ(1,I,J)
                POST%QQ(2,:,I,J) = VARIABLE%GETQQ(2,I,J)
              END DO
            END DO
          END DO
          WRITE(IO) (((POST%PV(N,I,J),N=1,POST%NPV),I=2,POST%IMAX),J=2,POST%JMAX)
          WRITE(IO) (((POST%TV(N,I,J),N=1,POST%NTV),I=2,POST%IMAX),J=2,POST%JMAX)
          WRITE(IO) ((((POST%QQ(L,N,I,J),L=1,POST%NQQ),N=1,POST%NPV),I=2,POST%IMAX),J=2,POST%JMAX)
          CLOSE(IO)
        END IF
        CALL MPI_BARRIER(MPI_COMM_WORLD,IER)
      END DO
      
    END SUBROUTINE EXPORT_VARIABLE
    !CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
END MODULE POST_MODULE