MODULE MUSCL_MODULE
  USE CONFIG_MODULE
  USE VARIABLE_MODULE
  IMPLICIT NONE
  PRIVATE
  PUBLIC :: T_MUSCL,T_TVD,T_MLP

  TYPE, ABSTRACT :: T_MUSCL
    PRIVATE
    INTEGER :: NPV
    REAL(8), POINTER :: X(:,:)
    REAL(8) :: R(2),R1(2),R2(2),ALP(2)
    PROCEDURE(P_LIMITER), POINTER :: LIMITER
    CONTAINS
      PROCEDURE :: CONSTRUCT                                !(ITURB,LIM)
      PROCEDURE :: DESTRUCT
      PROCEDURE :: SETPV                                    !(X(14,NPV))
      PROCEDURE(P_INTERPOLATION), DEFERRED :: INTERPOLATION !(XL(NPV),XR(NPV))
  END TYPE T_MUSCL

  ABSTRACT INTERFACE
    SUBROUTINE P_INTERPOLATION(MUSCL,XL,XR)
      IMPORT T_MUSCL
      IMPLICIT NONE
      CLASS(T_MUSCL), INTENT(INOUT) :: MUSCL
      REAL(8), INTENT(OUT) :: XL(MUSCL%NPV),XR(MUSCL%NPV)
    END SUBROUTINE P_INTERPOLATION
  END INTERFACE

  TYPE, EXTENDS(T_MUSCL) :: T_TVD
    CONTAINS
      PROCEDURE :: INTERPOLATION => TVD
  END TYPE T_TVD

  TYPE, EXTENDS(T_MUSCL) :: T_MLP
    CONTAINS
      PROCEDURE :: INTERPOLATION => MLP
  END TYPE T_MLP

  INTERFACE
    FUNCTION P_LIMITER(MUSCL,N) RESULT(PHI)
      IMPORT T_MUSCL
      IMPLICIT NONE
      CLASS(T_MUSCL), INTENT(IN) :: MUSCL
      INTEGER, INTENT(IN) :: N
      REAL(8)  :: PHI
    END FUNCTION P_LIMITER
  END INTERFACE
  
  CONTAINS
    !CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
    SUBROUTINE CONSTRUCT(MUSCL,CONFIG,VARIABLE)
      IMPLICIT NONE
      CLASS(T_MUSCL), INTENT(OUT) :: MUSCL
      TYPE(T_CONFIG), INTENT(IN) :: CONFIG
      TYPE(T_VARIABLE), INTENT(IN) :: VARIABLE
      
      SELECT CASE(CONFIG%GETNLIM())
      CASE(0)
        MUSCL%LIMITER => NO_LIMITER
      CASE(1)
        MUSCL%LIMITER => MINMOD
      CASE(2)
        MUSCL%LIMITER => SUPERBEE
      CASE(3)
        MUSCL%LIMITER => VANLEER
      CASE(4)
        MUSCL%LIMITER => MC
      CASE(5)
        MUSCL%LIMITER => VANALBADA
      CASE(6)
        MUSCL%LIMITER => BETA
      CASE(7)
        MUSCL%LIMITER => I_3RD
      CASE(8)
        MUSCL%LIMITER => I_5TH
      END SELECT
      
      MUSCL%NPV  = VARIABLE%GETNPV()
    END SUBROUTINE CONSTRUCT
    !CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
    SUBROUTINE DESTRUCT(MUSCL)
      IMPLICIT NONE
      CLASS(T_MUSCL), INTENT(INOUT) :: MUSCL
      
      IF(ASSOCIATED(MUSCL%X))       NULLIFY(MUSCL%X)
      IF(ASSOCIATED(MUSCL%LIMITER)) NULLIFY(MUSCL%LIMITER)

    END SUBROUTINE DESTRUCT
    !CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
    SUBROUTINE SETPV(MUSCL,X)
      IMPLICIT NONE
      CLASS(T_MUSCL), INTENT(INOUT) :: MUSCL
      REAL(8), INTENT(IN), TARGET :: X(14,MUSCL%NPV)
      
      MUSCL%X => X
      
    END SUBROUTINE SETPV
    !CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
    SUBROUTINE TVD(MUSCL,XL,XR)
      IMPLICIT NONE
      CLASS(T_TVD), INTENT(INOUT) :: MUSCL
      REAL(8), INTENT(OUT) :: XL(MUSCL%NPV),XR(MUSCL%NPV)
      INTEGER :: K
      REAL(8) :: DQ,DQM,DQP,DQM1,DQP1

      
      DO K = 1, MUSCL%NPV    
        DQ   = MUSCL%X(4,K)   - MUSCL%X(3,K)  !I+1/2
        DQM  = MUSCL%X(3,K)   - MUSCL%X(8,K)  !I-1/2
        DQM1 = MUSCL%X(8,K)   - MUSCL%X(13,K) !I-3/2
        DQP  = MUSCL%X(11,K)  - MUSCL%X(4,K)  !I+3/2
        DQP1 = MUSCL%X(14,K)  - MUSCL%X(11,K) !I+5/2

        IF(DABS(DQM).LE. 1.D-10) THEN
          MUSCL%R(1)  = 0.D0
          MUSCL%R1(1) = 0.D0
          MUSCL%R2(1) = 0.D0
        ELSE
          MUSCL%R(1)  = DQ/DQM
          MUSCL%R1(1) = DQM1/DQM ! INVERSE
          MUSCL%R2(1) = DQP/DQM
        END IF
        IF(DABS(DQP).LE.1.D-10) THEN
          MUSCL%R(2)  = 0.D0
          MUSCL%R1(2) = 0.D0
          MUSCL%R2(2) = 0.D0
        ELSE
          MUSCL%R(2)  = DQ/DQP ! INVERSE
          MUSCL%R1(2) = DQP1/DQP
          MUSCL%R2(2) = DQM/DQP ! INVERSE
        END IF
      
        MUSCL%ALP(1) = 2.D0
        MUSCL%ALP(2) = 2.D0
      
        XL(K) = MUSCL%X(3,K) + 0.5D0*MUSCL%LIMITER(1)*DQM
        XR(K) = MUSCL%X(4,K) - 0.5D0*MUSCL%LIMITER(2)*DQP
      END DO
    END SUBROUTINE TVD
    !CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
    SUBROUTINE MLP(MUSCL,XL,XR)
      IMPLICIT NONE
      CLASS(T_MLP), INTENT(INOUT) :: MUSCL
      REAL(8), INTENT(OUT) :: XL(MUSCL%NPV),XR(MUSCL%NPV)
      INTEGER :: K
      REAL(8) :: DQ,DQM,DQP,DQM1,DQP1
      REAL(8) :: RXY_L,RXY_R,QML,QMR,QMIN,QMAX
      REAL(8), PARAMETER :: EPS = 1.D-2

      DO K = 1, MUSCL%NPV     
        DQ   = MUSCL%X(4,K)   - MUSCL%X(3,K)  !I+1/2
        DQM  = MUSCL%X(3,K)   - MUSCL%X(8,K)  !I-1/2
        DQM1 = MUSCL%X(8,K)   - MUSCL%X(13,K) !I-3/2
        DQP  = MUSCL%X(11,K)  - MUSCL%X(4,K)  !I+3/2
        DQP1 = MUSCL%X(14,K)  - MUSCL%X(11,K) !I+5/2

        IF(DABS(DQM).LE. 1.D-10) THEN
          MUSCL%R(1)  = 0.D0
          MUSCL%R1(1) = 0.D0
          MUSCL%R2(1) = 0.D0
        ELSE
          MUSCL%R(1)  = DQ/DQM
          MUSCL%R1(1) = DQM1/DQM ! INVERSE
          MUSCL%R2(1) = DQP/DQM
        END IF
        IF(DABS(DQP).LE.1.D-10) THEN
          MUSCL%R(2)  = 0.D0
          MUSCL%R1(2) = 0.D0
          MUSCL%R2(2) = 0.D0
        ELSE
          MUSCL%R(2)  = DQ/DQP ! INVERSE
          MUSCL%R1(2) = DQP1/DQP
          MUSCL%R2(2) = DQM/DQP ! INVERSE
        END IF

        IF(DABS(MUSCL%X(4,K)-MUSCL%X(8,K)).LE.1.D-10) THEN
          RXY_L = 0.D0
        ELSE
          RXY_L = DABS((MUSCL%X(5,K)-MUSCL%X(1,K))/(MUSCL%X(4,K)-MUSCL%X(8,K)))
        END IF
        IF(DABS(MUSCL%X(11,K)-MUSCL%X(3,K)).LE.1.D-10) THEN
          RXY_R = 0.D0
        ELSE      
          RXY_R = DABS((MUSCL%X(6,K)-MUSCL%X(2,K))/(MUSCL%X(11,K)-MUSCL%X(3,K)))
        END IF
        
        IF(RXY_L.LT.EPS) THEN
          QMIN = DMIN1(MUSCL%X(1,K),MUSCL%X(2,K),MUSCL%X(3,K),MUSCL%X(4,K),MUSCL%X(5,K),MUSCL%X(6,K),MUSCL%X(7,K),MUSCL%X(8,K),MUSCL%X(9,K))
          QMAX = DMAX1(MUSCL%X(1,K),MUSCL%X(2,K),MUSCL%X(3,K),MUSCL%X(4,K),MUSCL%X(5,K),MUSCL%X(6,K),MUSCL%X(7,K),MUSCL%X(8,K),MUSCL%X(9,K))
        ELSE
          IF((MUSCL%X(4,K)-MUSCL%X(8,K)).GT.0.D0) THEN
            QMAX = DMAX1(MUSCL%X(1,K),MUSCL%X(2,K),MUSCL%X(3,K),MUSCL%X(4,K),MUSCL%X(5,K),MUSCL%X(6,K))
            QMIN = DMIN1(MUSCL%X(1,K),MUSCL%X(7,K),MUSCL%X(3,K),MUSCL%X(8,K),MUSCL%X(5,K),MUSCL%X(9,K))
          ELSE
            QMIN = DMIN1(MUSCL%X(1,K),MUSCL%X(2,K),MUSCL%X(3,K),MUSCL%X(4,K),MUSCL%X(5,K),MUSCL%X(6,K))
            QMAX = DMAX1(MUSCL%X(1,K),MUSCL%X(7,K),MUSCL%X(3,K),MUSCL%X(8,K),MUSCL%X(5,K),MUSCL%X(9,K))
          END IF
        END IF
        QML = DMIN1(DABS(QMAX-MUSCL%X(3,K)),DABS(QMIN-MUSCL%X(3,K)))
        
        IF(RXY_R.LT.EPS) THEN
          QMIN = DMIN1(MUSCL%X(1,K),MUSCL%X(2,K),MUSCL%X(3,K),MUSCL%X(4,K),MUSCL%X(5,K),MUSCL%X(6,K),MUSCL%X(10,K),MUSCL%X(11,K),MUSCL%X(12,K))
          QMAX = DMAX1(MUSCL%X(1,K),MUSCL%X(2,K),MUSCL%X(3,K),MUSCL%X(4,K),MUSCL%X(5,K),MUSCL%X(6,K),MUSCL%X(10,K),MUSCL%X(11,K),MUSCL%X(12,K))  
        ELSE
          IF((MUSCL%X(11,K)-MUSCL%X(3,K)).GT.0.D0) THEN
            QMAX = DMAX1(MUSCL%X(10,K),MUSCL%X(2,K),MUSCL%X(11,K),MUSCL%X(4,K),MUSCL%X(12,K),MUSCL%X(6,K))
            QMIN = DMIN1(MUSCL%X(1,K),MUSCL%X(2,K),MUSCL%X(3,K),MUSCL%X(4,K),MUSCL%X(5,K),MUSCL%X(6,K))                
          ELSE
            QMIN = DMIN1(MUSCL%X(10,K),MUSCL%X(2,K),MUSCL%X(11,K),MUSCL%X(4,K),MUSCL%X(12,K),MUSCL%X(6,K))
            QMAX = DMAX1(MUSCL%X(1,K),MUSCL%X(2,K),MUSCL%X(3,K),MUSCL%X(4,K),MUSCL%X(5,K),MUSCL%X(6,K))
          END IF
        END IF
        QMR = DMIN1(DABS(QMAX-MUSCL%X(4,K)),DABS(QMIN-MUSCL%X(4,K)))
        
        !IF(DQ.GT.0.D0) THEN
        !  QML = DMAX1(MUSCL%X(1,K),MUSCL%X(2,K),MUSCL%X(3,K),MUSCL%X(4,K),MUSCL%X(5,K),MUSCL%X(6,K))
        !  QMR = DMIN1(MUSCL%X(1,K),MUSCL%X(2,K),MUSCL%X(3,K),MUSCL%X(4,K),MUSCL%X(5,K),MUSCL%X(6,K))        
        !ELSE
        !  QML = DMIN1(MUSCL%X(1,K),MUSCL%X(2,K),MUSCL%X(3,K),MUSCL%X(4,K),MUSCL%X(5,K),MUSCL%X(6,K))
        !  QMR = DMAX1(MUSCL%X(1,K),MUSCL%X(2,K),MUSCL%X(3,K),MUSCL%X(4,K),MUSCL%X(5,K),MUSCL%X(6,K))
        !END IF

        IF(DABS(DQ).LE.1.D-10) THEN
          MUSCL%ALP(1) = 2.D0
          MUSCL%ALP(2) = 2.D0
        ELSE
          MUSCL%ALP(1) = 2.D0*DMAX1(1.D0,MUSCL%R(1))/DABS(DQ)/(1.D0+RXY_L)*QML
          MUSCL%ALP(2) = 2.D0*DMAX1(1.D0,MUSCL%R(2))/DABS(DQ)/(1.D0+RXY_R)*QMR
        END IF

        MUSCL%ALP(1) = DMAX1(1.D0,DMIN1(2.D0,MUSCL%ALP(1)))
        MUSCL%ALP(2) = DMAX1(1.D0,DMIN1(2.D0,MUSCL%ALP(2)))
      
        XL(K) = MUSCL%X(3,K) + 0.5D0*MUSCL%LIMITER(1)*DQM
        XR(K) = MUSCL%X(4,K) - 0.5D0*MUSCL%LIMITER(2)*DQP
      END DO
    END SUBROUTINE MLP
    !CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
    FUNCTION NO_LIMITER(MUSCL,N) RESULT(PHI)
      IMPLICIT NONE
      CLASS(T_MUSCL), INTENT(IN) :: MUSCL
      INTEGER, INTENT(IN) :: N
      REAL(8)  :: PHI
      
      PHI = 1.D0

    END FUNCTION
    !CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
    FUNCTION MINMOD(MUSCL,N) RESULT(PHI)
      IMPLICIT NONE
      CLASS(T_MUSCL), INTENT(IN) :: MUSCL
      INTEGER, INTENT(IN) :: N
      REAL(8)  :: PHI

      PHI = DMAX1(0.D0,DMIN1(1.D0,MUSCL%R(N)))

    END FUNCTION
    !CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
    FUNCTION SUPERBEE(MUSCL,N) RESULT(PHI)
      IMPLICIT NONE
      CLASS(T_MUSCL), INTENT(IN) :: MUSCL
      INTEGER, INTENT(IN) :: N
      REAL(8)  :: PHI

      PHI = DMAX1(0.D0,DMIN1(1.D0,MUSCL%ALP(N)*MUSCL%R(N)),DMIN1(MUSCL%ALP(N),MUSCL%R(N))) 

    END FUNCTION
    !CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
    FUNCTION VANLEER(MUSCL,N) RESULT(PHI)
      IMPLICIT NONE
      CLASS(T_MUSCL), INTENT(IN) :: MUSCL
      INTEGER, INTENT(IN) :: N
      REAL(8)  :: PHI
      REAL(8) :: C

      C = (MUSCL%R(N)+DABS(MUSCL%R(N)))/(1.D0+DABS(MUSCL%R(N)))
      PHI = DMAX1(0.D0,DMIN1(MUSCL%ALP(N),MUSCL%ALP(N)*MUSCL%R(N),C))

    END FUNCTION
    !CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
    FUNCTION MC(MUSCL,N) RESULT(PHI)
      IMPLICIT NONE
      CLASS(T_MUSCL), INTENT(IN) :: MUSCL
      INTEGER, INTENT(IN) :: N
      REAL(8)  :: PHI
      REAL(8) :: C

      C = (1.D0+MUSCL%R(N))/2.D0
      PHI = DMAX1(0.D0,DMIN1(MUSCL%ALP(N),MUSCL%ALP(N)*MUSCL%R(N),C))
        
    END FUNCTION
    !CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
    FUNCTION VANALBADA(MUSCL,N) RESULT(PHI)
      IMPLICIT NONE
      CLASS(T_MUSCL), INTENT(IN) :: MUSCL
      INTEGER, INTENT(IN) :: N
      REAL(8)  :: PHI
      REAL(8) :: C

      C =(MUSCL%R(N)**2+MUSCL%R(N))/(1.D0+MUSCL%R(N)**2)
      PHI = DMAX1(0.D0,DMIN1(C*MUSCL%R(N),1.D0),DMIN1(MUSCL%R(N),C))
        
    END FUNCTION
    !CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
    FUNCTION BETA(MUSCL,N) RESULT(PHI)
      IMPLICIT NONE
      CLASS(T_MUSCL), INTENT(IN) :: MUSCL
      INTEGER, INTENT(IN) :: N
      REAL(8)  :: PHI
      REAL(8) :: C

      C = DMIN1(1.5D0,MUSCL%ALP(N))
      PHI = DMAX1(0.D0,DMIN1(C*MUSCL%R(N),1.D0),DMIN1(MUSCL%R(N),C))
    
    END FUNCTION
    !CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
    FUNCTION I_3RD(MUSCL,N) RESULT(PHI)
      IMPLICIT NONE
      CLASS(T_MUSCL), INTENT(IN) :: MUSCL
      INTEGER, INTENT(IN) :: N
      REAL(8)  :: PHI
      REAL(8) :: C

      C = (1.D0+2.D0*MUSCL%R(N))/3.D0
      PHI = DMAX1(0.D0,DMIN1(MUSCL%ALP(N),MUSCL%ALP(N)*MUSCL%R(N),C))

    END FUNCTION
    !CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
    FUNCTION I_5TH(MUSCL,N) RESULT(PHI)
      IMPLICIT NONE
      CLASS(T_MUSCL), INTENT(IN) :: MUSCL
      INTEGER, INTENT(IN) :: N
      REAL(8)  :: PHI
      REAL(8) :: C
      
      C = (-2.D0*MUSCL%R1(N)+11.D0+24.D0*MUSCL%R(N)-3.D0*MUSCL%R2(N))/30.D0
      PHI = DMAX1(0.D0,DMIN1(MUSCL%ALP(N),MUSCL%ALP(N)*MUSCL%R(N),C))
        
    END FUNCTION
    !CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
END MODULE MUSCL_MODULE