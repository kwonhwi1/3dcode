MODULE TURBSOURCE_MODULE
  USE CONFIG_MODULE
  USE GRID_MODULE
  USE VARIABLE_MODULE
  IMPLICIT NONE
  PRIVATE
  PUBLIC :: T_TURBSOURCE,T_KEPSILON,T_KWSST
  
  TYPE, ABSTRACT :: T_TURBSOURCE
    PRIVATE
    LOGICAL :: TCOMP
    INTEGER :: NPV,NDV,NTV,NGRD
    REAL(8) :: OMEGA_CUT
    REAL(8), POINTER :: CX1(:),CX2(:),EX1(:),EX2(:),TX1(:),TX2(:)
    REAL(8), POINTER :: PV(:,:)
    REAL(8), POINTER :: DV(:),GRD(:),TV(:)
    CONTAINS
      PROCEDURE :: CONSTRUCT
      PROCEDURE :: DESTRUCT
      PROCEDURE :: SETNORM      ! (CX1,CX2,EX1,EX2,TX1,TX2)
      PROCEDURE :: SETGRD       ! (GRD) VOL,XCEN,YCEN,ZCEN,YDNS
      PROCEDURE :: SETPV        ! (PV) P,U,V,W,T,Y1,Y2,K,O
      PROCEDURE :: SETDV        ! (DV) RHO,H,RHOL,RHOV,RHOG,SND2,DRDP,DRDT,DRDY1,DRDY2,DHDP,DHDT,DHDY1,DHDY2,DRDPV,DRDTV,DRDPL,DRDTL
      PROCEDURE :: SETTV        ! (TV) VIS,COND,EMUT
      PROCEDURE(P_CALTURBSOURCE), DEFERRED :: CALTURBSOURCE
      PROCEDURE :: GETOMEGA_CUT
      PROCEDURE, PRIVATE :: CALBIGF
  END TYPE T_TURBSOURCE
  
  TYPE, EXTENDS(T_TURBSOURCE) :: T_KEPSILON
    CONTAINS
      PROCEDURE ::  CALTURBSOURCE => KEPSILON
  END TYPE T_KEPSILON
  
  TYPE, EXTENDS(T_TURBSOURCE) :: T_KWSST
    CONTAINS
      PROCEDURE ::  CALTURBSOURCE => KWSST
  END TYPE T_KWSST
  
  ABSTRACT INTERFACE
    FUNCTION P_CALTURBSOURCE(TS)
      IMPORT T_TURBSOURCE
      IMPLICIT NONE
      CLASS(T_TURBSOURCE), INTENT(INOUT) :: TS
      REAL(8) :: P_CALTURBSOURCE(6)
    END FUNCTION
  END INTERFACE
  
  CONTAINS
    !CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
    SUBROUTINE CONSTRUCT(TURBSOURCE,CONFIG,GRID,VARIABLE)
      IMPLICIT NONE
      CLASS(T_TURBSOURCE), INTENT(OUT) :: TURBSOURCE
      TYPE(T_CONFIG), INTENT(IN) :: CONFIG
      TYPE(T_GRID), INTENT(IN) :: GRID
      TYPE(T_VARIABLE), INTENT(IN) :: VARIABLE
      
      SELECT CASE(CONFIG%GETTCOMP())
      CASE(0)
        TURBSOURCE%TCOMP = .FALSE.
      CASE(1)
        TURBSOURCE%TCOMP = .TRUE.
      END SELECT
           
      TURBSOURCE%NGRD = GRID%GETNGRD()
      TURBSOURCE%NPV  = VARIABLE%GETNPV()
      TURBSOURCE%NDV  = VARIABLE%GETNDV()
      TURBSOURCE%NTV  = VARIABLE%GETNTV()

    END SUBROUTINE CONSTRUCT
    !CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
    SUBROUTINE DESTRUCT(TURBSOURCE)
      IMPLICIT NONE
      CLASS(T_TURBSOURCE), INTENT(INOUT) :: TURBSOURCE

      IF(ASSOCIATED(TURBSOURCE%CX1))     NULLIFY(TURBSOURCE%CX1)     
      IF(ASSOCIATED(TURBSOURCE%CX2))     NULLIFY(TURBSOURCE%CX2)     
      IF(ASSOCIATED(TURBSOURCE%EX1))     NULLIFY(TURBSOURCE%EX1)     
      IF(ASSOCIATED(TURBSOURCE%EX2))     NULLIFY(TURBSOURCE%EX2)
      IF(ASSOCIATED(TURBSOURCE%TX1))     NULLIFY(TURBSOURCE%TX1)     
      IF(ASSOCIATED(TURBSOURCE%TX2))     NULLIFY(TURBSOURCE%TX2)
      IF(ASSOCIATED(TURBSOURCE%GRD))     NULLIFY(TURBSOURCE%GRD)           
      IF(ASSOCIATED(TURBSOURCE%PV))      NULLIFY(TURBSOURCE%PV)      
      IF(ASSOCIATED(TURBSOURCE%DV))      NULLIFY(TURBSOURCE%DV)      
      IF(ASSOCIATED(TURBSOURCE%TV))      NULLIFY(TURBSOURCE%TV)      
      
    END SUBROUTINE DESTRUCT
    !CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
    SUBROUTINE SETNORM(TURBSOURCE,CX1,CX2,EX1,EX2,TX1,TX2)
      IMPLICIT NONE
      CLASS(T_TURBSOURCE), INTENT(INOUT) :: TURBSOURCE
      REAL(8), INTENT(IN), TARGET :: CX1(3),CX2(3),EX1(3),EX2(3),TX1(3),TX2(3)
      
      TURBSOURCE%CX1 => CX1
      TURBSOURCE%CX2 => CX2
      TURBSOURCE%EX1 => EX1
      TURBSOURCE%EX2 => EX2
      TURBSOURCE%TX1 => TX1
      TURBSOURCE%TX2 => TX2
      
    END SUBROUTINE SETNORM
    !CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
    SUBROUTINE SETGRD(TURBSOURCE,GRD)
      IMPLICIT NONE
      CLASS(T_TURBSOURCE), INTENT(INOUT) :: TURBSOURCE
      REAL(8), INTENT(IN), TARGET :: GRD(TURBSOURCE%NGRD)
      
      TURBSOURCE%GRD => GRD
      
    END SUBROUTINE SETGRD
    !CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
    SUBROUTINE SETPV(TURBSOURCE,PV)
      IMPLICIT NONE
      CLASS(T_TURBSOURCE), INTENT(INOUT) :: TURBSOURCE
      REAL(8), INTENT(IN), TARGET :: PV(38,TURBSOURCE%NPV) ! PV(7,:)~PV(38,:) ARE NOT USED !! PLZ CHECK
      
      TURBSOURCE%PV => PV
      
    END SUBROUTINE SETPV
    !CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
    SUBROUTINE SETDV(TURBSOURCE,DV)
      IMPLICIT NONE
      CLASS(T_TURBSOURCE), INTENT(INOUT) :: TURBSOURCE
      REAL(8), INTENT(IN), TARGET :: DV(TURBSOURCE%NDV)
      
      TURBSOURCE%DV => DV
      
    END SUBROUTINE SETDV
    !CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
    SUBROUTINE SETTV(TURBSOURCE,TV)
      IMPLICIT NONE
      CLASS(T_TURBSOURCE), INTENT(INOUT) :: TURBSOURCE
      REAL(8), INTENT(IN), TARGET :: TV(TURBSOURCE%NTV)
      
      TURBSOURCE%TV => TV
      
    END SUBROUTINE SETTV
    !CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
    FUNCTION KEPSILON(TS)
      IMPLICIT NONE
      CLASS(T_KEPSILON), INTENT(INOUT) :: TS
      REAL(8) :: KEPSILON(6)
      REAL(8) :: K1,K2,K3,K4,K5,K6
      REAL(8) :: U1,U2,U3,U4,U5,U6
      REAL(8) :: V1,V2,V3,V4,V5,V6
      REAL(8) :: W1,W2,W3,W4,W5,W6
      REAL(8) :: DKDX,DKDY,DKDZ,DUDX,DUDY,DUDZ,DVDX,DVDY,DVDZ,DWDX,DWDY,DWDZ
      REAL(8) :: KKYY,PROD,RE_T,FE1,FE2,MT2,EC,PD,DPDK,DPDO
      REAL(8) :: LAM1,LAM2,T,D
      REAL(8), PARAMETER :: PHI = 1.D0
      
      K1 = 0.5D0*(TS%PV(1,8)+TS%PV(2,8))
      K2 = 0.5D0*(TS%PV(3,8)+TS%PV(2,8))
      K3 = 0.5D0*(TS%PV(4,8)+TS%PV(2,8))
      K4 = 0.5D0*(TS%PV(5,8)+TS%PV(2,8))
      K5 = 0.5D0*(TS%PV(6,8)+TS%PV(2,8))
      K6 = 0.5D0*(TS%PV(7,8)+TS%PV(2,8))
      
      U1 = 0.5D0*(TS%PV(1,2)+TS%PV(2,2))
      U2 = 0.5D0*(TS%PV(3,2)+TS%PV(2,2))
      U3 = 0.5D0*(TS%PV(4,2)+TS%PV(2,2))
      U4 = 0.5D0*(TS%PV(5,2)+TS%PV(2,2))
      U5 = 0.5D0*(TS%PV(6,2)+TS%PV(2,2))
      U6 = 0.5D0*(TS%PV(7,2)+TS%PV(2,2))
      
      V1 = 0.5D0*(TS%PV(1,3)+TS%PV(2,3))
      V2 = 0.5D0*(TS%PV(3,3)+TS%PV(2,3))
      V3 = 0.5D0*(TS%PV(4,3)+TS%PV(2,3))
      V4 = 0.5D0*(TS%PV(5,3)+TS%PV(2,3))
      V5 = 0.5D0*(TS%PV(6,3)+TS%PV(2,3))
      V6 = 0.5D0*(TS%PV(7,3)+TS%PV(2,3))

      W1 = 0.5D0*(TS%PV(1,4)+TS%PV(2,4))
      W2 = 0.5D0*(TS%PV(3,4)+TS%PV(2,4))
      W3 = 0.5D0*(TS%PV(4,4)+TS%PV(2,4))
      W4 = 0.5D0*(TS%PV(5,4)+TS%PV(2,4))
      W5 = 0.5D0*(TS%PV(6,4)+TS%PV(2,4))
      W6 = 0.5D0*(TS%PV(7,4)+TS%PV(2,4))
      
      DKDX = (DSQRT(K2)*TS%CX2(1)+DSQRT(K4)*TS%EX2(1)+DSQRT(K6)*TS%TX2(1)-DSQRT(K1)*TS%CX1(1)-DSQRT(K3)*TS%EX1(1)-DSQRT(K5)*TS%TX1(1))/TS%GRD(1)
      DKDY = (DSQRT(K2)*TS%CX2(2)+DSQRT(K4)*TS%EX2(2)+DSQRT(K6)*TS%TX2(2)-DSQRT(K1)*TS%CX1(2)-DSQRT(K3)*TS%EX1(2)-DSQRT(K5)*TS%TX1(2))/TS%GRD(1)
      DKDZ = (DSQRT(K2)*TS%CX2(3)+DSQRT(K4)*TS%EX2(3)+DSQRT(K6)*TS%TX2(3)-DSQRT(K1)*TS%CX1(3)-DSQRT(K3)*TS%EX1(3)-DSQRT(K5)*TS%TX1(3))/TS%GRD(1)
      
      DUDX = (U2*TS%CX2(1)+U4*TS%EX2(1)+U6*TS%TX2(1)-U1*TS%CX1(1)-U3*TS%EX1(1)-U5*TS%TX1(1))/TS%GRD(1)
      DUDY = (U2*TS%CX2(2)+U4*TS%EX2(2)+U6*TS%TX2(2)-U1*TS%CX1(2)-U3*TS%EX1(2)-U5*TS%TX1(2))/TS%GRD(1)
      DUDZ = (U2*TS%CX2(3)+U4*TS%EX2(3)+U6*TS%TX2(3)-U1*TS%CX1(3)-U3*TS%EX1(3)-U5*TS%TX1(3))/TS%GRD(1)
      
      DVDX = (V2*TS%CX2(1)+V4*TS%EX2(1)+V6*TS%TX2(1)-V1*TS%CX1(1)-V3*TS%EX1(1)-V5*TS%TX1(1))/TS%GRD(1)
      DVDY = (V2*TS%CX2(2)+V4*TS%EX2(2)+V6*TS%TX2(2)-V1*TS%CX1(2)-V3*TS%EX1(2)-V5*TS%TX1(2))/TS%GRD(1)
      DVDZ = (V2*TS%CX2(3)+V4*TS%EX2(3)+V6*TS%TX2(3)-V1*TS%CX1(3)-V3*TS%EX1(3)-V5*TS%TX1(3))/TS%GRD(1)
      
      DWDX = (W2*TS%CX2(1)+W4*TS%EX2(1)+W6*TS%TX2(1)-W1*TS%CX1(1)-W3*TS%EX1(1)-W5*TS%TX1(1))/TS%GRD(1)
      DWDY = (W2*TS%CX2(2)+W4*TS%EX2(2)+W6*TS%TX2(2)-W1*TS%CX1(2)-W3*TS%EX1(2)-W5*TS%TX1(2))/TS%GRD(1)
      DWDZ = (W2*TS%CX2(3)+W4*TS%EX2(3)+W6*TS%TX2(3)-W1*TS%CX1(3)-W3*TS%EX1(3)-W5*TS%TX1(3))/TS%GRD(1)
      
      KKYY = DKDX**2+DKDY**2+DKDZ**2

      PROD = TS%TV(3)*(2.D0*(DUDX**2+DVDY**2+DWDZ**2)+(DUDY+DVDX)**2+(DUDZ+DWDX)**2+(DVDZ+DWDY)**2         &
           - 2.D0/3.D0*(DUDX+DVDY+DWDZ)**2) -2.D0/3.D0*TS%DV(1)*TS%PV(2,8)*(DUDX+DVDY+DWDZ)
      
      RE_T = TS%DV(1)*TS%PV(2,8)**2/TS%PV(2,9)/TS%TV(1)
      
      FE1 = 1.D0 - DEXP(-(RE_T/40.D0)**2)
      FE2 = 1.D0 - 2.D0/9.D0*DEXP(-(RE_T/6.D0)**2)
      
      PROD = DMIN1(PROD,30.D0*TS%DV(1)*TS%PV(2,9))
      
      IF(TS%TCOMP) THEN
        MT2 = 2.D0*TS%PV(2,8)/TS%DV(6)
        EC = MT2*TS%PV(2,9)
        PD = -0.4D0*PROD*MT2+0.2D0*TS%DV(1)*TS%PV(2,9)*MT2
      ELSE
        MT2 = 0.D0
        EC = 0.D0
        PD = 0.D0      
      END IF
      
      TS%OMEGA_CUT = 0.D0
      KEPSILON(1) = (PROD - TS%DV(1)*(TS%PV(2,9)+EC) + PD - 2.D0*TS%TV(1)*KKYY)*TS%GRD(1)
      KEPSILON(2) = (1.5D0*FE1*PROD*TS%PV(2,9) - 1.9D0*FE2*TS%DV(1)*TS%PV(2,9)**2 &
                   + 2.9556D0*TS%TV(1)*TS%PV(2,9)*KKYY)/TS%PV(2,8)*TS%GRD(1)
     
      
      DPDK = 2.D0*(PROD+2.D0/3.D0*TS%DV(1)*TS%PV(2,8)*(DUDX+DVDY+DWDZ))/TS%PV(2,8)-2.D0/3.D0*TS%DV(1)*(DUDX+DVDY+DWDZ)
      DPDO = - (PROD+2.D0/3.D0*TS%DV(1)*TS%PV(2,8)*(DUDX+DVDY+DWDZ))/TS%PV(2,9)
      
      KEPSILON(3) = DPDK
      KEPSILON(4) = DPDO - TS%DV(1)
      KEPSILON(5) = (1.9D0*FE2*TS%DV(1)*TS%PV(2,9)**2 - 2.9556D0*TS%TV(1)*TS%PV(2,9)*KKYY )/TS%PV(2,8)**2
      KEPSILON(6) = (1.5D0*FE1*DPDO*TS%PV(2,9) + 1.5D0*FE1*PROD - 3.8D0*FE2*TS%DV(1)*TS%PV(2,9) + 2.9556D0*TS%TV(1)*KKYY)/TS%PV(2,8)
      
      IF(TS%TCOMP) THEN
        KEPSILON(3) = KEPSILON(3) + (- 1.6D0*TS%DV(1)*TS%PV(2,9) - 0.8D0*DPDK*TS%PV(2,8)- 0.8D0*PROD)/TS%DV(6)
        KEPSILON(4) = KEPSILON(4) + (- TS%DV(1) - 0.4D0*DPDO + 0.2D0*TS%DV(1))*MT2
      END IF
      
      T = KEPSILON(3)+KEPSILON(6)
      D = KEPSILON(3)*KEPSILON(6)-KEPSILON(4)*KEPSILON(5)
      LAM1 = 0.5D0*T + DSQRT(DMAX1(0.25D0*T**2-D,0.D0))
      LAM2 = 0.5D0*T - DSQRT(DMAX1(0.25D0*T**2-D,0.D0))
      LAM1 = DSIGN(1.D0,LAM1)
      LAM2 = DSIGN(1.D0,LAM2)
      
      KEPSILON(3) = KEPSILON(3)*(PHI*(1.D0-0.5D0*(1.D0-LAM1))-0.5D0*(1.D0-LAM1))
      KEPSILON(4) = KEPSILON(4)*(PHI*(1.D0-0.5D0*(1.D0-LAM1))-0.5D0*(1.D0-LAM1))
      KEPSILON(5) = KEPSILON(5)*(PHI*(1.D0-0.5D0*(1.D0-LAM2))-0.5D0*(1.D0-LAM2))
      KEPSILON(6) = KEPSILON(6)*(PHI*(1.D0-0.5D0*(1.D0-LAM2))-0.5D0*(1.D0-LAM2))
    END FUNCTION KEPSILON
    !CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
    FUNCTION KWSST(TS)
      IMPLICIT NONE
      CLASS(T_KWSST), INTENT(INOUT) :: TS
      REAL(8) :: KWSST(6)
      REAL(8) :: K1,K2,K3,K4,K5,K6
      REAL(8) :: O1,O2,O3,O4,O5,O6
      REAL(8) :: U1,U2,U3,U4,U5,U6
      REAL(8) :: V1,V2,V3,V4,V5,V6
      REAL(8) :: W1,W2,W3,W4,W5,W6
      REAL(8) :: DKDX,DKDY,DKDZ,DODX,DODY,DODZ
      REAL(8) :: DUDX,DUDY,DUDZ,DVDX,DVDY,DVDZ,DWDX,DWDY,DWDZ
      REAL(8) :: PROD,BIGF,TALPHA,TBETA,MT2,PD,DPDK,DPDO
      REAL(8) :: LAM1,LAM2,T,D
      REAL(8), PARAMETER :: PHI = 1.D0

      K1 = 0.5D0*(TS%PV(1,8)+TS%PV(2,8))
      K2 = 0.5D0*(TS%PV(3,8)+TS%PV(2,8))
      K3 = 0.5D0*(TS%PV(4,8)+TS%PV(2,8))
      K4 = 0.5D0*(TS%PV(5,8)+TS%PV(2,8))
      K5 = 0.5D0*(TS%PV(6,8)+TS%PV(2,8))
      K6 = 0.5D0*(TS%PV(7,8)+TS%PV(2,8))

      O1 = 0.5D0*(TS%PV(1,9)+TS%PV(2,9))
      O2 = 0.5D0*(TS%PV(3,9)+TS%PV(2,9))
      O3 = 0.5D0*(TS%PV(4,9)+TS%PV(2,9))
      O4 = 0.5D0*(TS%PV(5,9)+TS%PV(2,9))
      O5 = 0.5D0*(TS%PV(6,9)+TS%PV(2,9))
      O6 = 0.5D0*(TS%PV(7,9)+TS%PV(2,9))
      
      U1 = 0.5D0*(TS%PV(1,2)+TS%PV(2,2))
      U2 = 0.5D0*(TS%PV(3,2)+TS%PV(2,2))
      U3 = 0.5D0*(TS%PV(4,2)+TS%PV(2,2))
      U4 = 0.5D0*(TS%PV(5,2)+TS%PV(2,2))
      U5 = 0.5D0*(TS%PV(6,2)+TS%PV(2,2))
      U6 = 0.5D0*(TS%PV(7,2)+TS%PV(2,2))
      
      V1 = 0.5D0*(TS%PV(1,3)+TS%PV(2,3))
      V2 = 0.5D0*(TS%PV(3,3)+TS%PV(2,3))
      V3 = 0.5D0*(TS%PV(4,3)+TS%PV(2,3))
      V4 = 0.5D0*(TS%PV(5,3)+TS%PV(2,3))
      V5 = 0.5D0*(TS%PV(6,3)+TS%PV(2,3))
      V6 = 0.5D0*(TS%PV(7,3)+TS%PV(2,3))

      W1 = 0.5D0*(TS%PV(1,4)+TS%PV(2,4))
      W2 = 0.5D0*(TS%PV(3,4)+TS%PV(2,4))
      W3 = 0.5D0*(TS%PV(4,4)+TS%PV(2,4))
      W4 = 0.5D0*(TS%PV(5,4)+TS%PV(2,4))
      W5 = 0.5D0*(TS%PV(6,4)+TS%PV(2,4))
      W6 = 0.5D0*(TS%PV(7,4)+TS%PV(2,4))
      
      DKDX = (K2*TS%CX2(1)+K4*TS%EX2(1)+K6*TS%TX2(1)-K1*TS%CX1(1)-K3*TS%EX1(1)-K5*TS%TX1(1))/TS%GRD(1)
      DKDY = (K2*TS%CX2(2)+K4*TS%EX2(2)+K6*TS%TX2(2)-K1*TS%CX1(2)-K3*TS%EX1(2)-K5*TS%TX1(2))/TS%GRD(1)
      DKDZ = (K2*TS%CX2(3)+K4*TS%EX2(3)+K6*TS%TX2(3)-K1*TS%CX1(3)-K3*TS%EX1(3)-K5*TS%TX1(3))/TS%GRD(1)

      DODX = (O2*TS%CX2(1)+O4*TS%EX2(1)+O6*TS%TX2(1)-O1*TS%CX1(1)-O3*TS%EX1(1)-O5*TS%TX1(1))/TS%GRD(1)
      DODY = (O2*TS%CX2(2)+O4*TS%EX2(2)+O6*TS%TX2(2)-O1*TS%CX1(2)-O3*TS%EX1(2)-O5*TS%TX1(2))/TS%GRD(1)
      DODZ = (O2*TS%CX2(3)+O4*TS%EX2(3)+O6*TS%TX2(3)-O1*TS%CX1(3)-O3*TS%EX1(3)-O5*TS%TX1(3))/TS%GRD(1)
      
      DUDX = (U2*TS%CX2(1)+U4*TS%EX2(1)+U6*TS%TX2(1)-U1*TS%CX1(1)-U3*TS%EX1(1)-U5*TS%TX1(1))/TS%GRD(1)
      DUDY = (U2*TS%CX2(2)+U4*TS%EX2(2)+U6*TS%TX2(2)-U1*TS%CX1(2)-U3*TS%EX1(2)-U5*TS%TX1(2))/TS%GRD(1)
      DUDZ = (U2*TS%CX2(3)+U4*TS%EX2(3)+U6*TS%TX2(3)-U1*TS%CX1(3)-U3*TS%EX1(3)-U5*TS%TX1(3))/TS%GRD(1)
      
      DVDX = (V2*TS%CX2(1)+V4*TS%EX2(1)+V6*TS%TX2(1)-V1*TS%CX1(1)-V3*TS%EX1(1)-V5*TS%TX1(1))/TS%GRD(1)
      DVDY = (V2*TS%CX2(2)+V4*TS%EX2(2)+V6*TS%TX2(2)-V1*TS%CX1(2)-V3*TS%EX1(2)-V5*TS%TX1(2))/TS%GRD(1)
      DVDZ = (V2*TS%CX2(3)+V4*TS%EX2(3)+V6*TS%TX2(3)-V1*TS%CX1(3)-V3*TS%EX1(3)-V5*TS%TX1(3))/TS%GRD(1)
      
      DWDX = (W2*TS%CX2(1)+W4*TS%EX2(1)+W6*TS%TX2(1)-W1*TS%CX1(1)-W3*TS%EX1(1)-W5*TS%TX1(1))/TS%GRD(1)
      DWDY = (W2*TS%CX2(2)+W4*TS%EX2(2)+W6*TS%TX2(2)-W1*TS%CX1(2)-W3*TS%EX1(2)-W5*TS%TX1(2))/TS%GRD(1)
      DWDZ = (W2*TS%CX2(3)+W4*TS%EX2(3)+W6*TS%TX2(3)-W1*TS%CX1(3)-W3*TS%EX1(3)-W5*TS%TX1(3))/TS%GRD(1)
      
      PROD = TS%TV(3)*(2.D0*(DUDX**2+DVDY**2+DWDZ**2)+(DUDY+DVDX)**2+(DUDZ+DWDX)**2+(DVDZ+DWDY)**2         &
           - 2.D0/3.D0*(DUDX+DVDY+DWDZ)**2) -2.D0/3.D0*TS%DV(1)*TS%PV(2,8)*(DUDX+DVDY+DWDZ) 
      
      PROD = DMIN1(PROD,0.9D0*TS%DV(1)*TS%PV(2,8)*TS%PV(2,9))
      
      TS%OMEGA_CUT = DSQRT(2.D0*(DUDX**2+DVDY**2+DWDZ**2)+(DUDY+DVDX)**2+(DUDZ+DWDX)**2+(DVDZ+DWDY)**2     &
           - 2.D0/3.D0*(DUDX+DVDY+DWDZ)**2)
      
      BIGF = TS%CALBIGF(DKDX*DODX+DKDY*DODY+DKDZ*DODZ)
      
      TALPHA = BIGF*5.D0/9.D0 +(1.D0-BIGF)*0.44D0
      TBETA  = BIGF*0.075D0   +(1.D0-BIGF)*0.0828D0
      
      IF(TS%TCOMP) THEN
        MT2 = 2.D0*TS%PV(2,8)/TS%DV(6)
        PD = -0.4D0*PROD*MT2+0.018D0*TS%DV(1)*TS%PV(2,8)*TS%PV(2,9)*MT2
      ELSE
        MT2 = 0.D0
        PD = 0.D0      
      END IF
      
      KWSST(1) = (PROD - 0.09D0*TS%DV(1)*TS%PV(2,8)*TS%PV(2,9)*(1.D0+5.D0/9.D0*MT2*(1.D0-BIGF)) &
               + (1.D0-BIGF)*PD)*TS%GRD(1)
      KWSST(2) = (TALPHA*TS%DV(1)/TS%TV(3)*PROD - TBETA*TS%DV(1)*TS%PV(2,9)**2 &
               + 1.712D0*(1.D0-BIGF)*TS%DV(1)/TS%PV(2,9)*(DKDX*DODX+DKDY*DODY+DKDZ*DODZ) &
               + (1.D0-BIGF)*0.05D0*MT2*TS%DV(1)*TS%PV(2,9)**2 &
               - (1.D0-BIGF)*TS%DV(1)/TS%TV(3)*PD )*TS%GRD(1)

      DPDK = PROD/TS%PV(2,8)   
      DPDO = - (PROD+2.D0/3.D0*TS%DV(1)*TS%PV(2,8)*(DUDX+DVDY+DWDZ))/TS%PV(2,9)

      KWSST(3) = DPDK - 0.09D0*TS%DV(1)*TS%PV(2,8)
      KWSST(4) = DPDO - 0.09D0*TS%DV(1)*TS%PV(2,9)
      KWSST(5) = 0.D0
      KWSST(6) = TALPHA*TS%DV(1)/TS%TV(3)*DPDO + TALPHA*TS%DV(1)/TS%TV(3)*PROD/TS%PV(2,9) - 2.D0*TBETA*TS%DV(1)*TS%PV(2,9) &
                 - 1.712D0*(1.D0-BIGF)*TS%DV(1)/TS%PV(2,9)**2*(DKDX*DODX+DKDY*DODY+DKDZ*DODZ)
                 
      IF(TS%TCOMP) THEN
        KWSST(3) = KWSST(3) + (- 0.128D0*TS%DV(1)*TS%PV(2,8)*TS%PV(2,9)/TS%DV(6) - 0.4D0*DPDK*MT2 - 0.8D0*PROD/TS%DV(6) )*(1.D0-BIGF)
        KWSST(4) = KWSST(4) + (- 0.032D0*TS%DV(1)*TS%PV(2,8)*MT2 - 0.4D0*DPDO*MT2 )*(1.D0-BIGF)
        KWSST(5) = KWSST(5) + (0.1D0*TS%DV(1)*TS%PV(2,9)**2/TS%DV(6) + TS%DV(1)/TS%TV(3)*PD/TS%PV(2,8) &
                            - TS%DV(1)/TS%TV(3)*(-0.4D0*DPDK*MT2-0.8D0*PROD/TS%DV(6)+0.072D0*TS%DV(1)*TS%PV(2,8)*TS%PV(2,9)/TS%DV(6)))*(1.D0-BIGF)
        KWSST(6) = KWSST(6) + (0.2D0*TS%DV(1)*TS%PV(2,9)*TS%PV(2,8)/TS%DV(6) - TS%DV(1)/TS%TV(3)*PD/TS%PV(2,9) &
                            - TS%DV(1)/TS%TV(3)*(-0.4D0*DPDO*MT2+0.018D0*TS%DV(1)*TS%PV(2,8)*MT2))*(1.D0-BIGF)
      END IF
                 
      T = KWSST(3)+KWSST(6)
      D = KWSST(3)*KWSST(6)-KWSST(4)*KWSST(5)
      LAM1 = 0.5D0*T + DSQRT(DMAX1(0.25D0*T**2-D,0.D0))
      LAM2 = 0.5D0*T - DSQRT(DMAX1(0.25D0*T**2-D,0.D0))
      LAM1 = DSIGN(1.D0,LAM1)
      LAM2 = DSIGN(1.D0,LAM2)
      
      KWSST(3) = KWSST(3)*(PHI*(1.D0-0.5D0*(1.D0-LAM1))-0.5D0*(1.D0-LAM1))
      KWSST(4) = KWSST(4)*(PHI*(1.D0-0.5D0*(1.D0-LAM1))-0.5D0*(1.D0-LAM1))
      KWSST(5) = KWSST(5)*(PHI*(1.D0-0.5D0*(1.D0-LAM2))-0.5D0*(1.D0-LAM2))
      KWSST(6) = KWSST(6)*(PHI*(1.D0-0.5D0*(1.D0-LAM2))-0.5D0*(1.D0-LAM2))
      
    END FUNCTION KWSST
    !CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
    FUNCTION CALBIGF(TS,PKW)
      IMPLICIT NONE
      CLASS(T_TURBSOURCE), INTENT(IN) :: TS
      REAL(8), INTENT(IN) :: PKW
      REAL(8) :: CALBIGF
      REAL(8) :: TERM0,TERM1,TERM2,TERM3,CDKW,ARG1,ARG2
      
      TERM0 = 1.712D0*TS%DV(1)/TS%PV(2,9)*PKW
      CDKW = DMAX1(TERM0,1.D-10)
      
      TERM1 = DSQRT(TS%PV(2,8))/(0.09D0*TS%PV(2,9)*TS%GRD(5))
      TERM2 = 500.D0*TS%TV(1)/TS%DV(1)/(TS%PV(2,9)*TS%GRD(5)**2)
      TERM3 = (3.424D0*TS%DV(1)*TS%PV(2,8))/(CDKW*TS%GRD(5)**2)
      
      ARG2 = DMAX1(TERM1,TERM2)
      ARG1 = DMIN1(ARG2,TERM3)
      
      CALBIGF = DTANH(ARG1**4)
      
    END FUNCTION CALBIGF
    !CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
    FUNCTION GETOMEGA_CUT(TS)
      IMPLICIT NONE
      CLASS(T_TURBSOURCE), INTENT(IN) :: TS
      REAL(8) :: GETOMEGA_CUT
      GETOMEGA_CUT = TS%OMEGA_CUT
    END FUNCTION GETOMEGA_CUT
    !CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
END MODULE TURBSOURCE_MODULE
