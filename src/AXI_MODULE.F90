MODULE AXI_MODULE
  USE CONFIG_MODULE
  USE GRID_MODULE
  USE VARIABLE_MODULE
  IMPLICIT NONE
  PRIVATE
  PUBLIC :: T_AXI,T_AXI_INVISCID,T_AXI_VISCOUS
  
  TYPE, ABSTRACT :: T_AXI
    PRIVATE
    INTEGER :: NPV,NTV,NGRD
    REAL(8) :: PREF
    REAL(8), POINTER :: CX1(:),CX2(:),EX1(:),EX2(:)
    REAL(8), POINTER :: TV(:),GRD(:)
    REAL(8), POINTER :: PV(:,:)
    PROCEDURE(P_VISEFF), POINTER :: VISEFF
    CONTAINS
      PROCEDURE :: CONSTRUCT
      PROCEDURE :: DESTRUCT
      PROCEDURE :: SETNORM      ! (CX1,CX2,EX1,EX2)
      PROCEDURE :: SETGRD       ! (GRD) VOL,XCEN,YCEN,YDNS
      PROCEDURE :: SETPV        ! (PV) P,U,V,T,Y1,Y2,K
      PROCEDURE :: SETTV        ! (TV) VIS,COND,EMUT
      PROCEDURE(P_CALAXI), DEFERRED :: CALAXI
  END TYPE T_AXI

  TYPE, EXTENDS(T_AXI) :: T_AXI_INVISCID
    CONTAINS
      PROCEDURE :: CALAXI => AXI_INVISCID
  END TYPE

  TYPE, EXTENDS(T_AXI) :: T_AXI_VISCOUS
    CONTAINS
      PROCEDURE :: CALAXI => AXI_VISCOUS  
  END TYPE
  
  ABSTRACT INTERFACE
    FUNCTION P_CALAXI(AXI) RESULT(AXI_SOURCE)
      IMPORT T_AXI
      IMPLICIT NONE
      CLASS(T_AXI), INTENT(IN) :: AXI
      REAL(8) :: AXI_SOURCE
    END FUNCTION P_CALAXI
  END INTERFACE
  
  INTERFACE
    FUNCTION P_VISEFF(AXI) RESULT(VAR)
      IMPORT T_AXI
      IMPLICIT NONE
      CLASS(T_AXI), INTENT(IN) :: AXI
      REAL(8) :: VAR
    END FUNCTION P_VISEFF
  END INTERFACE
  CONTAINS
    !CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
    SUBROUTINE CONSTRUCT(AXI,CONFIG,GRID,VARIABLE)
      IMPLICIT NONE
      CLASS(T_AXI), INTENT(OUT) :: AXI
      TYPE(T_CONFIG), INTENT(IN) :: CONFIG
      TYPE(T_GRID), INTENT(IN) :: GRID
      TYPE(T_VARIABLE), INTENT(IN) :: VARIABLE
      
      SELECT CASE(CONFIG%GETITURB())
      CASE(-3)
        AXI%VISEFF => NULL()
      CASE(-2)
        AXI%VISEFF => LAMINAR
      CASE(-1,0)
        AXI%VISEFF => TURBULENT
      END SELECT
      
      AXI%PREF = CONFIG%GETPREF()
      
      AXI%NGRD = GRID%GETNGRD()
      AXI%NPV = VARIABLE%GETNPV()
      AXI%NTV = VARIABLE%GETNTV()

    END SUBROUTINE CONSTRUCT
    !CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
    SUBROUTINE DESTRUCT(AXI)
      IMPLICIT NONE
      CLASS(T_AXI), INTENT(INOUT) :: AXI

      IF(ASSOCIATED(AXI%CX1))    NULLIFY(AXI%CX1)      
      IF(ASSOCIATED(AXI%CX2))    NULLIFY(AXI%CX2)      
      IF(ASSOCIATED(AXI%EX1))    NULLIFY(AXI%EX1)      
      IF(ASSOCIATED(AXI%EX2))    NULLIFY(AXI%EX2)  
      IF(ASSOCIATED(AXI%GRD))    NULLIFY(AXI%GRD)      
      IF(ASSOCIATED(AXI%PV))     NULLIFY(AXI%PV)   
      IF(ASSOCIATED(AXI%TV))     NULLIFY(AXI%TV)       
      IF(ASSOCIATED(AXI%VISEFF)) NULLIFY(AXI%VISEFF)   
      
    END SUBROUTINE DESTRUCT
    !CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
    SUBROUTINE SETNORM(AXI,CX1,CX2,EX1,EX2)
      IMPLICIT NONE
      CLASS(T_AXI), INTENT(INOUT) :: AXI
      REAL(8), INTENT(IN), TARGET :: CX1(2),CX2(2),EX1(2),EX2(2)
      
      AXI%CX1 => CX1
      AXI%CX2 => CX2
      AXI%EX1 => EX1
      AXI%EX2 => EX2
    
    END SUBROUTINE SETNORM
    !CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
    SUBROUTINE SETGRD(AXI,GRD)
      IMPLICIT NONE
      CLASS(T_AXI), INTENT(INOUT) :: AXI
      REAL(8), INTENT(IN), TARGET :: GRD(AXI%NGRD)
      
      AXI%GRD => GRD
    
    END SUBROUTINE SETGRD
    !CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
    SUBROUTINE SETPV(AXI,PV)
      IMPLICIT NONE
      CLASS(T_AXI), INTENT(INOUT) :: AXI
      REAL(8), INTENT(IN), TARGET :: PV(10,AXI%NPV) ! PV(6,:)~PV(10,:) ARE NOT USED !! PLZ CHECK
      
      AXI%PV => PV

    END SUBROUTINE SETPV
    !CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
    SUBROUTINE SETTV(AXI,TV)
      IMPLICIT NONE
      CLASS(T_AXI), INTENT(INOUT) :: AXI
      REAL(8), INTENT(IN), TARGET :: TV(AXI%NTV)
      
      AXI%TV => TV

    END SUBROUTINE SETTV
    !CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
    FUNCTION AXI_INVISCID(AXI) RESULT(AXI_SOURCE)
      IMPLICIT NONE
      CLASS(T_AXI_INVISCID), INTENT(IN) :: AXI
      REAL(8) :: AXI_SOURCE

      AXI_SOURCE = (AXI%PV(2,1)+AXI%PREF)*AXI%GRD(1)
   
    END FUNCTION AXI_INVISCID
    !CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
    FUNCTION AXI_VISCOUS(AXI) RESULT(AXI_SOURCE)
      IMPLICIT NONE
      CLASS(T_AXI_VISCOUS), INTENT(IN) :: AXI
      REAL(8) :: AXI_SOURCE
      REAL(8) :: U1,U2,U3,U4,V1,V2,V3,V4
      REAL(8) :: DUDX,DVDY,TAUTT
 
      U1 = 0.5D0*(AXI%PV(1,2)+AXI%PV(2,2))
      U2 = 0.5D0*(AXI%PV(3,2)+AXI%PV(2,2))
      U3 = 0.5D0*(AXI%PV(4,2)+AXI%PV(2,2))
      U4 = 0.5D0*(AXI%PV(5,2)+AXI%PV(2,2))
      
      V1 = 0.5D0*(AXI%PV(1,3)+AXI%PV(2,3))
      V2 = 0.5D0*(AXI%PV(3,3)+AXI%PV(2,3))
      V3 = 0.5D0*(AXI%PV(4,3)+AXI%PV(2,3))
      V4 = 0.5D0*(AXI%PV(5,3)+AXI%PV(2,3))
      
      DUDX = (U2*AXI%CX2(1)+U4*AXI%EX2(1)-U1*AXI%CX1(1)-U3*AXI%EX1(1))/AXI%GRD(1)
      DVDY = (V2*AXI%CX2(2)+V4*AXI%EX2(2)-V1*AXI%CX1(2)-V3*AXI%EX1(2))/AXI%GRD(1)      
      TAUTT = AXI%VISEFF()*(2.D0*AXI%PV(2,3)/AXI%GRD(3)-2.D0/3.D0*(DUDX+DVDY+AXI%PV(2,3)/AXI%GRD(3)))        
      AXI_SOURCE = (AXI%PV(2,1)+AXI%PREF-TAUTT)*AXI%GRD(1)
   
    END FUNCTION AXI_VISCOUS
    !CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
    FUNCTION LAMINAR(AXI) RESULT(VAR)
      IMPLICIT NONE
      CLASS(T_AXI), INTENT(IN) :: AXI
      REAL(8) :: VAR
      VAR = AXI%TV(1)
    END FUNCTION LAMINAR
    !CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
    FUNCTION TURBULENT(AXI) RESULT(VAR)
      IMPLICIT NONE
      CLASS(T_AXI), INTENT(IN) :: AXI
      REAL(8) :: VAR
      VAR = AXI%TV(1)+AXI%TV(3)
    END FUNCTION TURBULENT
    !CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
END MODULE AXI_MODULE