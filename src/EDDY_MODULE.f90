MODULE EDDY_MODULE
  USE CONFIG_MODULE
  USE GRID_MODULE
  USE VARIABLE_MODULE
  IMPLICIT NONE
  PRIVATE
  PUBLIC :: T_EDDY,T_EDDY_KE,T_EDDY_KWSST
  
  TYPE, ABSTRACT :: T_EDDY
    PRIVATE
    INTEGER :: NPV,NDV,NTV,NGRD
    REAL(8), POINTER :: PV(:,:)
    REAL(8), POINTER :: CX1(:),CX2(:),EX1(:),EX2(:)
    REAL(8), POINTER :: DV(:),TV(:),GRD(:)
    PROCEDURE(P_CALSKK2), POINTER :: CALSKK2
    CONTAINS
      PROCEDURE :: CONSTRUCT
      PROCEDURE :: DESTRUCT
      PROCEDURE :: SETNORM
      PROCEDURE :: SETGRD
      PROCEDURE :: SETPV
      PROCEDURE :: SETDV
      PROCEDURE :: SETTV
      PROCEDURE(P_CALEDDY), DEFERRED :: CALEDDY
  END TYPE T_EDDY
  
  ABSTRACT INTERFACE
    FUNCTION P_CALEDDY(EDDY) RESULT(EMUT)
      IMPORT T_EDDY
      CLASS(T_EDDY), INTENT(IN) :: EDDY
      REAL(8) :: EMUT
    END FUNCTION P_CALEDDY
  END INTERFACE
  
  TYPE, EXTENDS(T_EDDY) :: T_EDDY_KE
    CONTAINS
      PROCEDURE :: CALEDDY => KEPSILON
  END TYPE T_EDDY_KE

  TYPE, EXTENDS(T_EDDY) :: T_EDDY_KWSST
    CONTAINS
      PROCEDURE :: CALEDDY => KWSST
  END TYPE T_EDDY_KWSST

  INTERFACE
    FUNCTION P_CALSKK2(EDDY,DUDX,DVDY) RESULT(SKK2)
      IMPORT T_EDDY
      IMPLICIT NONE
      CLASS(T_EDDY), INTENT(IN) :: EDDY
      REAL(8), INTENT(IN) :: DUDX,DVDY
      REAL(8) :: SKK2
    END FUNCTION P_CALSKK2
  END INTERFACE
  CONTAINS
    !CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
    SUBROUTINE CONSTRUCT(EDDY,CONFIG,GRID,VARIABLE)
      IMPLICIT NONE
      CLASS(T_EDDY), INTENT(OUT) :: EDDY
      TYPE(T_CONFIG), INTENT(IN) :: CONFIG
      TYPE(T_GRID), INTENT(IN) :: GRID
      TYPE(T_VARIABLE), INTENT(IN) :: VARIABLE

      SELECT CASE(CONFIG%GETNAXI())
      CASE(0)
        EDDY%CALSKK2 => SKK2_NOAXI
      CASE(1)
        EDDY%CALSKK2 => SKK2_AXI
      END SELECT
      
      EDDY%NGRD = GRID%GETNGRD()
      EDDY%NPV  = VARIABLE%GETNPV()
      EDDY%NDV  = VARIABLE%GETNDV()
      EDDY%NTV  = VARIABLE%GETNTV()

    END SUBROUTINE CONSTRUCT
    !CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
    SUBROUTINE DESTRUCT(EDDY)
      IMPLICIT NONE
      CLASS(T_EDDY), INTENT(INOUT) :: EDDY
      
      IF(ASSOCIATED(EDDY%CX1))     NULLIFY(EDDY%CX1)
      IF(ASSOCIATED(EDDY%CX2))     NULLIFY(EDDY%CX2)
      IF(ASSOCIATED(EDDY%EX1))     NULLIFY(EDDY%EX1)
      IF(ASSOCIATED(EDDY%EX2))     NULLIFY(EDDY%EX2)
      IF(ASSOCIATED(EDDY%GRD))     NULLIFY(EDDY%GRD)
      IF(ASSOCIATED(EDDY%PV))      NULLIFY(EDDY%PV) 
      IF(ASSOCIATED(EDDY%DV))      NULLIFY(EDDY%DV) 
      IF(ASSOCIATED(EDDY%TV))      NULLIFY(EDDY%TV) 
      IF(ASSOCIATED(EDDY%CALSKK2)) NULLIFY(EDDY%CALSKK2)
    END SUBROUTINE DESTRUCT
    !CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
    SUBROUTINE SETNORM(EDDY,CX1,CX2,EX1,EX2)
      IMPLICIT NONE
      CLASS(T_EDDY), INTENT(INOUT) :: EDDY
      REAL(8), INTENT(IN), TARGET :: CX1(2),CX2(2),EX1(2),EX2(2)
      
      EDDY%CX1 => CX1
      EDDY%CX2 => CX2
      EDDY%EX1 => EX1
      EDDY%EX2 => EX2
      
    END SUBROUTINE SETNORM
    !CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
    SUBROUTINE SETGRD(EDDY,GRD)
      IMPLICIT NONE
      CLASS(T_EDDY), INTENT(INOUT) :: EDDY
      REAL(8), INTENT(IN), TARGET :: GRD(EDDY%NGRD)
      
      EDDY%GRD => GRD
    
    END SUBROUTINE SETGRD
    !CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
    SUBROUTINE SETPV(EDDY,PV)
      IMPLICIT NONE
      CLASS(T_EDDY), INTENT(INOUT) :: EDDY
      REAL(8), INTENT(IN), TARGET :: PV(5,EDDY%NPV)
      
      EDDY%PV => PV
      
    END SUBROUTINE SETPV
    !CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
    SUBROUTINE SETDV(EDDY,DV)
      IMPLICIT NONE
      CLASS(T_EDDY), INTENT(INOUT) :: EDDY
      REAL(8), INTENT(IN), TARGET :: DV(EDDY%NDV)
      
      EDDY%DV => DV
    
    END SUBROUTINE SETDV
    !CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
    SUBROUTINE SETTV(EDDY,TV)
      IMPLICIT NONE
      CLASS(T_EDDY), INTENT(INOUT) :: EDDY
      REAL(8), INTENT(IN), TARGET :: TV(EDDY%NTV)
      
      EDDY%TV => TV
    
    END SUBROUTINE SETTV
    !CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
    FUNCTION KEPSILON(EDDY) RESULT(EMUT)
      CLASS(T_EDDY_KE), INTENT(IN) :: EDDY
      REAL(8) :: EMUT
      REAL(8) :: RE_T,RE_E,F_MU
      
      RE_T = EDDY%DV(1)*EDDY%PV(2,7)**2/EDDY%PV(2,8)/EDDY%TV(1)
      RE_E = EDDY%DV(1)*(EDDY%TV(1)/EDDY%DV(1)*EDDY%PV(2,8))**0.25D0*EDDY%GRD(4)/EDDY%TV(1)
      F_MU = (1.D0+3.D0/RE_T**(3.D0/4.D0))*(1.D0+80.D0*DEXP(-RE_E))*(1.D0-DEXP(-RE_E/43.D0-RE_E**2/330.D0))**2
      EMUT = 0.09D0*EDDY%DV(1)*EDDY%PV(2,7)**2/EDDY%PV(2,8)*F_MU    
    END FUNCTION KEPSILON
    !CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
    FUNCTION KWSST(EDDY) RESULT(EMUT)
      CLASS(T_EDDY_KWSST), INTENT(IN) :: EDDY
      REAL(8) :: EMUT
      REAL(8) :: U1,U2,U3,U4,V1,V2,V3,V4
      REAL(8) :: DUDX,DUDY,DVDX,DVDY
      REAL(8) :: TERM1,TERM2,ARG2,BIGF2
      REAL(8) :: SIJSIJ,SSS,TERM4,TERM5,TERM6

      U1 = 0.5D0*(EDDY%PV(1,2)+EDDY%PV(2,2))
      U2 = 0.5D0*(EDDY%PV(3,2)+EDDY%PV(2,2))
      U3 = 0.5D0*(EDDY%PV(4,2)+EDDY%PV(2,2))
      U4 = 0.5D0*(EDDY%PV(5,2)+EDDY%PV(2,2))

      V1 = 0.5D0*(EDDY%PV(1,3)+EDDY%PV(2,3))
      V2 = 0.5D0*(EDDY%PV(3,3)+EDDY%PV(2,3))
      V3 = 0.5D0*(EDDY%PV(4,3)+EDDY%PV(2,3))
      V4 = 0.5D0*(EDDY%PV(5,3)+EDDY%PV(2,3))
      
      DUDX = (U2*EDDY%CX2(1)+U4*EDDY%EX2(1)-U1*EDDY%CX1(1)-U3*EDDY%EX1(1))/EDDY%GRD(1)
      DUDY = (U2*EDDY%CX2(2)+U4*EDDY%EX2(2)-U1*EDDY%CX1(2)-U3*EDDY%EX1(2))/EDDY%GRD(1) 
      DVDX = (V2*EDDY%CX2(1)+V4*EDDY%EX2(1)-V1*EDDY%CX1(1)-V3*EDDY%EX1(1))/EDDY%GRD(1)
      DVDY = (V2*EDDY%CX2(2)+V4*EDDY%EX2(2)-V1*EDDY%CX1(2)-V3*EDDY%EX1(2))/EDDY%GRD(1)

      TERM1 = DSQRT(EDDY%PV(2,7))/(0.09D0*EDDY%PV(2,8)*EDDY%GRD(4))
      TERM2 = 500.D0*EDDY%TV(1)/EDDY%DV(1)/(EDDY%PV(2,8)*EDDY%GRD(4)**2)
      ARG2 = DMAX1(2.D0*TERM1,TERM2)
      BIGF2 = DTANH(ARG2**2)   
      SIJSIJ = EDDY%CALSKK2(DUDX,DVDY)+0.5D0*(DUDY+DVDX)**2
      SSS = DSQRT(2.D0*SIJSIJ)
      TERM4 = 0.31D0*EDDY%PV(2,8)
      TERM5 = SSS*BIGF2
      TERM6 = DMAX1(TERM4,TERM5)
      EMUT = 0.31*EDDY%DV(1)*EDDY%PV(2,7)/TERM6
    END FUNCTION KWSST
    !CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
    FUNCTION SKK2_NOAXI(EDDY,DUDX,DVDY) RESULT(SKK2)
      IMPLICIT NONE
      CLASS(T_EDDY), INTENT(IN) :: EDDY
      REAL(8), INTENT(IN) :: DUDX,DVDY
      REAL(8) :: SKK2
      
      SKK2 = DUDX**2+DVDY**2
    END FUNCTION SKK2_NOAXI
    !CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
    FUNCTION SKK2_AXI(EDDY,DUDX,DVDY) RESULT(SKK2)
      IMPLICIT NONE
      CLASS(T_EDDY), INTENT(IN) :: EDDY
      REAL(8), INTENT(IN) :: DUDX,DVDY
      REAL(8) :: SKK2

      SKK2 = DUDX**2+DVDY**2+(EDDY%PV(2,3)/EDDY%GRD(3))**2
    END FUNCTION SKK2_AXI
    !CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
END MODULE EDDY_MODULE