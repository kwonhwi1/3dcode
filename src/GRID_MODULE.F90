MODULE GRID_MODULE
  USE CONFIG_MODULE
  IMPLICIT NONE
  PRIVATE
  PUBLIC :: T_GRID,T_BCINFO,T_CONNECTINFO

  TYPE T_BCINFO
    INTEGER :: ISTART(2),IEND(2)
    CHARACTER(32) :: BCNAME
  END TYPE T_BCINFO
  
  TYPE T_CONNECTINFO
    INTEGER :: DONOR,TRANSMAT(2,2)
    INTEGER :: ISTART(2),IEND(2),ISTART_DONOR(2),IEND_DONOR(2)
  END TYPE T_CONNECTINFO
  
  TYPE T_GRID
    PRIVATE
    INTEGER :: RANK
    INTEGER :: IMAX,JMAX,NBC,NCON,NGRD
    TYPE(T_BCINFO), DIMENSION(:), ALLOCATABLE :: BCINFO
    TYPE(T_CONNECTINFO), DIMENSION(:), ALLOCATABLE ::CONNECTINFO
    REAL(8), DIMENSION(:,:,:), ALLOCATABLE :: X
    REAL(8), DIMENSION(:,:,:), ALLOCATABLE :: CX,EX
    REAL(8), DIMENSION(:,:,:), ALLOCATABLE :: GRD ! VOL,XCEN,YCEN,YDNS
    CONTAINS
      PROCEDURE :: CONSTRUCT
      PROCEDURE :: DESTRUCT
      PROCEDURE, PRIVATE :: CALYDNS
      PROCEDURE, PRIVATE :: CALNORMAL
      PROCEDURE, PRIVATE :: CALVOLUME
      PROCEDURE :: GETIMAX
      PROCEDURE :: GETJMAX
      PROCEDURE :: GETNBC
      PROCEDURE :: GETNCON
      PROCEDURE :: GETX
      PROCEDURE :: GETCX
      PROCEDURE :: GETEX
      PROCEDURE :: GETNGRD
      PROCEDURE :: GETGRD
      PROCEDURE :: GETBCNAME
      PROCEDURE :: GETBCISTART
      PROCEDURE :: GETBCIEND
      PROCEDURE :: GETCONNECTDONOR
      PROCEDURE :: GETCONNECTTRANSMAT
      PROCEDURE :: GETCONNECTISTART
      PROCEDURE :: GETCONNECTIEND
      PROCEDURE :: GETCONNECTISTART_DONOR
      PROCEDURE :: GETCONNECTIEND_DONOR
  END TYPE T_GRID

  CONTAINS
    !CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
    SUBROUTINE CONSTRUCT(GRID,CONFIG)
      IMPLICIT NONE
      INCLUDE 'cgnslib_f.h'
      INCLUDE 'mpif.h'
      CLASS(T_GRID), INTENT(OUT) :: GRID
      TYPE(T_CONFIG), INTENT(IN) :: CONFIG
      CHARACTER(32) :: NODENAME,CONNECTNAME,DONORNAME
      CHARACTER(32), DIMENSION(:), ALLOCATABLE :: FAMNAME,FAMBCNAME,ZONENAME
      INTEGER :: I,J,N,M
      INTEGER :: IFILE,IER
      INTEGER :: NZONE,NFAM
      INTEGER :: NMAX(3),NSTART(3)
      INTEGER :: ISIZE(9),IPNTS(6),IPNTSDONOR(6),TRANSVEC(3) 
      INTEGER :: NFAMBC,NGEO,IBCTYPE
      INTEGER :: NORMALLIST
      INTEGER, PARAMETER :: DIM = 2
      REAL(8), DIMENSION(:,:), ALLOCATABLE :: XX,YY
      
      GRID%RANK = CONFIG%GETRANK()
      DO N = 0,CONFIG%GETSIZE()-1
        IF(N.EQ.GRID%RANK) THEN

          CALL CG_OPEN_F('./'//TRIM(CONFIG%GETNAME())//'.cgns',CG_MODE_READ,IFILE,IER)
          IF(IER.NE.CG_OK) CALL CG_ERROR_EXIT_F
          
          CALL CG_NZONES_F(IFILE,1,NZONE,IER)
          IF(NZONE.NE.CONFIG%GETSIZE()) STOP
          
          ALLOCATE(ZONENAME(NZONE))
          
          DO J= 0,NZONE-1
            CALL CG_ZONE_READ_F(IFILE,1,J+1,ZONENAME(J+1),ISIZE,IER)
            IF(J.EQ.N) THEN
              GRID%IMAX = ISIZE(1)
              GRID%JMAX = ISIZE(2)
            END IF
          END DO
          
          ALLOCATE(GRID%X(2,2:GRID%IMAX+1,2:GRID%JMAX+1))
          ALLOCATE(XX(2:GRID%IMAX+1,2:GRID%JMAX+1),YY(2:GRID%IMAX+1,2:GRID%JMAX+1))
          
          NMAX(1) = GRID%IMAX; NMAX(2) = GRID%JMAX; NMAX(3) = 0
          NSTART  = 1
          
          CALL CG_COORD_READ_F(IFILE,1,N+1,'CoordinateX',REALDOUBLE,NSTART,NMAX,XX,IER)
          CALL CG_COORD_READ_F(IFILE,1,N+1,'CoordinateY',REALDOUBLE,NSTART,NMAX,YY,IER)
          
          GRID%X(1,:,:) = XX*CONFIG%GETSCALE()
          GRID%X(2,:,:) = YY*CONFIG%GETSCALE()

          CALL CG_NFAMILIES_F(IFILE,1,NFAM,IER)
          
          ALLOCATE(FAMNAME(NFAM),FAMBCNAME(NFAM))
          
          DO I=1,NFAM
            CALL CG_FAMILY_READ_F(IFILE,1,I,FAMNAME(I),NFAMBC,NGEO,IER)
            CALL CG_FAMBC_READ_F(IFILE,1,I,1,NODENAME,IBCTYPE,IER)
            IF(TRIM(NODENAME)=='FamBC') THEN
              FAMBCNAME(I) = BCTYPENAME(IBCTYPE)
            ELSE
              FAMBCNAME(I) = '....'
            END IF 
          END DO     
          
          CALL CG_NBOCOS_F(IFILE,1,N+1,GRID%NBC,IER)
          ALLOCATE(GRID%BCINFO(GRID%NBC))
          
          DO I=1,GRID%NBC
            CALL CG_GOTO_F(IFILE,1,IER,'Zone_t',N+1,'ZoneBC_t',1,'BC_t',I,'end')
            CALL CG_FAMNAME_READ_F(NODENAME,IER)
            CALL CG_BOCO_READ_F(IFILE,1,N+1,I,IPNTS,NORMALLIST,IER)
            
            DO J=1,DIM
              IF(IPNTS(J).EQ.IPNTS(DIM+J)) THEN
                IF(IPNTS(J).EQ.1) THEN
                  SELECT CASE(J)
                  CASE(1)
                    GRID%BCINFO(I)%ISTART(1) = IPNTS(1)
                    GRID%BCINFO(I)%ISTART(2) = IPNTS(2)+1
                    GRID%BCINFO(I)%IEND(1)   = IPNTS(3)
                    GRID%BCINFO(I)%IEND(2)   = IPNTS(4)
                  CASE(2)
                    GRID%BCINFO(I)%ISTART(1) = IPNTS(1)+1
                    GRID%BCINFO(I)%ISTART(2) = IPNTS(2)
                    GRID%BCINFO(I)%IEND(1)   = IPNTS(3)
                    GRID%BCINFO(I)%IEND(2)   = IPNTS(4)
                  END SELECT
                ELSE
                  SELECT CASE(J)
                  CASE(1)
                    GRID%BCINFO(I)%ISTART(1) = IPNTS(1)+1
                    GRID%BCINFO(I)%ISTART(2) = IPNTS(2)+1
                    GRID%BCINFO(I)%IEND(1)   = IPNTS(3)+1
                    GRID%BCINFO(I)%IEND(2)   = IPNTS(4)
                  CASE(2)
                    GRID%BCINFO(I)%ISTART(1) = IPNTS(1)+1
                    GRID%BCINFO(I)%ISTART(2) = IPNTS(2)+1
                    GRID%BCINFO(I)%IEND(1)   = IPNTS(3)
                    GRID%BCINFO(I)%IEND(2)   = IPNTS(4)+1
                  END SELECT
                END IF
              END IF
            END DO
         
            DO J=1,NFAM
              IF(TRIM(NODENAME).EQ.TRIM(FAMNAME(J))) THEN
                GRID%BCINFO(I)%BCNAME = FAMBCNAME(J)
                IF(TRIM(GRID%BCINFO(I)%BCNAME).EQ.'UserDefined') THEN
                  GRID%BCINFO(I)%BCNAME = FAMNAME(J)
                END IF
              END IF
            END DO
          END DO

          CALL CG_N1TO1_F(IFILE,1,N+1,GRID%NCON,IER)
          ALLOCATE(GRID%CONNECTINFO(GRID%NCON))
          
          DO J=1,GRID%NCON
            CALL CG_1TO1_READ_F(IFILE,1,N+1,J,CONNECTNAME,DONORNAME,IPNTS,IPNTSDONOR,TRANSVEC,IER)
            
            GRID%CONNECTINFO(J)%TRANSMAT = 0
            GRID%CONNECTINFO(J)%TRANSMAT(ABS(TRANSVEC(1)),1)=TRANSVEC(1)/ABS(TRANSVEC(1))
            GRID%CONNECTINFO(J)%TRANSMAT(ABS(TRANSVEC(2)),2)=TRANSVEC(2)/ABS(TRANSVEC(2))

            DO M=1,DIM
              IF(IPNTS(M).EQ.IPNTS(DIM+M)) THEN
                IF(IPNTS(M).EQ.1) THEN
                  SELECT CASE(M)
                  CASE(1)
                    GRID%CONNECTINFO(J)%ISTART(1) = IPNTS(1)+1
                    GRID%CONNECTINFO(J)%ISTART(2) = IPNTS(2)
                    GRID%CONNECTINFO(J)%IEND(1)   = IPNTS(3)+1
                    GRID%CONNECTINFO(J)%IEND(2)   = IPNTS(4)
                  CASE(2)
                    GRID%CONNECTINFO(J)%ISTART(1) = IPNTS(1)
                    GRID%CONNECTINFO(J)%ISTART(2) = IPNTS(2)+1
                    GRID%CONNECTINFO(J)%IEND(1)   = IPNTS(3)
                    GRID%CONNECTINFO(J)%IEND(2)   = IPNTS(4)+1
                  END SELECT
                ELSE
                  GRID%CONNECTINFO(J)%ISTART(1) = IPNTS(1)
                  GRID%CONNECTINFO(J)%ISTART(2) = IPNTS(2)
                  GRID%CONNECTINFO(J)%IEND(1)   = IPNTS(3)
                  GRID%CONNECTINFO(J)%IEND(2)   = IPNTS(4)
                END IF
              END IF
              IF(IPNTSDONOR(M).EQ.IPNTSDONOR(DIM+M)) THEN
                IF(IPNTSDONOR(M).EQ.1) THEN
                  GRID%CONNECTINFO(J)%ISTART_DONOR(1) = IPNTSDONOR(1)
                  GRID%CONNECTINFO(J)%ISTART_DONOR(2) = IPNTSDONOR(2)
                  GRID%CONNECTINFO(J)%IEND_DONOR(1)   = IPNTSDONOR(3)
                  GRID%CONNECTINFO(J)%IEND_DONOR(2)   = IPNTSDONOR(4)
                ELSE
                  SELECT CASE(M)
                  CASE(1)
                    GRID%CONNECTINFO(J)%ISTART_DONOR(1) = IPNTSDONOR(1)+1
                    GRID%CONNECTINFO(J)%ISTART_DONOR(2) = IPNTSDONOR(2)
                    GRID%CONNECTINFO(J)%IEND_DONOR(1)   = IPNTSDONOR(3)+1
                    GRID%CONNECTINFO(J)%IEND_DONOR(2)   = IPNTSDONOR(4)
                  CASE(2)
                    GRID%CONNECTINFO(J)%ISTART_DONOR(1) = IPNTSDONOR(1)
                    GRID%CONNECTINFO(J)%ISTART_DONOR(2) = IPNTSDONOR(2)+1
                    GRID%CONNECTINFO(J)%IEND_DONOR(1)   = IPNTSDONOR(3)
                    GRID%CONNECTINFO(J)%IEND_DONOR(2)   = IPNTSDONOR(4)+1
                  END SELECT
                END IF
              END IF
            END DO

            DO I=1,NZONE
              IF(TRIM(DONORNAME).EQ.TRIM(ZONENAME(I))) THEN
                GRID%CONNECTINFO(J)%DONOR = I-1
              END IF
            END DO
          END DO  
          
          CALL CG_CLOSE_F(IFILE,IER)
        END IF
        CALL MPI_BARRIER(MPI_COMM_WORLD,IER)
      END DO
            
      IF(ALLOCATED(ZONENAME)) DEALLOCATE(ZONENAME)
      IF(ALLOCATED(FAMNAME)) DEALLOCATE(FAMNAME)
      IF(ALLOCATED(FAMBCNAME)) DEALLOCATE(FAMBCNAME)
      IF(ALLOCATED(XX)) DEALLOCATE(XX)
      IF(ALLOCATED(YY)) DEALLOCATE(YY)

      
      SELECT CASE(CONFIG%GETITURB())
      CASE(-3,-2)
        GRID%NGRD = 3
      CASE(-1,0)
        GRID%NGRD = 4
      END SELECT
      
      ALLOCATE(GRID%GRD(GRID%NGRD,GRID%IMAX+1,GRID%JMAX+1))
      
      CALL GRID%CALNORMAL() !! MUST BE CALLED BEFORE CALVOLUME !!
      CALL GRID%CALVOLUME() 
      IF(CONFIG%GETITURB().GT.-2) CALL GRID%CALYDNS(CONFIG) ! IN CASE OF TURBULENT, YDNS MUST BE CALCULATED !!
      
    END SUBROUTINE CONSTRUCT
    !CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
    SUBROUTINE DESTRUCT(GRID)
      IMPLICIT NONE
      CLASS(T_GRID), INTENT(INOUT) :: GRID
      
      IF(ALLOCATED(GRID%X))           DEALLOCATE(GRID%X)
      IF(ALLOCATED(GRID%CX))          DEALLOCATE(GRID%CX)
      IF(ALLOCATED(GRID%EX))          DEALLOCATE(GRID%EX)
      IF(ALLOCATED(GRID%GRD))         DEALLOCATE(GRID%GRD)
      IF(ALLOCATED(GRID%BCINFO))      DEALLOCATE(GRID%BCINFO)
      IF(ALLOCATED(GRID%CONNECTINFO)) DEALLOCATE(GRID%CONNECTINFO)
      
      
    END SUBROUTINE DESTRUCT
    !CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
    SUBROUTINE CALYDNS(GRID,CONFIG)
      IMPLICIT NONE
      INCLUDE 'mpif.h'
      CLASS(T_GRID), INTENT(INOUT) :: GRID
      TYPE(T_CONFIG), INTENT(IN) :: CONFIG
      INTEGER :: M,N,I,J,L,II,JJ,IS,IE,JS,JE
      INTEGER :: NUM
      INTEGER :: SENDNUM,RECVNUM
      INTEGER :: IER,REQUEST1,REQUEST2
      INTEGER :: STATUS(MPI_STATUS_SIZE)
      INTEGER :: RECVCOUNT(0:CONFIG%GETSIZE()-1),DISP(0:CONFIG%GETSIZE()-1)
      INTEGER, PARAMETER :: DIM = 2
      REAL(8), DIMENSION(:), ALLOCATABLE :: SENDXB,SENDYB,RECVXB,RECVYB,SENDBUF,RECVBUF
      REAL(8) :: XC,YC,YMIN,YTEMP
     
      SENDNUM = 0
     
      DO N = 1,GRID%NBC
        IF(TRIM(GRID%BCINFO(N)%BCNAME).EQ.'BCWallViscous') THEN
          IF(GRID%BCINFO(N)%ISTART(1).EQ.GRID%BCINFO(N)%IEND(1)) THEN
            SENDNUM = SENDNUM + (GRID%BCINFO(N)%IEND(2)-GRID%BCINFO(N)%ISTART(2)+1)
          END IF
          IF(GRID%BCINFO(N)%ISTART(2).EQ.GRID%BCINFO(N)%IEND(2)) THEN
            SENDNUM = SENDNUM + (GRID%BCINFO(N)%IEND(1)-GRID%BCINFO(N)%ISTART(1)+1)
          END IF          
        END IF
      END DO
      
      ALLOCATE(SENDXB(SENDNUM),SENDYB(SENDNUM))
      M=0
      DO N = 1,GRID%NBC
        IF(TRIM(GRID%BCINFO(N)%BCNAME).EQ.'BCWallViscous') THEN
          IF(GRID%BCINFO(N)%ISTART(1).EQ.GRID%BCINFO(N)%IEND(1)) THEN
            IF(GRID%BCINFO(N)%ISTART(1).EQ.1) THEN
              DO J = GRID%BCINFO(N)%ISTART(2),GRID%BCINFO(N)%IEND(2)
                M = M + 1
                SENDXB(M) = 0.5D0*(GRID%X(1,2,J)+GRID%X(1,2,J+1))
                SENDYB(M) = 0.5D0*(GRID%X(2,2,J)+GRID%X(2,2,J+1))
              END DO            
            ELSE
              DO J = GRID%BCINFO(N)%ISTART(2),GRID%BCINFO(N)%IEND(2)
                M = M + 1
                SENDXB(M) = 0.5D0*(GRID%X(1,GRID%IMAX+1,J)+GRID%X(1,GRID%IMAX+1,J+1))
                SENDYB(M) = 0.5D0*(GRID%X(2,GRID%IMAX+1,J)+GRID%X(2,GRID%IMAX+1,J+1))
              END DO             
            END IF
          END IF
          IF(GRID%BCINFO(N)%ISTART(2).EQ.GRID%BCINFO(N)%IEND(2)) THEN
            IF(GRID%BCINFO(N)%ISTART(2).EQ.1) THEN
              DO I = GRID%BCINFO(N)%ISTART(1),GRID%BCINFO(N)%IEND(1)
                M = M + 1
                SENDXB(M) = 0.5D0*(GRID%X(1,I,2)+GRID%X(1,I+1,2))
                SENDYB(M) = 0.5D0*(GRID%X(2,I,2)+GRID%X(2,I+1,2))
              END DO            
            ELSE
              DO I = GRID%BCINFO(N)%ISTART(1),GRID%BCINFO(N)%IEND(1)
                M = M + 1
                SENDXB(M) = 0.5D0*(GRID%X(1,I,GRID%JMAX+1)+GRID%X(1,I+1,GRID%JMAX+1))
                SENDYB(M) = 0.5D0*(GRID%X(2,I,GRID%JMAX+1)+GRID%X(2,I+1,GRID%JMAX+1))
              END DO             
            END IF
          END IF          
        END IF
      END DO
      
      CALL MPI_ALLGATHER(SENDNUM,1,MPI_INTEGER4,RECVCOUNT,1,MPI_INTEGER4,MPI_COMM_WORLD,IER)
      
      DISP(0) = 0
      DO M=1,CONFIG%GETSIZE()-1
        DISP(M) = DISP(M-1) + RECVCOUNT(M-1)
      END DO
      
      RECVNUM = 0
      DO M=0,CONFIG%GETSIZE()-1
        RECVNUM = RECVNUM + RECVCOUNT(M)
      END DO
      
      ALLOCATE(RECVXB(RECVNUM),RECVYB(RECVNUM))

      CALL MPI_ALLGATHERV(SENDXB,SENDNUM,MPI_REAL8,RECVXB,RECVCOUNT,DISP,MPI_REAL8,MPI_COMM_WORLD,IER)
      CALL MPI_ALLGATHERV(SENDYB,SENDNUM,MPI_REAL8,RECVYB,RECVCOUNT,DISP,MPI_REAL8,MPI_COMM_WORLD,IER)
      
      DO J=2,GRID%JMAX
        DO I=2,GRID%IMAX
          XC = 0.25D0*(GRID%X(1,I,J)+GRID%X(1,I+1,J)+GRID%X(1,I,J+1)+GRID%X(1,I+1,J+1))
          YC = 0.25D0*(GRID%X(2,I,J)+GRID%X(2,I+1,J)+GRID%X(2,I,J+1)+GRID%X(2,I+1,J+1))
          YMIN = 1.D6
          DO M =1,RECVNUM
            YTEMP = DSQRT((XC-RECVXB(M))**2+(YC-RECVYB(M))**2)
            IF(YTEMP.LT.YMIN) THEN
              YMIN = YTEMP
            END IF
          END DO
          GRID%GRD(4,I,J) = YMIN
        END DO
      END DO
      
      DEALLOCATE(SENDXB,SENDYB,RECVXB,RECVYB)
      
      DO I=2,GRID%IMAX
        GRID%GRD(4,I,1)      = GRID%GRD(4,I,2)
        GRID%GRD(4,I,GRID%JMAX+1) = GRID%GRD(4,I,GRID%JMAX)
      END DO
      DO J=1,GRID%JMAX+1
        GRID%GRD(4,1,J)      = GRID%GRD(4,2,J)
        GRID%GRD(4,GRID%IMAX+1,J) = GRID%GRD(4,GRID%IMAX,J)
      END DO

      DO N=1,GRID%NCON
        NUM = (ABS(GRID%CONNECTINFO(N)%ISTART(2)-GRID%CONNECTINFO(N)%IEND(2))+1)*(ABS(GRID%CONNECTINFO(N)%ISTART(1)-GRID%CONNECTINFO(N)%IEND(1))+1)
        ALLOCATE(SENDBUF(NUM),RECVBUF(NUM))
        
        L = 0
        DO J=GRID%CONNECTINFO(N)%ISTART(2),GRID%CONNECTINFO(N)%IEND(2)
          DO I=GRID%CONNECTINFO(N)%ISTART(1),GRID%CONNECTINFO(N)%IEND(1)
            L = L + 1
            SENDBUF(L) = GRID%GRD(4,I,J)
          END DO
        END DO
        
        IF(GRID%RANK.NE.GRID%CONNECTINFO(N)%DONOR) THEN 
          
          CALL MPI_ISEND(SENDBUF,NUM,MPI_REAL8,GRID%CONNECTINFO(N)%DONOR,GRID%RANK,MPI_COMM_WORLD,REQUEST1,IER)
          CALL MPI_IRECV(RECVBUF,NUM,MPI_REAL8,GRID%CONNECTINFO(N)%DONOR,GRID%CONNECTINFO(N)%DONOR,MPI_COMM_WORLD,REQUEST2,IER)
          CALL MPI_WAIT(REQUEST1,STATUS,IER)
          CALL MPI_WAIT(REQUEST2,STATUS,IER)
          
          IS = GRID%CONNECTINFO(N)%ISTART_DONOR(1)
          IE = GRID%CONNECTINFO(N)%IEND_DONOR(1)
          JS = GRID%CONNECTINFO(N)%ISTART_DONOR(2)
          JE = GRID%CONNECTINFO(N)%IEND_DONOR(2)
    
          IF(GRID%CONNECTINFO(N)%ISTART_DONOR(2).GT.GRID%CONNECTINFO(N)%IEND_DONOR(2)) THEN
            JS = GRID%CONNECTINFO(N)%IEND_DONOR(2)
            JE = GRID%CONNECTINFO(N)%ISTART_DONOR(2)
          END IF
          
          IF(GRID%CONNECTINFO(N)%ISTART_DONOR(1).GT.GRID%CONNECTINFO(N)%IEND_DONOR(1)) THEN
            IS = GRID%CONNECTINFO(N)%IEND_DONOR(1)
            IE = GRID%CONNECTINFO(N)%ISTART_DONOR(1)
          END IF         
          
          L = 0
          DO J=JS,JE
            DO I=IS,IE
              II = GRID%CONNECTINFO(N)%TRANSMAT(1,1)*(I-GRID%CONNECTINFO(N)%ISTART_DONOR(1))+GRID%CONNECTINFO(N)%TRANSMAT(2,1)*(J-GRID%CONNECTINFO(N)%ISTART_DONOR(2))+GRID%CONNECTINFO(N)%ISTART(1)
              JJ = GRID%CONNECTINFO(N)%TRANSMAT(1,2)*(I-GRID%CONNECTINFO(N)%ISTART_DONOR(1))+GRID%CONNECTINFO(N)%TRANSMAT(2,2)*(J-GRID%CONNECTINFO(N)%ISTART_DONOR(2))+GRID%CONNECTINFO(N)%ISTART(2)
              L = L + 1
              GRID%GRD(4,II,JJ) = RECVBUF(L)
            END DO
          END DO
        ELSE ! NO MPI BOUNDARY
          
          RECVBUF = SENDBUF
          
          L = 0
          DO J=GRID%CONNECTINFO(N)%ISTART(2),GRID%CONNECTINFO(N)%IEND(2)
            DO I=GRID%CONNECTINFO(N)%ISTART(1),GRID%CONNECTINFO(N)%IEND(1)
              II = GRID%CONNECTINFO(N)%TRANSMAT(1,1)*(I-GRID%CONNECTINFO(N)%ISTART(1))+GRID%CONNECTINFO(N)%TRANSMAT(1,2)*(J-GRID%CONNECTINFO(N)%ISTART(2))+GRID%CONNECTINFO(N)%ISTART_DONOR(1)
              JJ = GRID%CONNECTINFO(N)%TRANSMAT(2,1)*(I-GRID%CONNECTINFO(N)%ISTART(1))+GRID%CONNECTINFO(N)%TRANSMAT(2,2)*(J-GRID%CONNECTINFO(N)%ISTART(2))+GRID%CONNECTINFO(N)%ISTART_DONOR(2)
              L = L + 1
              GRID%GRD(4,II,JJ) = RECVBUF(L)
            END DO
          END DO
          
        END IF
        DEALLOCATE(SENDBUF,RECVBUF)
      END DO
                
    END SUBROUTINE CALYDNS
    !CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
    SUBROUTINE CALNORMAL(GRID)
      IMPLICIT NONE
      INCLUDE 'mpif.h'
      CLASS(T_GRID), INTENT(INOUT) :: GRID
      INTEGER :: I,J,N,M,L,II,JJ,IS,IE,JS,JE
      INTEGER :: NUM,SIGN
      INTEGER :: IER,REQUEST1,REQUEST2
      INTEGER :: STATUS(MPI_STATUS_SIZE)
      INTEGER, PARAMETER :: DIM = 2
      REAL(8), DIMENSION(:), ALLOCATABLE :: SENDBUF,RECVBUF
      
      ALLOCATE(GRID%CX(2,1:GRID%IMAX,1:GRID%JMAX+1),GRID%EX(2,1:GRID%IMAX+1,1:GRID%JMAX))
      
      DO J=2,GRID%JMAX
        DO I=1,GRID%IMAX
          GRID%CX(1,I,J) = GRID%X(2,I+1,J+1) - GRID%X(2,I+1,J)
          GRID%CX(2,I,J) = - GRID%X(1,I+1,J+1) + GRID%X(1,I+1,J)
        END DO
      END DO

      DO I=2,GRID%IMAX
        DO J=1,GRID%JMAX
          GRID%EX(1,I,J) = - GRID%X(2,I+1,J+1) + GRID%X(2,I,J+1)
          GRID%EX(2,I,J) = GRID%X(1,I+1,J+1) - GRID%X(1,I,J+1)
        END DO
      END DO
      
      DO N = 1,GRID%NBC
        IF(TRIM(GRID%BCINFO(N)%BCNAME).EQ.'DegeneratePoint') THEN
          IF(GRID%BCINFO(N)%ISTART(1).EQ.GRID%BCINFO(N)%IEND(1)) THEN
            IF(GRID%BCINFO(N)%ISTART(1).EQ.1) THEN
              GRID%CX(1,1,:) = 0.D0
              GRID%CX(2,1,:) = 0.D0         
            ELSE
              GRID%CX(1,GRID%IMAX,:) = 0.D0
              GRID%CX(2,GRID%IMAX,:) = 0.D0           
            END IF
          END IF
          IF(GRID%BCINFO(N)%ISTART(2).EQ.GRID%BCINFO(N)%IEND(2)) THEN
            IF(GRID%BCINFO(N)%ISTART(2).EQ.1) THEN
              GRID%EX(1,:,1) = 0.D0
              GRID%EX(2,:,1) = 0.D0           
            ELSE
              GRID%EX(1,:,GRID%JMAX) = 0.D0
              GRID%EX(2,:,GRID%JMAX) = 0.D0            
            END IF
          END IF            
        END IF
      END DO
      
      GRID%CX(:,:,1) = GRID%CX(:,:,2)
      GRID%CX(:,:,GRID%JMAX+1) = GRID%CX(:,:,GRID%JMAX)
      GRID%EX(:,1,:) = GRID%EX(:,2,:)
      GRID%EX(:,GRID%IMAX+1,:) = GRID%EX(:,GRID%IMAX,:)
      
        
      DO N=1,GRID%NCON
        NUM = 2*(ABS(GRID%CONNECTINFO(N)%ISTART(2)-GRID%CONNECTINFO(N)%IEND(2))+1)*(ABS(GRID%CONNECTINFO(N)%ISTART(1)-GRID%CONNECTINFO(N)%IEND(1))+1)
        ALLOCATE(SENDBUF(NUM),RECVBUF(NUM))
        
        L = 0
        DO J=GRID%CONNECTINFO(N)%ISTART(2),GRID%CONNECTINFO(N)%IEND(2)
          DO I=GRID%CONNECTINFO(N)%ISTART(1),GRID%CONNECTINFO(N)%IEND(1)
            DO M=1,2
              L = L + 1
              IF(GRID%CONNECTINFO(N)%ISTART(2).EQ.GRID%CONNECTINFO(N)%IEND(2)) THEN
                SENDBUF(L) = GRID%CX(M,I,J)
              ELSE IF(GRID%CONNECTINFO(N)%ISTART(1).EQ.GRID%CONNECTINFO(N)%IEND(1)) THEN
                SENDBUF(L) = GRID%EX(M,I,J)         
              END IF
            END DO
          END DO
        END DO
        
        IF(GRID%RANK.NE.GRID%CONNECTINFO(N)%DONOR) THEN 
       
          CALL MPI_ISEND(SENDBUF,NUM,MPI_REAL8,GRID%CONNECTINFO(N)%DONOR,GRID%RANK,MPI_COMM_WORLD,REQUEST1,IER)
          CALL MPI_IRECV(RECVBUF,NUM,MPI_REAL8,GRID%CONNECTINFO(N)%DONOR,GRID%CONNECTINFO(N)%DONOR,MPI_COMM_WORLD,REQUEST2,IER)
          CALL MPI_WAIT(REQUEST1,STATUS,IER)
          CALL MPI_WAIT(REQUEST2,STATUS,IER)
          
          SIGN = 1
          IS = GRID%CONNECTINFO(N)%ISTART_DONOR(1)
          IE = GRID%CONNECTINFO(N)%IEND_DONOR(1)
          JS = GRID%CONNECTINFO(N)%ISTART_DONOR(2)
          JE = GRID%CONNECTINFO(N)%IEND_DONOR(2)
    
          IF(GRID%CONNECTINFO(N)%ISTART_DONOR(2).GT.GRID%CONNECTINFO(N)%IEND_DONOR(2)) THEN
            JS = GRID%CONNECTINFO(N)%IEND_DONOR(2)
            JE = GRID%CONNECTINFO(N)%ISTART_DONOR(2)
            SIGN = -1*SIGN
          END IF
          
          IF(GRID%CONNECTINFO(N)%ISTART_DONOR(1).GT.GRID%CONNECTINFO(N)%IEND_DONOR(1)) THEN
            IS = GRID%CONNECTINFO(N)%IEND_DONOR(1)
            IE = GRID%CONNECTINFO(N)%ISTART_DONOR(1)
            SIGN = -1*SIGN
          END IF        
          
          L = 0
          DO J=JS,JE
            DO I=IS,IE
              II = GRID%CONNECTINFO(N)%TRANSMAT(1,1)*(I-GRID%CONNECTINFO(N)%ISTART_DONOR(1))+GRID%CONNECTINFO(N)%TRANSMAT(2,1)*(J-GRID%CONNECTINFO(N)%ISTART_DONOR(2))+GRID%CONNECTINFO(N)%ISTART(1)
              JJ = GRID%CONNECTINFO(N)%TRANSMAT(1,2)*(I-GRID%CONNECTINFO(N)%ISTART_DONOR(1))+GRID%CONNECTINFO(N)%TRANSMAT(2,2)*(J-GRID%CONNECTINFO(N)%ISTART_DONOR(2))+GRID%CONNECTINFO(N)%ISTART(2)
              DO M=1,2
                L = L + 1
                IF(GRID%CONNECTINFO(N)%ISTART(2).EQ.GRID%CONNECTINFO(N)%IEND(2)) THEN
                  GRID%CX(M,II,JJ) = SIGN*RECVBUF(L)
                ELSE IF(GRID%CONNECTINFO(N)%ISTART(1).EQ.GRID%CONNECTINFO(N)%IEND(1)) THEN
                  GRID%EX(M,II,JJ) = SIGN*RECVBUF(L)
                END IF
              END DO
            END DO
          END DO
        ELSE ! NO MPI BOUNDARY
          RECVBUF = SENDBUF
          
          SIGN = 1
          IF(GRID%CONNECTINFO(N)%ISTART_DONOR(2).GT.GRID%CONNECTINFO(N)%IEND_DONOR(2)) THEN
            SIGN = -1*SIGN
          END IF
          
          IF(GRID%CONNECTINFO(N)%ISTART_DONOR(1).GT.GRID%CONNECTINFO(N)%IEND_DONOR(1)) THEN
            SIGN = -1*SIGN
          END IF
          
          L = 0
          DO J=GRID%CONNECTINFO(N)%ISTART(2),GRID%CONNECTINFO(N)%IEND(2)
            DO I=GRID%CONNECTINFO(N)%ISTART(1),GRID%CONNECTINFO(N)%IEND(1)
              II = GRID%CONNECTINFO(N)%TRANSMAT(1,1)*(I-GRID%CONNECTINFO(N)%ISTART(1))+GRID%CONNECTINFO(N)%TRANSMAT(1,2)*(J-GRID%CONNECTINFO(N)%ISTART(2))+GRID%CONNECTINFO(N)%ISTART_DONOR(1)
              JJ = GRID%CONNECTINFO(N)%TRANSMAT(2,1)*(I-GRID%CONNECTINFO(N)%ISTART(1))+GRID%CONNECTINFO(N)%TRANSMAT(2,2)*(J-GRID%CONNECTINFO(N)%ISTART(2))+GRID%CONNECTINFO(N)%ISTART_DONOR(2)
              DO M=1,2
                L = L + 1
                IF(GRID%CONNECTINFO(N)%ISTART_DONOR(2).EQ.GRID%CONNECTINFO(N)%IEND_DONOR(2)) THEN
                  GRID%CX(M,II,JJ) = SIGN*RECVBUF(L)
                ELSE IF(GRID%CONNECTINFO(N)%ISTART_DONOR(1).EQ.GRID%CONNECTINFO(N)%IEND_DONOR(1)) THEN
                  GRID%EX(M,II,JJ) = SIGN*RECVBUF(L)           
                END IF
              END DO
            END DO
          END DO
        END IF
        DEALLOCATE(SENDBUF,RECVBUF)
      END DO
      
      
      ! REODERING
      DO N=1,GRID%NCON
        DO M=1,DIM
          IF(GRID%CONNECTINFO(N)%ISTART(M).NE.GRID%CONNECTINFO(N)%IEND(M) ) THEN
            GRID%CONNECTINFO(N)%ISTART(M) = GRID%CONNECTINFO(N)%ISTART(M) + 1            
          END IF

          IF(GRID%CONNECTINFO(N)%ISTART_DONOR(M).NE.GRID%CONNECTINFO(N)%IEND_DONOR(M) ) THEN
            IF(GRID%CONNECTINFO(N)%ISTART_DONOR(M).LT.GRID%CONNECTINFO(N)%IEND_DONOR(M) ) THEN
              GRID%CONNECTINFO(N)%ISTART_DONOR(M) = GRID%CONNECTINFO(N)%ISTART_DONOR(M) + 1
            ELSE IF(GRID%CONNECTINFO(N)%ISTART_DONOR(M).GT.GRID%CONNECTINFO(N)%IEND_DONOR(M) ) THEN
              GRID%CONNECTINFO(N)%IEND_DONOR(M) = GRID%CONNECTINFO(N)%IEND_DONOR(M) + 1            
            END IF
          END IF
        END DO
      END DO
      
    END SUBROUTINE CALNORMAL
    !CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
    SUBROUTINE CALVOLUME(GRID)
      IMPLICIT NONE
      INCLUDE 'mpif.h'
      CLASS(T_GRID), INTENT(INOUT) :: GRID
      INTEGER :: I,J,N,L,II,JJ,IS,IE,JS,JE
      REAL(8) :: XS,YS,XE,YE
      INTEGER :: NUM
      INTEGER :: IER,REQUEST1,REQUEST2
      INTEGER :: STATUS(MPI_STATUS_SIZE)
      REAL(8), DIMENSION(:), ALLOCATABLE :: SENDBUF,RECVBUF 
      
      DO J=2,GRID%JMAX
        DO I=2,GRID%IMAX
          XS = (GRID%X(1,I+1,J)+GRID%X(1,I+1,J+1)-GRID%X(1,I,J)-GRID%X(1,I,J+1))*0.5D0
          YS = (GRID%X(2,I+1,J)+GRID%X(2,I+1,J+1)-GRID%X(2,I,J)-GRID%X(2,I,J+1))*0.5D0
          XE = (GRID%X(1,I,J+1)+GRID%X(1,I+1,J+1)-GRID%X(1,I,J)-GRID%X(1,I+1,J))*0.5D0
          YE = (GRID%X(2,I,J+1)+GRID%X(2,I+1,J+1)-GRID%X(2,I,J)-GRID%X(2,I+1,J))*0.5D0
          GRID%GRD(1,I,J)=(XS*YE-XE*YS)
        
          IF(GRID%GRD(1,I,J).LT.0.D0) WRITE(*,*) 'NEGATIVE VOLIAN',I,J,GRID%GRD(1,I,J)
        
          GRID%GRD(2,I,J) = 0.25D0*(GRID%X(1,I,J)+GRID%X(1,I+1,J)+GRID%X(1,I,J+1)+GRID%X(1,I+1,J+1))
          GRID%GRD(3,I,J) = 0.25D0*(GRID%X(2,I,J)+GRID%X(2,I+1,J)+GRID%X(2,I,J+1)+GRID%X(2,I+1,J+1))        
        END DO
      END DO


      DO I=2,GRID%IMAX
        GRID%GRD(1,I,1) = GRID%GRD(1,I,2)
        GRID%GRD(2,I,1) = 2.D0*GRID%GRD(2,I,2) - GRID%GRD(2,I,3)
        GRID%GRD(3,I,1) = 2.D0*GRID%GRD(3,I,2) - GRID%GRD(3,I,3)
        
        GRID%GRD(1,I,GRID%JMAX+1) = GRID%GRD(1,I,GRID%JMAX)
        GRID%GRD(2,I,GRID%JMAX+1) = 2.D0*GRID%GRD(2,I,GRID%JMAX) - GRID%GRD(2,I,GRID%JMAX-1)
        GRID%GRD(3,I,GRID%JMAX+1) = 2.D0*GRID%GRD(3,I,GRID%JMAX) - GRID%GRD(3,I,GRID%JMAX-1)
      END DO

      DO J=1,GRID%JMAX+1
        GRID%GRD(1,1,J) = GRID%GRD(1,2,J)
        GRID%GRD(2,1,J) = 2.D0*GRID%GRD(2,2,J) - GRID%GRD(2,3,J)
        GRID%GRD(3,1,J) = 2.D0*GRID%GRD(3,2,J) - GRID%GRD(3,3,J)

        GRID%GRD(1,GRID%IMAX+1,J) = GRID%GRD(1,GRID%IMAX,J)
        GRID%GRD(2,GRID%IMAX+1,J) = 2.D0*GRID%GRD(2,GRID%IMAX,J) - GRID%GRD(2,GRID%IMAX-1,J)
        GRID%GRD(3,GRID%IMAX+1,J) = 2.D0*GRID%GRD(3,GRID%IMAX,J) - GRID%GRD(3,GRID%IMAX-1,J)
      END DO

      DO N=1,GRID%NCON
        NUM = (ABS(GRID%CONNECTINFO(N)%ISTART(2)-GRID%CONNECTINFO(N)%IEND(2))+1)*(ABS(GRID%CONNECTINFO(N)%ISTART(1)-GRID%CONNECTINFO(N)%IEND(1))+1)
        ALLOCATE(SENDBUF(NUM),RECVBUF(NUM))
        
        L = 0
        DO J=GRID%CONNECTINFO(N)%ISTART(2),GRID%CONNECTINFO(N)%IEND(2)
          DO I=GRID%CONNECTINFO(N)%ISTART(1),GRID%CONNECTINFO(N)%IEND(1)
            L = L + 1
            SENDBUF(L) = GRID%GRD(1,I,J)
          END DO
        END DO
        
        IF(GRID%RANK.NE.GRID%CONNECTINFO(N)%DONOR) THEN 
          
          CALL MPI_ISEND(SENDBUF,NUM,MPI_REAL8,GRID%CONNECTINFO(N)%DONOR,GRID%RANK,MPI_COMM_WORLD,REQUEST1,IER)
          CALL MPI_IRECV(RECVBUF,NUM,MPI_REAL8,GRID%CONNECTINFO(N)%DONOR,GRID%CONNECTINFO(N)%DONOR,MPI_COMM_WORLD,REQUEST2,IER)
          CALL MPI_WAIT(REQUEST1,STATUS,IER)
          CALL MPI_WAIT(REQUEST2,STATUS,IER)
          
          IS = GRID%CONNECTINFO(N)%ISTART_DONOR(1)
          IE = GRID%CONNECTINFO(N)%IEND_DONOR(1)
          JS = GRID%CONNECTINFO(N)%ISTART_DONOR(2)
          JE = GRID%CONNECTINFO(N)%IEND_DONOR(2)
    
          IF(GRID%CONNECTINFO(N)%ISTART_DONOR(2).GT.GRID%CONNECTINFO(N)%IEND_DONOR(2)) THEN
            JS = GRID%CONNECTINFO(N)%IEND_DONOR(2)
            JE = GRID%CONNECTINFO(N)%ISTART_DONOR(2)
          END IF
          
          IF(GRID%CONNECTINFO(N)%ISTART_DONOR(1).GT.GRID%CONNECTINFO(N)%IEND_DONOR(1)) THEN
            IS = GRID%CONNECTINFO(N)%IEND_DONOR(1)
            IE = GRID%CONNECTINFO(N)%ISTART_DONOR(1)
          END IF         
          
          L = 0
          DO J=JS,JE
            DO I=IS,IE
              II = GRID%CONNECTINFO(N)%TRANSMAT(1,1)*(I-GRID%CONNECTINFO(N)%ISTART_DONOR(1))+GRID%CONNECTINFO(N)%TRANSMAT(2,1)*(J-GRID%CONNECTINFO(N)%ISTART_DONOR(2))+GRID%CONNECTINFO(N)%ISTART(1)
              JJ = GRID%CONNECTINFO(N)%TRANSMAT(1,2)*(I-GRID%CONNECTINFO(N)%ISTART_DONOR(1))+GRID%CONNECTINFO(N)%TRANSMAT(2,2)*(J-GRID%CONNECTINFO(N)%ISTART_DONOR(2))+GRID%CONNECTINFO(N)%ISTART(2)
              L = L + 1
              GRID%GRD(1,II,JJ) = RECVBUF(L)
            END DO
          END DO
        ELSE ! NO MPI BOUNDARY
          
          RECVBUF = SENDBUF
          
          L = 0
          DO J=GRID%CONNECTINFO(N)%ISTART(2),GRID%CONNECTINFO(N)%IEND(2)
            DO I=GRID%CONNECTINFO(N)%ISTART(1),GRID%CONNECTINFO(N)%IEND(1)
              II = GRID%CONNECTINFO(N)%TRANSMAT(1,1)*(I-GRID%CONNECTINFO(N)%ISTART(1))+GRID%CONNECTINFO(N)%TRANSMAT(1,2)*(J-GRID%CONNECTINFO(N)%ISTART(2))+GRID%CONNECTINFO(N)%ISTART_DONOR(1)
              JJ = GRID%CONNECTINFO(N)%TRANSMAT(2,1)*(I-GRID%CONNECTINFO(N)%ISTART(1))+GRID%CONNECTINFO(N)%TRANSMAT(2,2)*(J-GRID%CONNECTINFO(N)%ISTART(2))+GRID%CONNECTINFO(N)%ISTART_DONOR(2)
              L = L + 1
              GRID%GRD(1,II,JJ) = RECVBUF(L)
            END DO
          END DO
          
        END IF
        DEALLOCATE(SENDBUF,RECVBUF)
      END DO
      
    END SUBROUTINE CALVOLUME
    !CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
    FUNCTION GETIMAX(GRID)
      IMPLICIT NONE
      CLASS(T_GRID), INTENT(IN) :: GRID
      INTEGER :: GETIMAX
      
      GETIMAX = GRID%IMAX
    END FUNCTION GETIMAX
    !CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
    FUNCTION GETJMAX(GRID)
      IMPLICIT NONE
      CLASS(T_GRID), INTENT(IN) :: GRID
      INTEGER :: GETJMAX
      
      GETJMAX = GRID%JMAX
    END FUNCTION GETJMAX
    !CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC     
    FUNCTION GETNBC(GRID)
      IMPLICIT NONE
      CLASS(T_GRID), INTENT(IN) :: GRID
      INTEGER :: GETNBC
      
      GETNBC = GRID%NBC
    END FUNCTION GETNBC
    !CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC     
    FUNCTION GETNCON(GRID)
      IMPLICIT NONE
      CLASS(T_GRID), INTENT(IN) :: GRID
      INTEGER :: GETNCON
      
      GETNCON = GRID%NCON
    END FUNCTION GETNCON
    !CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
    FUNCTION GETX(GRID,I,J)
      IMPLICIT NONE
      CLASS(T_GRID), INTENT(IN) :: GRID
      REAL(8) :: GETX(2)
      INTEGER, INTENT(IN) :: I,J
      
      GETX = GRID%X(:,I,J)
    END FUNCTION GETX
    !CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
    FUNCTION GETCX(GRID,I,J)
      IMPLICIT NONE
      CLASS(T_GRID), INTENT(IN) :: GRID
      REAL(8) :: GETCX(2)
      INTEGER, INTENT(IN) :: I,J
      
      GETCX = GRID%CX(:,I,J)
    END FUNCTION GETCX
    !CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
    FUNCTION GETEX(GRID,I,J)
      IMPLICIT NONE
      CLASS(T_GRID), INTENT(IN) :: GRID
      REAL(8) :: GETEX(2)
      INTEGER, INTENT(IN) :: I,J
      
      GETEX = GRID%EX(:,I,J)
    END FUNCTION GETEX
    !CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
    FUNCTION GETNGRD(GRID)
      IMPLICIT NONE
      CLASS(T_GRID), INTENT(IN) :: GRID
      INTEGER :: GETNGRD
      
      GETNGRD = GRID%NGRD
    END FUNCTION GETNGRD
    !CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
    FUNCTION GETGRD(GRID,I,J)
      IMPLICIT NONE
      CLASS(T_GRID), INTENT(IN) :: GRID
      REAL(8) :: GETGRD(GRID%NGRD)
      INTEGER, INTENT(IN) :: I,J
      
      GETGRD = GRID%GRD(:,I,J)
    END FUNCTION GETGRD
    !CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
    FUNCTION GETBCNAME(GRID,I)
      IMPLICIT NONE
      CLASS(T_GRID), INTENT(IN) :: GRID
      INTEGER, INTENT(IN) :: I
      CHARACTER(32) :: GETBCNAME
      
      GETBCNAME = GRID%BCINFO(I)%BCNAME
    END FUNCTION GETBCNAME
    !CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
    FUNCTION GETBCISTART(GRID,I,J)
      IMPLICIT NONE
      CLASS(T_GRID), INTENT(IN) :: GRID
      INTEGER, INTENT(IN) :: I,J
      INTEGER :: GETBCISTART
      
      GETBCISTART = GRID%BCINFO(I)%ISTART(J)
    END FUNCTION GETBCISTART
    !CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
    FUNCTION GETBCIEND(GRID,I,J)
      IMPLICIT NONE
      CLASS(T_GRID), INTENT(IN) :: GRID
      INTEGER, INTENT(IN) :: I,J
      INTEGER :: GETBCIEND
      
      GETBCIEND = GRID%BCINFO(I)%IEND(J)
    END FUNCTION GETBCIEND
    !CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC 
    FUNCTION GETCONNECTDONOR(GRID,I)
      IMPLICIT NONE
      CLASS(T_GRID), INTENT(IN) :: GRID
      INTEGER :: GETCONNECTDONOR
      INTEGER, INTENT(IN) :: I
      
      GETCONNECTDONOR = GRID%CONNECTINFO(I)%DONOR
    END FUNCTION GETCONNECTDONOR
    !CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
    FUNCTION GETCONNECTTRANSMAT(GRID,I,J,K)
      IMPLICIT NONE
      CLASS(T_GRID), INTENT(IN) :: GRID
      INTEGER :: GETCONNECTTRANSMAT
      INTEGER, INTENT(IN) :: I,J,K
      
      GETCONNECTTRANSMAT = GRID%CONNECTINFO(I)%TRANSMAT(J,K)
    END FUNCTION GETCONNECTTRANSMAT
    !CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
    FUNCTION GETCONNECTISTART(GRID,I,J)
      IMPLICIT NONE
      CLASS(T_GRID), INTENT(IN) :: GRID
      INTEGER, INTENT(IN) :: I,J
      INTEGER :: GETCONNECTISTART
      
      GETCONNECTISTART = GRID%CONNECTINFO(I)%ISTART(J)
    END FUNCTION GETCONNECTISTART
    !CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
    FUNCTION GETCONNECTIEND(GRID,I,J)
      IMPLICIT NONE
      CLASS(T_GRID), INTENT(IN) :: GRID
      INTEGER, INTENT(IN) :: I,J
      INTEGER :: GETCONNECTIEND
      
      GETCONNECTIEND = GRID%CONNECTINFO(I)%IEND(J)
    END FUNCTION GETCONNECTIEND
    !CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
    FUNCTION GETCONNECTISTART_DONOR(GRID,I,J)
      IMPLICIT NONE
      CLASS(T_GRID), INTENT(IN) :: GRID
      INTEGER, INTENT(IN) :: I,J
      INTEGER :: GETCONNECTISTART_DONOR
      
      GETCONNECTISTART_DONOR = GRID%CONNECTINFO(I)%ISTART_DONOR(J)
    END FUNCTION GETCONNECTISTART_DONOR
    !CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
    FUNCTION GETCONNECTIEND_DONOR(GRID,I,J)
      IMPLICIT NONE
      CLASS(T_GRID), INTENT(IN) :: GRID
      INTEGER, INTENT(IN) :: I,J
      INTEGER :: GETCONNECTIEND_DONOR
      
      GETCONNECTIEND_DONOR = GRID%CONNECTINFO(I)%IEND_DONOR(J)
    END FUNCTION GETCONNECTIEND_DONOR
    !CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
END MODULE GRID_MODULE