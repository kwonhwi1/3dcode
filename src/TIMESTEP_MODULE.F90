MODULE TIMESTEP_MODULE
  USE CONFIG_MODULE
  USE GRID_MODULE
  USE VARIABLE_MODULE
  IMPLICIT NONE
  PRIVATE
  PUBLIC :: T_TIMESTEP,T_LOCALTIME,T_MINTIME,T_FIXEDTIME

  TYPE, ABSTRACT :: T_TIMESTEP
    PRIVATE
    INTEGER :: NPV,NDV,NTV,NGRD,IMAX,JMAX
    REAL(8) :: CFL,UREF,STR
    REAL(8), DIMENSION(:,:), ALLOCATABLE :: DT
    PROCEDURE(P_GETSNDP2), POINTER :: GETSNDP2
    PROCEDURE(P_GETEIGENVIS), POINTER :: GETEIGENVIS
    CONTAINS
      PROCEDURE :: CONSTRUCT
      PROCEDURE :: DESTRUCT
      PROCEDURE :: GETDT
      PROCEDURE(P_CALTIMESTEP), DEFERRED :: CALTIMESTEP
  END TYPE T_TIMESTEP

  ABSTRACT INTERFACE
    SUBROUTINE P_CALTIMESTEP(TIMESTEP,GRID,VARIABLE)
      IMPORT T_TIMESTEP
      IMPORT T_GRID
      IMPORT T_VARIABLE
      IMPLICIT NONE
      CLASS(T_TIMESTEP), INTENT(INOUT) :: TIMESTEP
      TYPE(T_GRID), INTENT(IN) :: GRID
      TYPE(T_VARIABLE), INTENT(IN) :: VARIABLE
      
    END SUBROUTINE P_CALTIMESTEP
  END INTERFACE
  
  TYPE, EXTENDS(T_TIMESTEP) :: T_LOCALTIME
    CONTAINS
      PROCEDURE :: CALTIMESTEP => LOCALTIME
  END TYPE T_LOCALTIME
  
  TYPE, EXTENDS(T_TIMESTEP) :: T_MINTIME
    CONTAINS
      PROCEDURE :: CALTIMESTEP => MINTIME
  END TYPE T_MINTIME
  
  TYPE, EXTENDS(T_TIMESTEP) :: T_FIXEDTIME
    CONTAINS
      PROCEDURE :: CALTIMESTEP => FIXEDTIME
  END TYPE T_FIXEDTIME
  
  INTERFACE
    FUNCTION P_GETSNDP2(TIMESTEP,SND2,UUU2) RESULT(SNDP2)
      IMPORT T_TIMESTEP
      IMPLICIT NONE
      CLASS(T_TIMESTEP), INTENT(IN) :: TIMESTEP
      REAL(8), INTENT(IN) :: SND2,UUU2
      REAL(8) :: SNDP2
    END FUNCTION P_GETSNDP2
    FUNCTION P_GETEIGENVIS(TIMESTEP,DV,TV) RESULT(EIGENVIS)
      IMPORT T_TIMESTEP
      IMPLICIT NONE
      CLASS(T_TIMESTEP), INTENT(IN) :: TIMESTEP
      REAL(8), INTENT(IN) :: DV(TIMESTEP%NDV),TV(TIMESTEP%NTV)
      REAL(8) :: EIGENVIS
    END FUNCTION P_GETEIGENVIS
  END INTERFACE
  
  CONTAINS
    !CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
    SUBROUTINE CONSTRUCT(TIMESTEP,CONFIG,GRID,VARIABLE)
      IMPLICIT NONE
      CLASS(T_TIMESTEP), INTENT(OUT) :: TIMESTEP
      TYPE(T_CONFIG), INTENT(IN) :: CONFIG
      TYPE(T_GRID), INTENT(IN) :: GRID
      TYPE(T_VARIABLE), INTENT(IN) :: VARIABLE
      
      TIMESTEP%CFL  = CONFIG%GETCFL()
      TIMESTEP%UREF = CONFIG%GETUREF()
      TIMESTEP%STR  =  CONFIG%GETSTR()
      
      SELECT CASE(CONFIG%GETITURB())
      CASE(-1,0)
        TIMESTEP%GETEIGENVIS => TURBULENT
      CASE(-2)
        TIMESTEP%GETEIGENVIS => LAMINAR
      CASE(-3)
        TIMESTEP%GETEIGENVIS => EULER
      END SELECT

      SELECT CASE(CONFIG%GETPREC())
      CASE(0)
        TIMESTEP%GETSNDP2 => NO_PREC
      CASE(1)
        TIMESTEP%GETSNDP2 => STEADY_PREC
      CASE(2)
        TIMESTEP%GETSNDP2 => UNSTEADY_PREC
      END SELECT

      TIMESTEP%NGRD = GRID%GETNGRD()
      TIMESTEP%IMAX = GRID%GETIMAX()
      TIMESTEP%JMAX = GRID%GETJMAX()

      TIMESTEP%NPV = VARIABLE%GETNPV()
      TIMESTEP%NDV = VARIABLE%GETNDV()
      TIMESTEP%NTV = VARIABLE%GETNTV()
      
      ALLOCATE(TIMESTEP%DT(2:TIMESTEP%IMAX,2:TIMESTEP%JMAX))
      
    END SUBROUTINE CONSTRUCT
    !CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
    SUBROUTINE DESTRUCT(TIMESTEP)
      IMPLICIT NONE
      CLASS(T_TIMESTEP), INTENT(INOUT) :: TIMESTEP
      
      IF(ASSOCIATED(TIMESTEP%GETSNDP2))     NULLIFY(TIMESTEP%GETSNDP2)
      IF(ASSOCIATED(TIMESTEP%GETEIGENVIS))  NULLIFY(TIMESTEP%GETEIGENVIS)
      
      DEALLOCATE(TIMESTEP%DT)

    END SUBROUTINE DESTRUCT
    !CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
    SUBROUTINE LOCALTIME(TIMESTEP,GRID,VARIABLE)
      IMPLICIT NONE
      CLASS(T_LOCALTIME), INTENT(INOUT) :: TIMESTEP
      TYPE(T_GRID), INTENT(IN) :: GRID
      TYPE(T_VARIABLE), INTENT(IN) :: VARIABLE
      INTEGER :: I,J    
      REAL(8) :: CX1(2),CX2(2),EX1(2),EX2(2)
      REAL(8) :: PV(TIMESTEP%NPV)
      REAL(8) :: TV(TIMESTEP%NTV)
      REAL(8) :: DV(TIMESTEP%NDV)
      REAL(8) :: GRD(TIMESTEP%NGRD)
      REAL(8) :: C1,C2,E1,E2,S1,S2
      REAL(8) :: UC,VC,UV2,SNDP2
      REAL(8) :: UP,D,EIGENX,EIGENY,EIGENVIS
      
      DO J = 2,TIMESTEP%JMAX 
        DO I = 2,TIMESTEP%IMAX 
          
          PV = VARIABLE%GETPV(I,J)
          TV = VARIABLE%GETTV(I,J)
          DV = VARIABLE%GETDV(I,J)
          GRD = GRID%GETGRD(I,J)
          CX1 = GRID%GETCX(I-1,J)
          CX2 = GRID%GETCX(I,J)
          EX1 = GRID%GETEX(I,J-1)
          EX2 = GRID%GETEX(I,J)

          C1 = 0.5D0*(CX1(1)+CX2(1))
          C2 = 0.5D0*(CX1(2)+CX2(2))
          E1 = 0.5D0*(EX1(1)+EX2(1))
          E2 = 0.5D0*(EX1(2)+EX2(2))
          
          UC = DABS(C1*PV(2)+C2*PV(3))
          VC = DABS(E1*PV(2)+E2*PV(3))
          S1 = C1**2+C2**2
          S2 = E1**2+E2**2
          UV2 = PV(2)**2+PV(3)**2
          
          SNDP2 = TIMESTEP%GETSNDP2(DV(6),UV2)
          
          UP = UC*(1.D0+SNDP2/DV(6))
          D = DSQRT(UC**2*(1.D0-SNDP2/DV(6))**2+4.D0*SNDP2*S1)
          EIGENX = 0.5D0*(UP+D)
          
          UP = VC*(1.D0+SNDP2/DV(6))
          D = DSQRT(VC**2*(1.D0-SNDP2/DV(6))**2+4.D0*SNDP2*S2)
          EIGENY = 0.5D0*(UP+D)
          
          EIGENVIS = TIMESTEP%GETEIGENVIS(DV,TV)*(S1+S2)/GRD(1)
          
          TIMESTEP%DT(I,J) = TIMESTEP%CFL*GRD(1)/(EIGENX + EIGENY + 4.D0*EIGENVIS)
         END DO
      END DO

    END SUBROUTINE LOCALTIME
    !CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
    SUBROUTINE MINTIME(TIMESTEP,GRID,VARIABLE)
      IMPLICIT NONE
      CLASS(T_MINTIME), INTENT(INOUT) :: TIMESTEP
      TYPE(T_GRID), INTENT(IN) :: GRID
      TYPE(T_VARIABLE), INTENT(IN) :: VARIABLE
      INCLUDE 'mpif.h'
      INTEGER :: I,J
      REAL(8) :: CX1(2),CX2(2),EX1(2),EX2(2)
      REAL(8) :: PV(TIMESTEP%NPV)
      REAL(8) :: TV(TIMESTEP%NTV)
      REAL(8) :: DV(TIMESTEP%NDV)
      REAL(8) :: GRD(TIMESTEP%NGRD)
      REAL(8) :: C1,C2,E1,E2,S1,S2
      REAL(8) :: UC,VC,UV2,SNDP2
      REAL(8) :: UP,D,EIGENX,EIGENY,EIGENVIS
      REAL(8) :: DTMIN,MPI_DTMIN
      INTEGER :: IERR
            
      DO J = 2,TIMESTEP%JMAX 
        DO I = 2,TIMESTEP%IMAX 
        
          PV = VARIABLE%GETPV(I,J)
          TV = VARIABLE%GETTV(I,J)
          DV = VARIABLE%GETDV(I,J)
          GRD = GRID%GETGRD(I,J)
          CX1 = GRID%GETCX(I-1,J)
          CX2 = GRID%GETCX(I,J)
          EX1 = GRID%GETEX(I,J-1)
          EX2 = GRID%GETEX(I,J)

          C1 = 0.5D0*(CX1(1)+CX2(1))
          C2 = 0.5D0*(CX1(2)+CX2(2))
          E1 = 0.5D0*(EX1(1)+EX2(1))
          E2 = 0.5D0*(EX1(2)+EX2(2))
          
          UC = DABS(C1*PV(2)+C2*PV(3))
          VC = DABS(E1*PV(2)+E2*PV(3))
          S1 = C1**2+C2**2
          S2 = E1**2+E2**2
          UV2 = PV(2)**2+PV(3)**2
          
          SNDP2 = TIMESTEP%GETSNDP2(DV(6),UV2)
          
          UP = UC*(1.D0+SNDP2/DV(6))
          D = DSQRT(UC**2*(1.D0-SNDP2/DV(6))**2+4.D0*SNDP2*S1)
          EIGENX = 0.5D0*(UP+D)
          
          UP = VC*(1.D0+SNDP2/DV(6))
          D = DSQRT(VC**2*(1.D0-SNDP2/DV(6))**2+4.D0*SNDP2*S2)
          EIGENY = 0.5D0*(UP+D)
          
          EIGENVIS = TIMESTEP%GETEIGENVIS(DV,TV)*(S1+S2)/GRD(1)
          
          TIMESTEP%DT(I,J) = TIMESTEP%CFL*GRD(1)/(EIGENX + EIGENY + 4.D0*EIGENVIS)
          
        END DO
      END DO

      DTMIN = MINVAL(TIMESTEP%DT)
      
      MPI_DTMIN = DTMIN
      CALL MPI_REDUCE(DTMIN,MPI_DTMIN,1,MPI_REAL8,MPI_MIN,0,MPI_COMM_WORLD,IERR)
      CALL MPI_BCAST(MPI_DTMIN,1,MPI_REAL8,0,MPI_COMM_WORLD,IERR)
      TIMESTEP%DT = MPI_DTMIN
      
    END SUBROUTINE MINTIME
    !CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
    SUBROUTINE FIXEDTIME(TIMESTEP,GRID,VARIABLE)
      IMPLICIT NONE
      CLASS(T_FIXEDTIME), INTENT(INOUT) :: TIMESTEP
      TYPE(T_GRID), INTENT(IN) :: GRID
      TYPE(T_VARIABLE), INTENT(IN) :: VARIABLE

      TIMESTEP%DT = TIMESTEP%CFL

    END SUBROUTINE FIXEDTIME
    !CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
    FUNCTION NO_PREC(TIMESTEP,SND2,UUU2) RESULT(SNDP2)
      IMPLICIT NONE
      CLASS(T_TIMESTEP), INTENT(IN) :: TIMESTEP
      REAL(8), INTENT(IN) :: SND2,UUU2
      REAL(8) :: SNDP2
      
      SNDP2 = SND2
      
    END FUNCTION NO_PREC
    !CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
    FUNCTION STEADY_PREC(TIMESTEP,SND2,UUU2) RESULT(SNDP2)
      IMPLICIT NONE
      CLASS(T_TIMESTEP), INTENT(IN) :: TIMESTEP
      REAL(8), INTENT(IN) :: SND2,UUU2
      REAL(8) :: SNDP2
      
      SNDP2 = DMIN1(SND2,DMAX1(UUU2,TIMESTEP%UREF**2))
      
    END FUNCTION STEADY_PREC
    !CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
    FUNCTION UNSTEADY_PREC(TIMESTEP,SND2,UUU2) RESULT(SNDP2)
      IMPLICIT NONE
      CLASS(T_TIMESTEP), INTENT(IN) :: TIMESTEP
      REAL(8), INTENT(IN) :: SND2,UUU2
      REAL(8) :: SNDP2
      
      SNDP2 = DMIN1(SND2,DMAX1(UUU2,TIMESTEP%UREF**2,TIMESTEP%STR**2*UUU2))
      
    END FUNCTION UNSTEADY_PREC
    !CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
    FUNCTION EULER(TIMESTEP,DV,TV) RESULT(EIGENVIS)
      IMPLICIT NONE
      CLASS(T_TIMESTEP), INTENT(IN) :: TIMESTEP
      REAL(8), INTENT(IN) :: DV(TIMESTEP%NDV),TV(TIMESTEP%NTV)
      REAL(8) :: EIGENVIS
      
      EIGENVIS = 0.D0
      
    END FUNCTION EULER
    !CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
    FUNCTION LAMINAR(TIMESTEP,DV,TV) RESULT(EIGENVIS)
      IMPLICIT NONE
      CLASS(T_TIMESTEP), INTENT(IN) :: TIMESTEP
      REAL(8), INTENT(IN) :: DV(TIMESTEP%NDV),TV(TIMESTEP%NTV)
      REAL(8) :: EIGENVIS

      EIGENVIS = DMAX1(DV(7)*TV(2)/(DV(8)+DV(12)*DV(7)*DV(1)-DV(11)*DV(8)*DV(1)),  &
                       4.D0/3.D0*TV(1)/DV(1)) 
    END FUNCTION LAMINAR
    !CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
    FUNCTION TURBULENT(TIMESTEP,DV,TV) RESULT(EIGENVIS)
      IMPLICIT NONE
      CLASS(T_TIMESTEP), INTENT(IN) :: TIMESTEP
      REAL(8), INTENT(IN) :: DV(TIMESTEP%NDV),TV(TIMESTEP%NTV)
      REAL(8) :: EIGENVIS
      REAL(8), PARAMETER :: PR_T = 0.9D0
   
      EIGENVIS = DMAX1(DV(7)*(TV(2)+DV(12)*TV(3)/PR_T)/(DV(8)+DV(12)*DV(7)*DV(1)    &
                       -DV(11)*DV(8)*DV(1)),4.D0/3.D0*(TV(1)+TV(3))/DV(1))
    END FUNCTION TURBULENT
    !CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
    FUNCTION GETDT(TIMESTEP,I,J)
      IMPLICIT NONE
      CLASS(T_TIMESTEP), INTENT(IN) :: TIMESTEP
      INTEGER, INTENT(IN) :: I,J
      REAL(8) :: GETDT
      
      GETDT = TIMESTEP%DT(I,J)
      
    END FUNCTION GETDT
    !CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
END MODULE TIMESTEP_MODULE