MODULE UPDATE_MODULE
  USE CONFIG_MODULE 
  USE GRID_MODULE
  USE VARIABLE_MODULE
  USE EOS_MODULE
  USE PROP_MODULE
!CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
! SUBORDINATE TO UPDATE MODULE  
!CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC  
  USE RHS_MODULE
  USE TIMESTEP_MODULE
  USE LHS_MODULE
  USE JACOBIAN_MODULE
  USE EDDY_MODULE
  USE BC_MODULE
!CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC    
  IMPLICIT NONE
  PRIVATE
  PUBLIC :: T_UPDATE,T_EULEREX,T_RK3RD,T_LUSGS
  
  TYPE, ABSTRACT :: T_UPDATE
    PRIVATE
    INTEGER :: NPV,NDV,NTV,NGRD,IMAX,JMAX,KMAX
    LOGICAL :: L_TIMESTEP,L_LHS,L_EDDY,L_JAC,L_TV,L_TURB,L_CAV
    REAL(8) :: PREF,KREF,OREF
    CLASS(T_LHS), ALLOCATABLE :: LHS
    CLASS(T_TIMESTEP), ALLOCATABLE :: TIMESTEP
    CLASS(T_EDDY), ALLOCATABLE :: EDDY
    CLASS(T_JAC), ALLOCATABLE :: JAC
    CLASS(T_RHS), ALLOCATABLE :: RHS
    CLASS(T_BC), ALLOCATABLE :: BC
    CONTAINS
      PROCEDURE :: CONSTRUCT => CONSTRUCT_EULEREX
      PROCEDURE :: DESTRUCT  => DESTRUCT_EULEREX
      PROCEDURE(P_TIMEINTEG), DEFERRED :: TIMEINTEG
  END TYPE T_UPDATE
  
  ABSTRACT INTERFACE
    SUBROUTINE P_TIMEINTEG(UPDATE,GRID,VARIABLE,EOS,PROP)
      IMPORT T_UPDATE
      IMPORT T_GRID
      IMPORT T_VARIABLE
      IMPORT T_EOS
      IMPORT T_PROP
      IMPLICIT NONE
      CLASS(T_UPDATE), INTENT(INOUT) :: UPDATE
      TYPE(T_GRID), INTENT(IN) :: GRID
      TYPE(T_VARIABLE), INTENT(INOUT) :: VARIABLE
      TYPE(T_EOS), INTENT(IN) :: EOS
      TYPE(T_PROP), INTENT(IN) :: PROP
    END SUBROUTINE P_TIMEINTEG
  END INTERFACE
  
  TYPE, EXTENDS(T_UPDATE) :: T_EULEREX
    CONTAINS
      PROCEDURE :: TIMEINTEG => EULEREX
  END TYPE T_EULEREX
  
  TYPE, EXTENDS(T_UPDATE) :: T_RK3RD
    PRIVATE
    REAL(8) :: A1(3),A2(3),A3(3)
    REAL(8), DIMENSION(:,:,:,:), ALLOCATABLE :: RK
    CONTAINS
      PROCEDURE :: CONSTRUCT => CONSTRUCT_RK3RD
      PROCEDURE :: DESTRUCT  => DESTRUCT_RK3RD
      PROCEDURE :: TIMEINTEG => RK3RD
  END TYPE T_RK3RD
  
  TYPE, EXTENDS(T_UPDATE) :: T_LUSGS
    PRIVATE
    REAL(8), DIMENSION(:,:,:,:), ALLOCATABLE  :: DQS,DCV
    REAL(8), DIMENSION(:,:,:,:,:), ALLOCATABLE :: INV
    CONTAINS
      PROCEDURE :: CONSTRUCT => CONSTRUCT_LUSGS
      PROCEDURE :: DESTRUCT  => DESTRUCT_LUSGS
      PROCEDURE :: TIMEINTEG => LUSGS
  END TYPE T_LUSGS
  

  CONTAINS
    !CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
    SUBROUTINE CONSTRUCT_EULEREX(UPDATE,CONFIG,GRID,VARIABLE,EOS,PROP)
      IMPLICIT NONE
      CLASS(T_UPDATE), INTENT(OUT) :: UPDATE
      TYPE(T_CONFIG), INTENT(IN) :: CONFIG
      TYPE(T_GRID), INTENT(IN) :: GRID
      TYPE(T_VARIABLE), INTENT(IN) :: VARIABLE
      TYPE(T_EOS), INTENT(IN) :: EOS
      TYPE(T_PROP), INTENT(IN) :: PROP   
      
      UPDATE%PREF = CONFIG%GETPREF()
      UPDATE%KREF = CONFIG%GETKREF()*1.D-12
      UPDATE%OREF = CONFIG%GETOREF()*1.D-12
      
      UPDATE%L_TIMESTEP = .FALSE.
      UPDATE%L_LHS = .FALSE.
      UPDATE%L_EDDY = .FALSE.
      UPDATE%L_JAC = .FALSE.
      
      SELECT CASE(CONFIG%GETLOCAL())
      CASE(-1)
        ALLOCATE(T_FIXEDTIME::UPDATE%TIMESTEP)
      CASE(0)
        ALLOCATE(T_MINTIME::UPDATE%TIMESTEP)
      CASE(1)
        ALLOCATE(T_LOCALTIME::UPDATE%TIMESTEP)
      END SELECT
            
      SELECT CASE(CONFIG%GETITURB())
      CASE(0)
        UPDATE%L_TV = .TRUE.
        UPDATE%L_TURB = .TRUE.
        ALLOCATE(T_EDDY_KWSST::UPDATE%EDDY)
        ALLOCATE(T_LHS_FLOWTURBALL::UPDATE%LHS) 
        IF(CONFIG%GETTIMEMETHOD().EQ.3) ALLOCATE(T_JAC_FLOWTURBALL::UPDATE%JAC)
      CASE(-1)
        UPDATE%L_TV = .TRUE.
        UPDATE%L_TURB = .TRUE.
        ALLOCATE(T_EDDY_KE::UPDATE%EDDY)
        ALLOCATE(T_LHS_FLOWTURBALL::UPDATE%LHS)
        IF(CONFIG%GETTIMEMETHOD().EQ.3) ALLOCATE(T_JAC_FLOWTURBALL::UPDATE%JAC)
      CASE(-2)
        UPDATE%L_TV = .TRUE.
        UPDATE%L_TURB = .FALSE.
        ALLOCATE(T_LHS_FLOWONLY::UPDATE%LHS)
        IF(CONFIG%GETTIMEMETHOD().EQ.3) ALLOCATE(T_JAC_FLOWONLY::UPDATE%JAC)
      CASE(-3)
        UPDATE%L_TV = .FALSE.
        UPDATE%L_TURB = .FALSE.
        ALLOCATE(T_LHS_FLOWONLY::UPDATE%LHS)
        IF(CONFIG%GETTIMEMETHOD().EQ.3) ALLOCATE(T_JAC_FLOWONLY::UPDATE%JAC)
      END SELECT
      
      SELECT CASE(CONFIG%GETNCAV())
      CASE(0)
        UPDATE%L_CAV = .FALSE.
      CASE(1,2,3)
        UPDATE%L_CAV = .TRUE.
      END SELECT
      
      IF(ALLOCATED(UPDATE%TIMESTEP)) THEN
        CALL UPDATE%TIMESTEP%CONSTRUCT(CONFIG,GRID,VARIABLE)
        UPDATE%L_TIMESTEP = .TRUE.
      END IF

      IF(ALLOCATED(UPDATE%LHS)) THEN
        CALL UPDATE%LHS%CONSTRUCT(CONFIG,GRID,VARIABLE)
        UPDATE%L_LHS = .TRUE.
      END IF
      
      IF(ALLOCATED(UPDATE%EDDY)) THEN
        CALL UPDATE%EDDY%CONSTRUCT(GRID,VARIABLE)
        UPDATE%L_EDDY = .TRUE.
      END IF
      
      IF(ALLOCATED(UPDATE%JAC)) THEN
        CALL UPDATE%JAC%CONSTRUCT(CONFIG,GRID,VARIABLE)
        UPDATE%L_JAC = .TRUE.
      END IF
      
      ALLOCATE(UPDATE%RHS)
      CALL UPDATE%RHS%CONSTRUCT(CONFIG,GRID,VARIABLE)

      ALLOCATE(UPDATE%BC)
      CALL UPDATE%BC%CONSTRUCT(CONFIG,GRID,VARIABLE,EOS,PROP)
      
      UPDATE%NPV = VARIABLE%GETNPV()
      UPDATE%NDV = VARIABLE%GETNDV()
      UPDATE%NTV = VARIABLE%GETNTV()
      UPDATE%NGRD = GRID%GETNGRD()
      UPDATE%IMAX = GRID%GETIMAX()
      UPDATE%JMAX = GRID%GETJMAX()
      UPDATE%KMAX = GRID%GETKMAX()
      
    END SUBROUTINE CONSTRUCT_EULEREX
    !CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
    SUBROUTINE CONSTRUCT_RK3RD(UPDATE,CONFIG,GRID,VARIABLE,EOS,PROP)
      IMPLICIT NONE
      CLASS(T_RK3RD), INTENT(OUT) :: UPDATE
      TYPE(T_CONFIG), INTENT(IN) :: CONFIG
      TYPE(T_GRID), INTENT(IN) :: GRID
      TYPE(T_VARIABLE), INTENT(IN) :: VARIABLE
      TYPE(T_EOS), INTENT(IN) :: EOS
      TYPE(T_PROP), INTENT(IN) :: PROP 
      
      UPDATE%PREF = CONFIG%GETPREF()
      UPDATE%KREF = CONFIG%GETKREF()*1.D-12
      UPDATE%OREF = CONFIG%GETOREF()*1.D-12
      
      UPDATE%L_TIMESTEP = .FALSE.
      UPDATE%L_LHS = .FALSE.
      UPDATE%L_EDDY = .FALSE.
      UPDATE%L_JAC = .FALSE.
      
      SELECT CASE(CONFIG%GETLOCAL())
      CASE(-1)
        ALLOCATE(T_FIXEDTIME::UPDATE%TIMESTEP)
      CASE(0)
        ALLOCATE(T_MINTIME::UPDATE%TIMESTEP)
      CASE(1)
        ALLOCATE(T_LOCALTIME::UPDATE%TIMESTEP)
      END SELECT
            
      SELECT CASE(CONFIG%GETITURB())
      CASE(0)
        UPDATE%L_TV = .TRUE.
        UPDATE%L_TURB = .TRUE.
        ALLOCATE(T_EDDY_KWSST::UPDATE%EDDY)
        ALLOCATE(T_LHS_FLOWTURBALL::UPDATE%LHS) 
        IF(CONFIG%GETTIMEMETHOD().EQ.3) ALLOCATE(T_JAC_FLOWTURBALL::UPDATE%JAC)
      CASE(-1)
        UPDATE%L_TV = .TRUE.
        UPDATE%L_TURB = .TRUE.
        ALLOCATE(T_EDDY_KE::UPDATE%EDDY)
        ALLOCATE(T_LHS_FLOWTURBALL::UPDATE%LHS)
        IF(CONFIG%GETTIMEMETHOD().EQ.3) ALLOCATE(T_JAC_FLOWTURBALL::UPDATE%JAC)
      CASE(-2)
        UPDATE%L_TV = .TRUE.
        UPDATE%L_TURB = .FALSE.
        ALLOCATE(T_LHS_FLOWONLY::UPDATE%LHS)
        IF(CONFIG%GETTIMEMETHOD().EQ.3) ALLOCATE(T_JAC_FLOWONLY::UPDATE%JAC)
      CASE(-3)
        UPDATE%L_TV = .FALSE.
        UPDATE%L_TURB = .FALSE.
        ALLOCATE(T_LHS_FLOWONLY::UPDATE%LHS)
        IF(CONFIG%GETTIMEMETHOD().EQ.3) ALLOCATE(T_JAC_FLOWONLY::UPDATE%JAC)
      END SELECT
      
      SELECT CASE(CONFIG%GETNCAV())
      CASE(0)
        UPDATE%L_CAV = .FALSE.
      CASE(1,2,3)
        UPDATE%L_CAV = .TRUE.
      END SELECT
      
      IF(ALLOCATED(UPDATE%TIMESTEP)) THEN
        CALL UPDATE%TIMESTEP%CONSTRUCT(CONFIG,GRID,VARIABLE)
        UPDATE%L_TIMESTEP = .TRUE.
      END IF

      IF(ALLOCATED(UPDATE%LHS)) THEN
        CALL UPDATE%LHS%CONSTRUCT(CONFIG,GRID,VARIABLE)
        UPDATE%L_LHS = .TRUE.
      END IF
      
      IF(ALLOCATED(UPDATE%EDDY)) THEN
        CALL UPDATE%EDDY%CONSTRUCT(GRID,VARIABLE)
        UPDATE%L_EDDY = .TRUE.
      END IF
      
      IF(ALLOCATED(UPDATE%JAC)) THEN
        CALL UPDATE%JAC%CONSTRUCT(CONFIG,GRID,VARIABLE)
        UPDATE%L_JAC = .TRUE.
      END IF
      
      ALLOCATE(UPDATE%RHS)
      CALL UPDATE%RHS%CONSTRUCT(CONFIG,GRID,VARIABLE)

      ALLOCATE(UPDATE%BC)
      CALL UPDATE%BC%CONSTRUCT(CONFIG,GRID,VARIABLE,EOS,PROP)
      
      UPDATE%NPV = VARIABLE%GETNPV()
      UPDATE%NDV = VARIABLE%GETNDV()
      UPDATE%NTV = VARIABLE%GETNTV()
      UPDATE%NGRD = GRID%GETNGRD()
      UPDATE%IMAX = GRID%GETIMAX()
      UPDATE%JMAX = GRID%GETJMAX()
      UPDATE%KMAX = GRID%GETKMAX()
      
      UPDATE%A1=(/0.D0,0.75D0,1.D0/3.D0/)
      UPDATE%A2=(/1.D0,0.25D0,2.D0/3.D0/)
      UPDATE%A3=(/1.D0,0.25D0,2.D0/3.D0/)
      
      ALLOCATE(UPDATE%RK(UPDATE%NPV,UPDATE%IMAX,UPDATE%JMAX,UPDATE%KMAX))
    END SUBROUTINE CONSTRUCT_RK3RD
    !CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
    SUBROUTINE CONSTRUCT_LUSGS(UPDATE,CONFIG,GRID,VARIABLE,EOS,PROP)
      IMPLICIT NONE
      CLASS(T_LUSGS), INTENT(OUT) :: UPDATE
      TYPE(T_CONFIG), INTENT(IN) :: CONFIG
      TYPE(T_GRID), INTENT(IN) :: GRID
      TYPE(T_VARIABLE), INTENT(IN) :: VARIABLE
      TYPE(T_EOS), INTENT(IN) :: EOS
      TYPE(T_PROP), INTENT(IN) :: PROP   
      
      UPDATE%PREF = CONFIG%GETPREF()
      UPDATE%KREF = CONFIG%GETKREF()*1.D-12
      UPDATE%OREF = CONFIG%GETOREF()*1.D-12
      
      UPDATE%L_TIMESTEP = .FALSE.
      UPDATE%L_LHS = .FALSE.
      UPDATE%L_EDDY = .FALSE.
      UPDATE%L_JAC = .FALSE.
      
      SELECT CASE(CONFIG%GETLOCAL())
      CASE(-1)
        ALLOCATE(T_FIXEDTIME::UPDATE%TIMESTEP)
      CASE(0)
        ALLOCATE(T_MINTIME::UPDATE%TIMESTEP)
      CASE(1)
        ALLOCATE(T_LOCALTIME::UPDATE%TIMESTEP)
      END SELECT
            
      SELECT CASE(CONFIG%GETITURB())
      CASE(0)
        UPDATE%L_TV = .TRUE.
        UPDATE%L_TURB = .TRUE.
        ALLOCATE(T_EDDY_KWSST::UPDATE%EDDY)
        ALLOCATE(T_LHS_FLOWTURBALL::UPDATE%LHS) 
        IF(CONFIG%GETTIMEMETHOD().EQ.3) ALLOCATE(T_JAC_FLOWTURBALL::UPDATE%JAC)
      CASE(-1)
        UPDATE%L_TV = .TRUE.
        UPDATE%L_TURB = .TRUE.
        ALLOCATE(T_EDDY_KE::UPDATE%EDDY)
        ALLOCATE(T_LHS_FLOWTURBALL::UPDATE%LHS)
        IF(CONFIG%GETTIMEMETHOD().EQ.3) ALLOCATE(T_JAC_FLOWTURBALL::UPDATE%JAC)
      CASE(-2)
        UPDATE%L_TV = .TRUE.
        UPDATE%L_TURB = .FALSE.
        ALLOCATE(T_LHS_FLOWONLY::UPDATE%LHS)
        IF(CONFIG%GETTIMEMETHOD().EQ.3) ALLOCATE(T_JAC_FLOWONLY::UPDATE%JAC)
      CASE(-3)
        UPDATE%L_TV = .FALSE.
        UPDATE%L_TURB = .FALSE.
        ALLOCATE(T_LHS_FLOWONLY::UPDATE%LHS)
        IF(CONFIG%GETTIMEMETHOD().EQ.3) ALLOCATE(T_JAC_FLOWONLY::UPDATE%JAC)
      END SELECT
      
      SELECT CASE(CONFIG%GETNCAV())
      CASE(0)
        UPDATE%L_CAV = .FALSE.
      CASE(1,2,3)
        UPDATE%L_CAV = .TRUE.
      END SELECT
      
      IF(ALLOCATED(UPDATE%TIMESTEP)) THEN
        CALL UPDATE%TIMESTEP%CONSTRUCT(CONFIG,GRID,VARIABLE)
        UPDATE%L_TIMESTEP = .TRUE.
      END IF

      IF(ALLOCATED(UPDATE%LHS)) THEN
        CALL UPDATE%LHS%CONSTRUCT(CONFIG,GRID,VARIABLE)
        UPDATE%L_LHS = .TRUE.
      END IF
      
      IF(ALLOCATED(UPDATE%EDDY)) THEN
        CALL UPDATE%EDDY%CONSTRUCT(GRID,VARIABLE)
        UPDATE%L_EDDY = .TRUE.
      END IF
      
      IF(ALLOCATED(UPDATE%JAC)) THEN
        CALL UPDATE%JAC%CONSTRUCT(CONFIG,GRID,VARIABLE)
        UPDATE%L_JAC = .TRUE.
      END IF
      
      ALLOCATE(UPDATE%RHS)
      CALL UPDATE%RHS%CONSTRUCT(CONFIG,GRID,VARIABLE)

      ALLOCATE(UPDATE%BC)
      CALL UPDATE%BC%CONSTRUCT(CONFIG,GRID,VARIABLE,EOS,PROP)
      
      UPDATE%NPV = VARIABLE%GETNPV()
      UPDATE%NDV = VARIABLE%GETNDV()
      UPDATE%NTV = VARIABLE%GETNTV()
      UPDATE%NGRD = GRID%GETNGRD()
      UPDATE%IMAX = GRID%GETIMAX()
      UPDATE%JMAX = GRID%GETJMAX()
      UPDATE%KMAX = GRID%GETKMAX()
      
      ALLOCATE(UPDATE%DQS(UPDATE%NPV,UPDATE%IMAX+1,UPDATE%JMAX+1,UPDATE%KMAX+1))
      ALLOCATE(UPDATE%DCV(UPDATE%NPV,UPDATE%IMAX+1,UPDATE%JMAX+1,UPDATE%KMAX+1))
      ALLOCATE(UPDATE%INV(UPDATE%NPV,UPDATE%NPV,UPDATE%IMAX,UPDATE%JMAX,UPDATE%KMAX))
      
    END SUBROUTINE CONSTRUCT_LUSGS
    !CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
    SUBROUTINE DESTRUCT_EULEREX(UPDATE)
      IMPLICIT NONE
      CLASS(T_UPDATE), INTENT(INOUT) :: UPDATE
          
      IF(UPDATE%L_TIMESTEP) THEN
        CALL UPDATE%TIMESTEP%DESTRUCT()
        DEALLOCATE(UPDATE%TIMESTEP)
      END IF
      
      IF(UPDATE%L_LHS) THEN
        CALL UPDATE%LHS%DESTRUCT()
        DEALLOCATE(UPDATE%LHS)
      END IF
      
      IF(UPDATE%L_EDDY) THEN
        CALL UPDATE%EDDY%DESTRUCT()
        DEALLOCATE(UPDATE%EDDY)
      END IF

      IF(UPDATE%L_JAC) THEN
        CALL UPDATE%JAC%DESTRUCT()
        DEALLOCATE(UPDATE%JAC)
      END IF
      
      CALL UPDATE%RHS%DESTRUCT()
      DEALLOCATE(UPDATE%RHS)

      CALL UPDATE%BC%DESTRUCT()
      DEALLOCATE(UPDATE%BC)
      
    END SUBROUTINE DESTRUCT_EULEREX
    !CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
    SUBROUTINE DESTRUCT_RK3RD(UPDATE)
      IMPLICIT NONE
      CLASS(T_RK3RD), INTENT(INOUT) :: UPDATE
          
      IF(UPDATE%L_TIMESTEP) THEN
        CALL UPDATE%TIMESTEP%DESTRUCT()
        DEALLOCATE(UPDATE%TIMESTEP)
      END IF
      
      IF(UPDATE%L_LHS) THEN
        CALL UPDATE%LHS%DESTRUCT()
        DEALLOCATE(UPDATE%LHS)
      END IF
      
      IF(UPDATE%L_EDDY) THEN
        CALL UPDATE%EDDY%DESTRUCT()
        DEALLOCATE(UPDATE%EDDY)
      END IF

      IF(UPDATE%L_JAC) THEN
        CALL UPDATE%JAC%DESTRUCT()
        DEALLOCATE(UPDATE%JAC)
      END IF
      
      CALL UPDATE%RHS%DESTRUCT()
      DEALLOCATE(UPDATE%RHS)

      CALL UPDATE%BC%DESTRUCT()
      DEALLOCATE(UPDATE%BC)
      
      DEALLOCATE(UPDATE%RK)
    END SUBROUTINE DESTRUCT_RK3RD
    !CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
    SUBROUTINE DESTRUCT_LUSGS(UPDATE)
      IMPLICIT NONE
      CLASS(T_LUSGS), INTENT(INOUT) :: UPDATE
          
      IF(UPDATE%L_TIMESTEP) THEN
        CALL UPDATE%TIMESTEP%DESTRUCT()
        DEALLOCATE(UPDATE%TIMESTEP)
      END IF
      
      IF(UPDATE%L_LHS) THEN
        CALL UPDATE%LHS%DESTRUCT()
        DEALLOCATE(UPDATE%LHS)
      END IF
      
      IF(UPDATE%L_EDDY) THEN
        CALL UPDATE%EDDY%DESTRUCT()
        DEALLOCATE(UPDATE%EDDY)
      END IF

      IF(UPDATE%L_JAC) THEN
        CALL UPDATE%JAC%DESTRUCT()
        DEALLOCATE(UPDATE%JAC)
      END IF
      
      CALL UPDATE%RHS%DESTRUCT()
      DEALLOCATE(UPDATE%RHS)

      CALL UPDATE%BC%DESTRUCT()
      DEALLOCATE(UPDATE%BC)

      DEALLOCATE(UPDATE%DQS,UPDATE%DCV,UPDATE%INV)
      
    END SUBROUTINE DESTRUCT_LUSGS
    !CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
    SUBROUTINE EULEREX(UPDATE,GRID,VARIABLE,EOS,PROP)
      IMPLICIT NONE
      CLASS(T_EULEREX), INTENT(INOUT) :: UPDATE
      TYPE(T_GRID), INTENT(IN) :: GRID
      TYPE(T_VARIABLE), INTENT(INOUT) :: VARIABLE
      TYPE(T_EOS), INTENT(IN) :: EOS
      TYPE(T_PROP), INTENT(IN) :: PROP
      INTEGER :: I,J,K,N,L
      REAL(8) :: NX1(3),NX2(3),NX3(3),NX4(3),NX5(3),NX6(3)
      REAL(8) :: PV(UPDATE%NPV),DV(UPDATE%NDV),TV(UPDATE%NTV)
      REAL(8) :: GRD(UPDATE%NGRD),DT
      REAL(8) :: DPV(UPDATE%NPV)
      REAL(8) :: X(7,UPDATE%NPV)
      
      CALL UPDATE%BC%SETBC(GRID,VARIABLE,EOS,PROP)
      CALL UPDATE%TIMESTEP%CALTIMESTEP(GRID,VARIABLE)
      CALL UPDATE%RHS%CALRHS(GRID,VARIABLE,EOS)
      
      DO K=2,UPDATE%KMAX
        DO J=2,UPDATE%JMAX
          DO I=2,UPDATE%IMAX
          
            NX1 = GRID%GETCX(I-1,J,K)
            NX2 = GRID%GETCX(I,J,K)
            NX3 = GRID%GETEX(I,J-1,K)
            NX4 = GRID%GETEX(I,J,K)
            NX5 = GRID%GETTX(I,J,K-1)
            NX6 = GRID%GETTX(I,J,K)
            GRD = GRID%GETGRD(I,J,K)
            PV = VARIABLE%GETPV(I,J,K)
            DV = VARIABLE%GETDV(I,J,K)
            TV = VARIABLE%GETTV(I,J,K)
            DT = UPDATE%TIMESTEP%GETDT(I,J,K)
            
            CALL UPDATE%LHS%SETNORM(NX1,NX2,NX3,NX4,NX5,NX6)
            CALL UPDATE%LHS%SETGRD(GRD)
            CALL UPDATE%LHS%SETPV(PV)
            CALL UPDATE%LHS%SETDV(DV)
            CALL UPDATE%LHS%SETTV(TV)
            CALL UPDATE%LHS%SETDT(DT)
            CALL UPDATE%LHS%INVERSE()
            
            DPV = 0.D0
            DO N=1,UPDATE%NPV
              DO L=1,UPDATE%NPV
                DPV(N) = DPV(N) + UPDATE%LHS%GETX(N,L)*UPDATE%RHS%GETRES(L,I,J,K)
              END DO
            END DO
            
            PV = PV + DPV
            PV(1) = DMAX1(-UPDATE%PREF+100.D0,PV(1))
            PV(6) = DMIN1(1.D0,DMAX1(0.D0,PV(6)))
            IF(UPDATE%L_TURB) THEN
              PV(8) = DMAX1(PV(8),UPDATE%KREF)
              PV(9) = DMAX1(PV(9),UPDATE%OREF,UPDATE%RHS%GETOMEGA_CUT(I,J,K))
            END IF
            DO N=1,UPDATE%NPV
              CALL VARIABLE%SETPV(N,I,J,K,PV(N))
            END DO
            
            CALL EOS%DETEOS(PV(1)+UPDATE%PREF,PV(5),PV(6),PV(7),DV)
            
            DO N=1,UPDATE%NDV
              CALL VARIABLE%SETDV(N,I,J,K,DV(N))
            END DO
            
            IF(UPDATE%L_TV) THEN
              CALL PROP%DETPROP(DV(3),DV(4),DV(5),PV(5),PV(6),PV(7),TV(1:2))
            END IF
        
            DO N=1,UPDATE%NTV
              CALL VARIABLE%SETTV(N,I,J,K,TV(N))
            END DO
            
            IF(UPDATE%L_EDDY) THEN
              X(1,:) = VARIABLE%GETPV(I-1,J,K)
              X(2,:) = VARIABLE%GETPV(I,J,K)
              X(3,:) = VARIABLE%GETPV(I+1,J,K)
              X(4,:) = VARIABLE%GETPV(I,J-1,K)
              X(5,:) = VARIABLE%GETPV(I,J+1,K)
              X(6,:) = VARIABLE%GETPV(I,J,K-1)
              X(7,:) = VARIABLE%GETPV(I,J,K+1)              
              CALL UPDATE%EDDY%SETNORM(NX1,NX2,NX3,NX4,NX5,NX6)
              CALL UPDATE%EDDY%SETGRD(GRD)
              CALL UPDATE%EDDY%SETPV(X)
              CALL UPDATE%EDDY%SETDV(DV)
              CALL UPDATE%EDDY%SETTV(TV)
              TV(3) = UPDATE%EDDY%CALEDDY()
              CALL VARIABLE%SETTV(3,I,J,K,TV(3))
            END IF
            
          END DO
        END DO
      END DO
    END SUBROUTINE EULEREX
    !CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
    SUBROUTINE RK3RD(UPDATE,GRID,VARIABLE,EOS,PROP)
      IMPLICIT NONE
      CLASS(T_RK3RD), INTENT(INOUT) :: UPDATE
      TYPE(T_GRID), INTENT(IN) :: GRID
      TYPE(T_VARIABLE), INTENT(INOUT) :: VARIABLE
      TYPE(T_EOS), INTENT(IN) :: EOS
      TYPE(T_PROP), INTENT(IN) :: PROP
      INTEGER :: I,J,K,N,L,M
      REAL(8) :: NX1(3),NX2(3),NX3(3),NX4(3),NX5(3),NX6(3)
      REAL(8) :: PV(UPDATE%NPV),DV(UPDATE%NDV),TV(UPDATE%NTV)
      REAL(8) :: GRD(UPDATE%NGRD),DT
      REAL(8) :: DPV(UPDATE%NPV)
      REAL(8) :: X(7,UPDATE%NPV)
      
      
      DO M=1,3
        CALL UPDATE%BC%SETBC(GRID,VARIABLE,EOS,PROP)
        IF(M.EQ.1) CALL UPDATE%TIMESTEP%CALTIMESTEP(GRID,VARIABLE)
        CALL UPDATE%RHS%CALRHS(GRID,VARIABLE,EOS)
        DO K=2,UPDATE%KMAX
          DO J=2,UPDATE%JMAX
            DO I=2,UPDATE%IMAX
              IF(M.EQ.1) UPDATE%RK(:,I,J,K) = VARIABLE%GETPV(I,J,K)
              NX1 = GRID%GETCX(I-1,J,K)
              NX2 = GRID%GETCX(I,J,K)
              NX3 = GRID%GETEX(I,J-1,K)
              NX4 = GRID%GETEX(I,J,K)
              NX5 = GRID%GETTX(I,J,K-1)
              NX6 = GRID%GETTX(I,J,K)
              GRD = GRID%GETGRD(I,J,K)
              PV = VARIABLE%GETPV(I,J,K)
              DV = VARIABLE%GETDV(I,J,K)
              TV = VARIABLE%GETTV(I,J,K)
              DT = UPDATE%TIMESTEP%GETDT(I,J,K)
            
              CALL UPDATE%LHS%SETNORM(NX1,NX2,NX3,NX4,NX5,NX6)
              CALL UPDATE%LHS%SETGRD(GRD)
              CALL UPDATE%LHS%SETPV(PV)
              CALL UPDATE%LHS%SETDV(DV)
              CALL UPDATE%LHS%SETTV(TV)
              CALL UPDATE%LHS%SETDT(DT)
              CALL UPDATE%LHS%INVERSE()
            
              DPV = 0.D0
              DO N=1,UPDATE%NPV
                DO L=1,UPDATE%NPV
                  DPV(N) = DPV(N) + UPDATE%LHS%GETX(N,L)*UPDATE%RHS%GETRES(L,I,J,K)
                END DO
              END DO
            
              PV = UPDATE%A1(M)*UPDATE%RK(:,I,J,K) + UPDATE%A2(M)*PV + UPDATE%A3(M)*DPV
          
              PV(1) = DMAX1(-UPDATE%PREF+100.D0,PV(1))
              PV(6) = DMIN1(1.D0,DMAX1(0.D0,PV(6)))
              IF(UPDATE%L_TURB) THEN
                PV(8) = DMAX1(PV(8),UPDATE%KREF)
                PV(9) = DMAX1(PV(9),UPDATE%OREF,UPDATE%RHS%GETOMEGA_CUT(I,J,K))
              END IF
              DO N=1,UPDATE%NPV
                CALL VARIABLE%SETPV(N,I,J,K,PV(N))
              END DO
                     
              CALL EOS%DETEOS(PV(1)+UPDATE%PREF,PV(5),PV(6),PV(7),DV)
            
              DO N=1,UPDATE%NDV
                CALL VARIABLE%SETDV(N,I,J,K,DV(N))
              END DO
            
              IF(UPDATE%L_TV) THEN
                CALL PROP%DETPROP(DV(3),DV(4),DV(5),PV(5),PV(6),PV(7),TV(1:2))
              END IF
          
              DO N=1,UPDATE%NTV
                CALL VARIABLE%SETTV(N,I,J,K,TV(N))
              END DO
              
              IF(UPDATE%L_EDDY) THEN
                X(1,:) = VARIABLE%GETPV(I-1,J,K)
                X(2,:) = VARIABLE%GETPV(I,J,K)
                X(3,:) = VARIABLE%GETPV(I+1,J,K)
                X(4,:) = VARIABLE%GETPV(I,J-1,K)
                X(5,:) = VARIABLE%GETPV(I,J+1,K)
                X(6,:) = VARIABLE%GETPV(I,J,K-1)
                X(7,:) = VARIABLE%GETPV(I,J,K+1) 
                CALL UPDATE%EDDY%SETNORM(NX1,NX2,NX3,NX4,NX5,NX6)
                CALL UPDATE%EDDY%SETGRD(GRD)
                CALL UPDATE%EDDY%SETPV(X)
                CALL UPDATE%EDDY%SETDV(DV)
                CALL UPDATE%EDDY%SETTV(TV)
                TV(3) = UPDATE%EDDY%CALEDDY()
                CALL VARIABLE%SETTV(3,I,J,K,TV(3))
              END IF

            END DO
          END DO
        END DO
      END DO
      
    END SUBROUTINE RK3RD
    !CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
    SUBROUTINE LUSGS(UPDATE,GRID,VARIABLE,EOS,PROP)
      IMPLICIT NONE
      CLASS(T_LUSGS), INTENT(INOUT) :: UPDATE
      TYPE(T_GRID), INTENT(IN) :: GRID
      TYPE(T_VARIABLE), INTENT(INOUT) :: VARIABLE
      TYPE(T_EOS), INTENT(IN) :: EOS
      TYPE(T_PROP), INTENT(IN) :: PROP
      INTEGER :: I,J,K,N,L
      REAL(8) :: NX1(3),NX2(3),NX3(3),NX4(3),NX5(3),NX6(3)
      REAL(8) :: PV(UPDATE%NPV),DV(UPDATE%NDV),TV(UPDATE%NTV)
      REAL(8) :: GRD(UPDATE%NGRD),DT,C(4),T(4)
      REAL(8) :: X(7,UPDATE%NPV)
            
      CALL UPDATE%BC%SETBC(GRID,VARIABLE,EOS,PROP)
      CALL UPDATE%TIMESTEP%CALTIMESTEP(GRID,VARIABLE)
      CALL UPDATE%RHS%CALRHS(GRID,VARIABLE,EOS)
      
      UPDATE%DCV = 0.D0
      UPDATE%DQS = 0.D0
      DO K=2,UPDATE%KMAX
        DO J=2,UPDATE%JMAX
          DO I=2,UPDATE%IMAX
            NX1 = GRID%GETCX(I-1,J,K)
            NX2 = GRID%GETCX(I,J,K)
            NX3 = GRID%GETEX(I,J-1,K)
            NX4 = GRID%GETEX(I,J,K)
            NX5 = GRID%GETTX(I,J,K-1)
            NX6 = GRID%GETTX(I,J,K)
            GRD = GRID%GETGRD(I,J,K)
            PV = VARIABLE%GETPV(I,J,K)
            DV = VARIABLE%GETDV(I,J,K)
            TV = VARIABLE%GETTV(I,J,K)
            DT = UPDATE%TIMESTEP%GETDT(I,J,K)
            
            CALL UPDATE%LHS%SETNORM(NX1,NX2,NX3,NX4,NX5,NX6)
            CALL UPDATE%LHS%SETGRD(GRD)
            CALL UPDATE%LHS%SETPV(PV)
            CALL UPDATE%LHS%SETDV(DV)
            CALL UPDATE%LHS%SETTV(TV)
            CALL UPDATE%LHS%SETDT(DT)
            IF(UPDATE%L_CAV) THEN
              C = UPDATE%RHS%GETICAV(I,J,K)
              CALL UPDATE%LHS%SETC(C)
            END IF
            IF(UPDATE%L_TURB) THEN
              T = UPDATE%RHS%GETITT(I,J,K)
              CALL UPDATE%LHS%SETT(T)
            END IF
            CALL UPDATE%LHS%INVERSE()
            
            DO N=1,UPDATE%NPV
              DO L=1,UPDATE%NPV
                UPDATE%INV(N,L,I,J,K) = UPDATE%LHS%GETX(N,L)
              END DO
            END DO
            
            NX1 = GRID%GETCX(I-1,J,K)
            GRD = GRID%GETGRD(I-1,J,K)
            PV = VARIABLE%GETPV(I-1,J,K)
            DV = VARIABLE%GETDV(I-1,J,K)
            TV = VARIABLE%GETTV(I-1,J,K)
            CALL UPDATE%JAC%SETNORM(NX1)
            CALL UPDATE%JAC%SETGRD(GRD)
            CALL UPDATE%JAC%SETPV(PV)
            CALL UPDATE%JAC%SETDV(DV)
            CALL UPDATE%JAC%SETTV(TV)
            CALL UPDATE%JAC%CALJAC(1)
            
            DO N=1,UPDATE%NPV
              UPDATE%DCV(N,I,J,K) = UPDATE%RHS%GETRES(N,I,J,K)
              DO L=1,UPDATE%NPV
                UPDATE%DCV(N,I,J,K) = UPDATE%DCV(N,I,J,K) + UPDATE%JAC%GETA(N,L)*UPDATE%DQS(L,I-1,J,K)
              END DO
            END DO
            
            NX1 = GRID%GETEX(I,J-1,K)
            GRD = GRID%GETGRD(I,J-1,K)
            PV = VARIABLE%GETPV(I,J-1,K)
            DV = VARIABLE%GETDV(I,J-1,K)
            TV = VARIABLE%GETTV(I,J-1,K)          
            CALL UPDATE%JAC%SETNORM(NX1)
            CALL UPDATE%JAC%SETGRD(GRD)
            CALL UPDATE%JAC%SETPV(PV)
            CALL UPDATE%JAC%SETDV(DV)
            CALL UPDATE%JAC%SETTV(TV)
            CALL UPDATE%JAC%CALJAC(1)
        
            DO N=1,UPDATE%NPV
              DO L=1,UPDATE%NPV
                UPDATE%DCV(N,I,J,K) = UPDATE%DCV(N,I,J,K) + UPDATE%JAC%GETA(N,L)*UPDATE%DQS(L,I,J-1,K)
              END DO
            END DO

            NX1 = GRID%GETTX(I,J,K-1)
            GRD = GRID%GETGRD(I,J,K-1)
            PV = VARIABLE%GETPV(I,J,K-1)
            DV = VARIABLE%GETDV(I,J,K-1)
            TV = VARIABLE%GETTV(I,J,K-1)          
            CALL UPDATE%JAC%SETNORM(NX1)
            CALL UPDATE%JAC%SETGRD(GRD)
            CALL UPDATE%JAC%SETPV(PV)
            CALL UPDATE%JAC%SETDV(DV)
            CALL UPDATE%JAC%SETTV(TV)
            CALL UPDATE%JAC%CALJAC(1)
        
            DO N=1,UPDATE%NPV
              DO L=1,UPDATE%NPV
                UPDATE%DCV(N,I,J,K) = UPDATE%DCV(N,I,J,K) + UPDATE%JAC%GETA(N,L)*UPDATE%DQS(L,I,J,K-1)
              END DO
            END DO
            
            DO N=1,UPDATE%NPV
              DO L=1,UPDATE%NPV
                UPDATE%DQS(N,I,J,K) = UPDATE%DQS(N,I,J,K) + UPDATE%INV(N,L,I,J,K)*UPDATE%DCV(L,I,J,K)
              END DO
            END DO
            
          END DO
        END DO
      END DO
      UPDATE%DQS = 0.D0
      DO K=UPDATE%KMAX,2,-1
        DO J=UPDATE%JMAX,2,-1
          DO I=UPDATE%IMAX,2,-1
            
            NX1 = GRID%GETCX(I,J,K)
            GRD = GRID%GETGRD(I+1,J,K)
            PV = VARIABLE%GETPV(I+1,J,K)
            DV = VARIABLE%GETDV(I+1,J,K)
            TV = VARIABLE%GETTV(I+1,J,K)
            CALL UPDATE%JAC%SETNORM(NX1)
            CALL UPDATE%JAC%SETGRD(GRD)
            CALL UPDATE%JAC%SETPV(PV)
            CALL UPDATE%JAC%SETDV(DV)
            CALL UPDATE%JAC%SETTV(TV)
            CALL UPDATE%JAC%CALJAC(-1)
        
            DO N=1,UPDATE%NPV
              DO L=1,UPDATE%NPV
                UPDATE%DCV(N,I,J,K) = UPDATE%DCV(N,I,J,K) - UPDATE%JAC%GETA(N,L)*UPDATE%DQS(L,I+1,J,K)
              END DO
            END DO
            
            NX1 = GRID%GETEX(I,J,K)
            GRD = GRID%GETGRD(I,J+1,K)
            PV = VARIABLE%GETPV(I,J+1,K)
            DV = VARIABLE%GETDV(I,J+1,K)
            TV = VARIABLE%GETTV(I,J+1,K)          
            CALL UPDATE%JAC%SETNORM(NX1)
            CALL UPDATE%JAC%SETGRD(GRD)
            CALL UPDATE%JAC%SETPV(PV)
            CALL UPDATE%JAC%SETDV(DV)
            CALL UPDATE%JAC%SETTV(TV)
            CALL UPDATE%JAC%CALJAC(-1)
            
            DO N=1,UPDATE%NPV
              DO L=1,UPDATE%NPV
                UPDATE%DCV(N,I,J,K) = UPDATE%DCV(N,I,J,K) - UPDATE%JAC%GETA(N,L)*UPDATE%DQS(L,I,J+1,K)
              END DO
            END DO

            NX1 = GRID%GETTX(I,J,K)
            GRD = GRID%GETGRD(I,J,K+1)
            PV = VARIABLE%GETPV(I,J,K+1)
            DV = VARIABLE%GETDV(I,J,K+1)
            TV = VARIABLE%GETTV(I,J,K+1)          
            CALL UPDATE%JAC%SETNORM(NX1)
            CALL UPDATE%JAC%SETGRD(GRD)
            CALL UPDATE%JAC%SETPV(PV)
            CALL UPDATE%JAC%SETDV(DV)
            CALL UPDATE%JAC%SETTV(TV)
            CALL UPDATE%JAC%CALJAC(-1)
            
            DO N=1,UPDATE%NPV
              DO L=1,UPDATE%NPV
                UPDATE%DCV(N,I,J,K) = UPDATE%DCV(N,I,J,K) - UPDATE%JAC%GETA(N,L)*UPDATE%DQS(L,I,J,K+1)
              END DO
            END DO
            
            DO N=1,UPDATE%NPV
              DO L=1,UPDATE%NPV
                UPDATE%DQS(N,I,J,K) = UPDATE%DQS(N,I,J,K) + UPDATE%INV(N,L,I,J,K)*UPDATE%DCV(L,I,J,K)
              END DO
            END DO
            
          END DO
        END DO
      END DO
      DO K=2,UPDATE%KMAX
        DO J=2,UPDATE%JMAX
          DO I=2,UPDATE%IMAX
            PV = VARIABLE%GETPV(I,J,K) + UPDATE%DQS(:,I,J,K)
            
            PV(1) = DMAX1(-UPDATE%PREF+100.D0,PV(1))
            PV(6) = DMIN1(1.D0,DMAX1(0.D0,PV(6)))
            IF(UPDATE%L_TURB) THEN
              PV(8) = DMAX1(PV(8),UPDATE%KREF)
              PV(9) = DMAX1(PV(9),UPDATE%OREF,UPDATE%RHS%GETOMEGA_CUT(I,J,K))
            END IF
            
            DO N=1,UPDATE%NPV
              CALL VARIABLE%SETPV(N,I,J,K,PV(N))
            END DO
            
            CALL EOS%DETEOS(PV(1)+UPDATE%PREF,PV(5),PV(6),PV(7),DV)
            
            DO N=1,UPDATE%NDV
              CALL VARIABLE%SETDV(N,I,J,K,DV(N))
            END DO
            
            IF(UPDATE%L_TV) THEN
              CALL PROP%DETPROP(DV(3),DV(4),DV(5),PV(5),PV(6),PV(7),TV(1:2))
            END IF
        
        
            DO N=1,UPDATE%NTV
              CALL VARIABLE%SETTV(N,I,J,K,TV(N))
            END DO
            
            IF(UPDATE%L_EDDY) THEN
              NX1 = GRID%GETCX(I-1,J,K)
              NX2 = GRID%GETCX(I,J,K)
              NX3 = GRID%GETEX(I,J-1,K)
              NX4 = GRID%GETEX(I,J,K)
              NX5 = GRID%GETTX(I,J,K-1)
              NX6 = GRID%GETTX(I,J,K)
              GRD = GRID%GETGRD(I,J,K)
              X(1,:) = VARIABLE%GETPV(I-1,J,K)
              X(2,:) = VARIABLE%GETPV(I,J,K)
              X(3,:) = VARIABLE%GETPV(I+1,J,K)
              X(4,:) = VARIABLE%GETPV(I,J-1,K)
              X(5,:) = VARIABLE%GETPV(I,J+1,K)
              X(6,:) = VARIABLE%GETPV(I,J,K-1)
              X(7,:) = VARIABLE%GETPV(I,J,K+1) 
              DV = VARIABLE%GETDV(I,J,K)
              TV = VARIABLE%GETTV(I,J,K)
              CALL UPDATE%EDDY%SETNORM(NX1,NX2,NX3,NX4,NX5,NX6)
              CALL UPDATE%EDDY%SETGRD(GRD)
              CALL UPDATE%EDDY%SETPV(X)
              CALL UPDATE%EDDY%SETDV(DV)
              CALL UPDATE%EDDY%SETTV(TV)
              TV(3) = UPDATE%EDDY%CALEDDY()
              CALL VARIABLE%SETTV(3,I,J,K,TV(3))
            END IF
        
            
          END DO
        END DO
      END DO
    END SUBROUTINE LUSGS
    !CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
END MODULE UPDATE_MODULE