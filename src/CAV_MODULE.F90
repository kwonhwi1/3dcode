MODULE CAV_MODULE
  USE CONFIG_MODULE
  USE VARIABLE_MODULE
  USE GRID_MODULE
  USE EOS_MODULE
  IMPLICIT NONE
  PRIVATE
  PUBLIC :: T_CAV,T_MERKLE,T_KUNZ,T_SINGHAL
  
  TYPE, ABSTRACT :: T_CAV
    PRIVATE
    INTEGER :: NDV,NPV,NGRD
    REAL(8) :: PREF
    LOGICAL :: AXI
    REAL(8) :: UREF,CHORD_LENGTH,RHO,C_C,C_V
    REAL(8), POINTER :: PV(:),DV(:),GRD(:)
    CONTAINS
      PROCEDURE :: CONSTRUCT
      PROCEDURE :: DESTRUCT
      PROCEDURE :: SETGRD       ! (GRD) VOL,XCEN,YCEN,YDNS
      PROCEDURE :: SETPV        ! (PV)  P,U,V,T,Y1,Y2,K,O
      PROCEDURE :: SETDV        ! (DV)  RHO,H,RHOL,RHOV,RHOG,SND2,DRDP,DRDT,DRDY1,DRDY2,DHDP,DHDT,DHDY1,DHDY2,DRDPV,DRDTV,DRDPL,DRDTL
      PROCEDURE(P_CAVSOURCE), DEFERRED :: CAVSOURCE
  END TYPE T_CAV
  
  TYPE, EXTENDS(T_CAV) :: T_MERKLE
    CONTAINS
      PROCEDURE :: CAVSOURCE => MERKLE
  END TYPE T_MERKLE
  
  TYPE, EXTENDS(T_CAV) :: T_KUNZ
    CONTAINS
      PROCEDURE :: CAVSOURCE => KUNZ
  END TYPE T_KUNZ
  
  TYPE, EXTENDS(T_CAV) :: T_SINGHAL
    CONTAINS
      PROCEDURE :: CAVSOURCE => SINGHAL
  END TYPE T_SINGHAL
  
  ABSTRACT INTERFACE
    FUNCTION P_CAVSOURCE(CAV,EOS) RESULT(MDOT)
      IMPORT T_CAV
      IMPORT T_EOS
      IMPLICIT NONE
      CLASS(T_CAV), INTENT(IN) :: CAV
      TYPE(T_EOS),INTENT(IN) :: EOS
      REAL(8):: MDOT(0:4)
    END FUNCTION P_CAVSOURCE
  END INTERFACE
  
  CONTAINS
    !CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
    SUBROUTINE CONSTRUCT(CAV,CONFIG,GRID,VARIABLE)
      IMPLICIT NONE
      CLASS(T_CAV), INTENT(OUT) :: CAV
      TYPE(T_CONFIG), INTENT(IN) :: CONFIG
      TYPE(T_GRID), INTENT(IN) :: GRID
      TYPE(T_VARIABLE), INTENT(IN) :: VARIABLE
      
      SELECT CASE(CONFIG%GETNAXI())
      CASE(0)
        CAV%AXI = .FALSE.
      CASE(1)
        CAV%AXI = .TRUE.
      END SELECT
      
      CAV%PREF         = CONFIG%GETPREF()
      CAV%UREF         = CONFIG%GETUREF()
      CAV%CHORD_LENGTH = CONFIG%GETL_CHORD()
      CAV%RHO          = CONFIG%GETRHOREF()
      CAV%C_C          = CONFIG%GETC_C()
      CAV%C_V          = CONFIG%GETC_V()
         
      CAV%NGRD         = GRID%GETNGRD()
      CAV%NPV          = VARIABLE%GETNPV()      
      CAV%NDV          = VARIABLE%GETNDV()

    END SUBROUTINE CONSTRUCT
    !CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
    SUBROUTINE DESTRUCT(CAV)
      IMPLICIT NONE
      CLASS(T_CAV), INTENT(INOUT) :: CAV
      
      IF(ASSOCIATED(CAV%GRD))  NULLIFY(CAV%GRD) 
      IF(ASSOCIATED(CAV%PV))   NULLIFY(CAV%PV) 
      IF(ASSOCIATED(CAV%DV))   NULLIFY(CAV%DV)

    END SUBROUTINE DESTRUCT
    !CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
    SUBROUTINE SETGRD(CAV,GRD)
      IMPLICIT NONE
      CLASS(T_CAV), INTENT(INOUT) :: CAV
      REAL(8), INTENT(IN), TARGET :: GRD(CAV%NGRD)
      
      CAV%GRD => GRD
      
    END SUBROUTINE SETGRD
    !CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
    SUBROUTINE SETPV(CAV,PV)
      IMPLICIT NONE
      CLASS(T_CAV), INTENT(INOUT) :: CAV
      REAL(8), INTENT(IN), TARGET :: PV(CAV%NPV)
      
      CAV%PV => PV

    END SUBROUTINE SETPV
    !CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
    SUBROUTINE SETDV(CAV,DV)
      IMPLICIT NONE
      CLASS(T_CAV), INTENT(INOUT) :: CAV
      REAL(8), INTENT(IN), TARGET :: DV(CAV%NDV)
      
      CAV%DV  => DV
    
    END SUBROUTINE SETDV 
    !CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
    FUNCTION MERKLE(CAV,EOS) RESULT(MDOT)
      IMPLICIT NONE
      CLASS(T_MERKLE), INTENT(IN) :: CAV
      TYPE(T_EOS), INTENT(IN) :: EOS
      REAL(8):: MDOT(0:4)
      REAL(8) :: R_C,R_V,PWW,LAM
      REAL(8), PARAMETER :: PHI = 1.D0
      
      MDOT = 0.D0
      PWW = EOS%GET_PWW(CAV%PV(4))
      
      R_C = CAV%C_C*CAV%DV(1)*CAV%PV(5)*DMAX1(CAV%PV(1)+CAV%PREF-PWW,0.D0) &
          /(0.5D0*CAV%RHO*CAV%UREF**2)/(CAV%CHORD_LENGTH/CAV%UREF)
      R_V = CAV%C_V*CAV%DV(1)*(1.D0-CAV%PV(5)-CAV%PV(6))*DMAX1(PWW-CAV%PV(1)-CAV%PREF,0.D0) &
          /(0.5D0*CAV%RHO*CAV%UREF**2)/(CAV%CHORD_LENGTH/CAV%UREF)
           
      MDOT(0) = (R_V - R_C)*CAV%GRD(1)
      IF(CAV%AXI) MDOT(0) = MDOT(0)*CAV%GRD(3)
      
      IF(R_C.NE.0.D0) THEN
        MDOT(1) = - R_C*(1.D0/(CAV%PV(1)+CAV%PREF-PWW)+CAV%DV(7)/CAV%DV(1))
        MDOT(2) = - R_C*CAV%DV(8)/CAV%DV(1)
        MDOT(3) = - R_C*(1.D0/CAV%PV(5)+CAV%DV(9)/CAV%DV(1))
        MDOT(4) = - R_C*CAV%DV(10)/CAV%DV(1)
      END IF
      IF(R_V.NE.0.D0) THEN
        MDOT(1) = R_V*(-1.D0/(PWW-CAV%PV(1)-CAV%PREF)+CAV%DV(7)/CAV%DV(1))
        MDOT(2) = R_V*CAV%DV(8)/CAV%DV(1)
        MDOT(3) = R_V*(CAV%DV(9)/CAV%DV(1)-1.D0/(1.D0-CAV%PV(5)-CAV%PV(6)))
        MDOT(4) = R_V*(CAV%DV(10)/CAV%DV(1)-1.D0/(1.D0-CAV%PV(5)-CAV%PV(6)))
      END IF
      LAM = DSIGN(1.D0,MDOT(3))
      MDOT(1:4) = MDOT(1:4)*(PHI*(1.D0-0.5D0*(1.D0-LAM))-0.5D0*(1.D0-LAM))
    END FUNCTION MERKLE
    !CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
    FUNCTION KUNZ(CAV,EOS) RESULT(MDOT)
      IMPLICIT NONE
      CLASS(T_KUNZ), INTENT(IN) :: CAV
      TYPE(T_EOS), INTENT(IN) :: EOS
      REAL(8):: MDOT(0:4)
      REAL(8) :: R_C,R_V,AL,AV,AG,PWW,AV1,LAM
      REAL(8), PARAMETER :: PHI = 1.D0

      MDOT = 0.D0
      PWW = EOS%GET_PWW(CAV%PV(4))
      
      AV = CAV%DV(1)*CAV%PV(5)/CAV%DV(4)
      AG = CAV%DV(1)*CAV%PV(6)/CAV%DV(5)
      AL = 1.D0 - AV - AG

      R_C = CAV%C_C*CAV%DV(4)*AV*(AL+AG)**2/(CAV%CHORD_LENGTH/CAV%UREF)
      R_V = CAV%C_V*CAV%DV(1)*(1.D0-CAV%PV(5)-CAV%PV(6))*DMAX1(PWW-CAV%PV(1)-CAV%PREF,0.D0) &
          /(0.5D0*CAV%RHO*CAV%UREF**2)/(CAV%CHORD_LENGTH/CAV%UREF)

      MDOT(0) = (R_V - R_C)*CAV%GRD(1)
      IF(CAV%AXI) MDOT(0) = MDOT(0)*CAV%GRD(3)
      
      AV1 = 1.D0-4.D0*AV+3.D0*AV**2
      IF(R_C.NE.0.D0 ) THEN
        MDOT(1) = - CAV%C_C/(CAV%CHORD_LENGTH/CAV%UREF)*(CAV%DV(7)*CAV%PV(5)*AV1+2.D0*AV**2*CAV%DV(15)*(AL+AG))
        MDOT(2) = - CAV%C_C/(CAV%CHORD_LENGTH/CAV%UREF)*(CAV%DV(8)*CAV%PV(5)*AV1+2.D0*AV**2*CAV%DV(16)*(AL+AG))
        MDOT(3) = - CAV%C_C/(CAV%CHORD_LENGTH/CAV%UREF)*(CAV%DV(9)*CAV%PV(5)+CAV%DV(1))*AV1
        MDOT(4) = - CAV%C_C/(CAV%CHORD_LENGTH/CAV%UREF)*CAV%DV(10)*CAV%PV(5)*AV1
      END IF
      IF(R_V.NE.0.D0) THEN
        MDOT(1) = R_V*(-1.D0/(PWW-CAV%PV(1)-CAV%PREF)+CAV%DV(7)/CAV%DV(1))
        MDOT(2) = R_V*CAV%DV(8)/CAV%DV(1)
        MDOT(3) = R_V*(CAV%DV(9)/CAV%DV(1)-1.D0/(1.D0-CAV%PV(5)-CAV%PV(6)))
        MDOT(4) = R_V*(CAV%DV(10)/CAV%DV(1)-1.D0/(1.D0-CAV%PV(5)-CAV%PV(6)))
      END IF
      LAM = DSIGN(1.D0,MDOT(3))
      MDOT(1:4) = MDOT(1:4)*(PHI*(1.D0-0.5D0*(1.D0-LAM))-0.5D0*(1.D0-LAM))
    END FUNCTION KUNZ  
    !CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
    FUNCTION SINGHAL(CAV,EOS) RESULT(MDOT)
      IMPLICIT NONE
      CLASS(T_SINGHAL), INTENT(IN) :: CAV
      TYPE(T_EOS), INTENT(IN) :: EOS
      REAL(8):: MDOT(0:4)
      REAL(8) :: R_C,R_V,PWW,SIGMA,LAM
      REAL(8), PARAMETER :: PHI = 1.D0
      
      MDOT = 0.D0
      PWW = EOS%GET_PWW(CAV%PV(4))
      SIGMA = EOS%GET_SIGMA(CAV%PV(4))
      
      R_C = CAV%C_C*CAV%UREF/SIGMA*CAV%DV(3)*CAV%DV(4) &
          *DSQRT(2.D0/3.D0*DMAX1(CAV%PV(1)+CAV%PREF-PWW,0.D0)/CAV%DV(3))*CAV%PV(5)
      R_V = CAV%C_V*CAV%UREF/SIGMA*CAV%DV(3)*CAV%DV(4) &
          *DSQRT(2.D0/3.D0*DMAX1(PWW-CAV%PV(1)-CAV%PREF,0.D0)/CAV%DV(3))*(1.D0-CAV%PV(5)-CAV%PV(6))

      MDOT(0) = (R_V - R_C)*CAV%GRD(1)
      IF(CAV%AXI) MDOT(0) = MDOT(0)*CAV%GRD(3)
      
      IF(R_C.NE.0.D0 ) THEN
        MDOT(1) = - R_C*(CAV%DV(15)/CAV%DV(4)+0.5D0*CAV%DV(17)/CAV%DV(3)+0.5D0/(CAV%PV(1)+CAV%PREF-PWW))
        MDOT(2) = - R_C*(CAV%DV(16)/CAV%DV(4)+0.5D0*CAV%DV(18)/CAV%DV(3))
        MDOT(3) = - R_C*1.D0/CAV%PV(5)
        MDOT(4) = 0.D0
      END IF
      IF(R_V.NE.0.D0 ) THEN
        MDOT(1) = R_V*(CAV%DV(15)/CAV%DV(4)+0.5D0*CAV%DV(17)/CAV%DV(3)-0.5D0/(PWW-CAV%PV(1)-CAV%PREF))
        MDOT(2) = R_V*(CAV%DV(16)/CAV%DV(4)+0.5D0*CAV%DV(18)/CAV%DV(3))
        MDOT(3) = - R_V*1.D0/(1.D0-CAV%PV(5)-CAV%PV(6))
        MDOT(4) = - R_V*1.D0/(1.D0-CAV%PV(5)-CAV%PV(6))
      END IF
      LAM = DSIGN(1.D0,MDOT(3))
      MDOT(1:4) = MDOT(1:4)*(PHI*(1.D0-0.5D0*(1.D0-LAM))-0.5D0*(1.D0-LAM))
    END FUNCTION SINGHAL
    !CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
END MODULE CAV_MODULE
    