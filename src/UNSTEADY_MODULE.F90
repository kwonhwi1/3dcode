MODULE UNSTEADY_MODULE
  USE CONFIG_MODULE
  USE GRID_MODULE
  USE VARIABLE_MODULE
  IMPLICIT NONE
  PRIVATE
  PUBLIC :: T_UNSTEADY
 
  TYPE T_UNSTEADY
    PRIVATE
    INTEGER :: NPV,NDV,NGRD
    REAL(8) :: PREF,DT_PHY
    REAL(8), POINTER :: PV(:),DV(:),GRD(:),QQ1(:),QQ2(:) 
    CONTAINS
      PROCEDURE :: CONSTRUCT
      PROCEDURE :: DESTRUCT
      PROCEDURE :: SETGRD       ! (GRD) VOL,XCEN,YCEN,ZCEN,YDNS
      PROCEDURE :: SETPV        ! (PV) P,U,V,W,T,Y1,Y2,K,O
      PROCEDURE :: SETDV        ! (DV) RHO,H,RHOL,RHOV,RHOG,SND2,DRDP,DRDT,DRDY1,DRDY2,DHDP,DHDT,DHDY1,DHDY2,DRDPV,DRDTV,DRDPL,DRDTL
      PROCEDURE :: SETQQ        ! (QQ1,QQ2)
      PROCEDURE :: UNSTEADYSOURCE
  END TYPE T_UNSTEADY

  CONTAINS
    !CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
    SUBROUTINE CONSTRUCT(UNSTEADY,CONFIG,GRID,VARIABLE)
      IMPLICIT NONE
      CLASS(T_UNSTEADY), INTENT(OUT) :: UNSTEADY
      TYPE(T_CONFIG), INTENT(IN) :: CONFIG
      TYPE(T_GRID), INTENT(IN) :: GRID
      TYPE(T_VARIABLE), INTENT(IN) :: VARIABLE
 
      UNSTEADY%PREF = CONFIG%GETPREF()
      UNSTEADY%DT_PHY = CONFIG%GETDT_PHY()
      
      UNSTEADY%NGRD = GRID%GETNGRD()

      UNSTEADY%NPV = VARIABLE%GETNPV()
      UNSTEADY%NDV = VARIABLE%GETNDV()
      

      
    END SUBROUTINE CONSTRUCT
    !CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
    SUBROUTINE DESTRUCT(UNSTEADY)
      IMPLICIT NONE
      CLASS(T_UNSTEADY), INTENT(INOUT) :: UNSTEADY
      
      IF(ASSOCIATED(UNSTEADY%GRD)) NULLIFY(UNSTEADY%GRD)      
      IF(ASSOCIATED(UNSTEADY%PV))  NULLIFY(UNSTEADY%PV) 
      IF(ASSOCIATED(UNSTEADY%DV))  NULLIFY(UNSTEADY%DV) 
      IF(ASSOCIATED(UNSTEADY%QQ1)) NULLIFY(UNSTEADY%QQ1)
      IF(ASSOCIATED(UNSTEADY%QQ2)) NULLIFY(UNSTEADY%QQ2)

    END SUBROUTINE DESTRUCT
    !CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
    SUBROUTINE SETGRD(UNSTEADY,GRD)
      IMPLICIT NONE
      CLASS(T_UNSTEADY), INTENT(INOUT) :: UNSTEADY
      REAL(8), INTENT(IN), TARGET :: GRD(UNSTEADY%NGRD)
      
      UNSTEADY%GRD => GRD
      
    END SUBROUTINE SETGRD
    !CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
    SUBROUTINE SETPV(UNSTEADY,PV)
      IMPLICIT NONE
      CLASS(T_UNSTEADY), INTENT(INOUT) :: UNSTEADY
      REAL(8), INTENT(IN), TARGET :: PV(UNSTEADY%NPV)
      
      UNSTEADY%PV => PV
      
    END SUBROUTINE SETPV
    !CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
    SUBROUTINE SETDV(UNSTEADY,DV)
      IMPLICIT NONE
      CLASS(T_UNSTEADY), INTENT(INOUT) :: UNSTEADY
      REAL(8), INTENT(IN), TARGET :: DV(UNSTEADY%NDV)
      
      UNSTEADY%DV => DV
      
    END SUBROUTINE SETDV
    !CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
    SUBROUTINE SETQQ(UNSTEADY,QQ1,QQ2)
      IMPLICIT NONE
      CLASS(T_UNSTEADY), INTENT(INOUT) :: UNSTEADY
      REAL(8), INTENT(IN), TARGET :: QQ1(UNSTEADY%NPV),QQ2(UNSTEADY%NPV)
      
      UNSTEADY%QQ1 => QQ1
      UNSTEADY%QQ2 => QQ2
      
    END SUBROUTINE SETQQ
    !CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
    FUNCTION UNSTEADYSOURCE(UNSTEADY) RESULT(FX)
      IMPLICIT NONE
      CLASS(T_UNSTEADY), INTENT(IN) :: UNSTEADY
      REAL(8) :: FX(UNSTEADY%NPV)
      INTEGER :: N
      
      FX(1) = UNSTEADY%DV(1)
      FX(2) = UNSTEADY%DV(1)*UNSTEADY%PV(2)
      FX(3) = UNSTEADY%DV(1)*UNSTEADY%PV(3)
      FX(4) = UNSTEADY%DV(1)*UNSTEADY%PV(4)
      FX(5) = UNSTEADY%DV(1)*(UNSTEADY%DV(2)+0.5D0*(UNSTEADY%PV(2)**2+UNSTEADY%PV(3)**2+UNSTEADY%PV(4)**2))-(UNSTEADY%PV(1)+UNSTEADY%PREF)
      DO N = 6,UNSTEADY%NPV
        FX(N) = UNSTEADY%DV(1)*UNSTEADY%PV(N)
      END DO
      
      FX = (1.5D0*FX - 2.D0*UNSTEADY%QQ1 + 0.5D0*UNSTEADY%QQ2)*UNSTEADY%GRD(1)/UNSTEADY%DT_PHY       
      
    END FUNCTION UNSTEADYSOURCE
    !CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
END MODULE UNSTEADY_MODULE