MODULE VSFLUX_MODULE
  USE CONFIG_MODULE
  USE GRID_MODULE
  USE VARIABLE_MODULE
  IMPLICIT NONE
  PRIVATE
  PUBLIC :: T_VSFLUX,T_VSFLUX_LAMINAR,T_VSFLUX_TURBULENT

  TYPE, ABSTRACT :: T_VSFLUX
    PRIVATE
    LOGICAL :: AXI
    INTEGER :: NPV,NTV,NGRD,NDV
    REAL(8) :: TDK1,TDK2,TDO1,TDO2
    REAL(8) :: PR_T
    REAL(8), POINTER :: PV(:,:)
    REAL(8), POINTER :: NX(:),TX1(:),TX2(:),TX3(:),TX4(:),R
    REAL(8), POINTER :: GRDL(:),GRDR(:)
    REAL(8), POINTER :: DVL(:),DVR(:)
    REAL(8), POINTER :: TVL(:),TVR(:)
    PROCEDURE(P_CALSKK), POINTER :: CALSKK
    PROCEDURE(P_CALBIGF), POINTER :: CALBIGF
    CONTAINS
      PROCEDURE :: CONSTRUCT  
      PROCEDURE :: DESTRUCT
      PROCEDURE :: SETNORM    ! (NX,TX1,TX2,TX3,TX4,R)
      PROCEDURE :: SETGRD     ! (GRDL,GRDR) VOL,XCEN,YCEN,YDNS
      PROCEDURE :: SETPV      ! (PV(14,NPV)) P,U,V,T,Y1,Y2,K,O
      PROCEDURE :: SETDV      ! (DVL,DVR) RHO,H,RHOL,RHOV,RHOG,SND2,DRDP,DRDT,DRDY1,DRDY2,DHDP,DHDT,DHDY1,DHDY2,DRDPV,DRDTV,DRDPL,DRDTL
      PROCEDURE :: SETTV      ! (TVL,TVR) VIS,COND,EMUT
      PROCEDURE(P_CALFLUX), DEFERRED :: CALFLUX
  END TYPE T_VSFLUX
 
  ABSTRACT INTERFACE
    SUBROUTINE P_CALFLUX(VSFLUX,FX)
      IMPORT T_VSFLUX
      IMPLICIT NONE
      CLASS(T_VSFLUX), INTENT(IN) :: VSFLUX
      REAL(8), INTENT(OUT) :: FX(VSFLUX%NPV)
    END SUBROUTINE P_CALFLUX
  END INTERFACE
  
  TYPE, EXTENDS(T_VSFLUX) :: T_VSFLUX_LAMINAR
    CONTAINS
    PROCEDURE :: CALFLUX => VSFLUX_LAMINAR
  END TYPE T_VSFLUX_LAMINAR
  
  TYPE, EXTENDS(T_VSFLUX) :: T_VSFLUX_TURBULENT
    CONTAINS
    PROCEDURE :: CALFLUX => VSFLUX_TURBULENT
  END TYPE T_VSFLUX_TURBULENT
  
  INTERFACE
    FUNCTION P_CALSKK(VSFLUX,DUDX,DVDY,V) RESULT(SKK)
      IMPORT T_VSFLUX
      IMPLICIT NONE
      CLASS(T_VSFLUX), INTENT(IN) :: VSFLUX
      REAL(8), INTENT(IN) :: DUDX,DVDY,V
      REAL(8) :: SKK
    END FUNCTION P_CALSKK
    FUNCTION P_CALBIGF(VSFLUX,PKW) RESULT(BIGF)
      IMPORT T_VSFLUX
      IMPLICIT NONE
      CLASS(T_VSFLUX), INTENT(IN) :: VSFLUX
      REAL(8), INTENT(IN) :: PKW
      REAL(8) :: BIGF
    END FUNCTION P_CALBIGF
  END INTERFACE
  
  CONTAINS
    !CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC 
    SUBROUTINE CONSTRUCT(VSFLUX,CONFIG,GRID,VARIABLE)
      IMPLICIT NONE
      CLASS(T_VSFLUX), INTENT(OUT) :: VSFLUX
      TYPE(T_CONFIG), INTENT(IN) :: CONFIG
      TYPE(T_GRID), INTENT(IN) :: GRID
      TYPE(T_VARIABLE), INTENT(IN) :: VARIABLE
      
      SELECT CASE(CONFIG%GETITURB())
      CASE(-1)
        VSFLUX%TDK1 = 0.D0
        VSFLUX%TDK2 = 1.D0
        VSFLUX%TDO1 = 0.D0
        VSFLUX%TDO2 = 1.D0/1.3D0
        VSFLUX%CALBIGF => KEPSILON
        VSFLUX%PR_T=0.9D0
      CASE(0)
        VSFLUX%TDK1 = 0.85D0
        VSFLUX%TDK2 = 1.D0
        VSFLUX%TDO1 = 0.5D0
        VSFLUX%TDO2 = 0.856D0
        VSFLUX%CALBIGF => KWSST
        VSFLUX%PR_T=0.9D0
      CASE(-2)
        VSFLUX%TDK1 = 0.D0
        VSFLUX%TDK2 = 0.D0
        VSFLUX%TDO1 = 0.D0
        VSFLUX%TDO2 = 0.D0
        VSFLUX%CALBIGF => NULL()
        VSFLUX%PR_T=0.D0
      END SELECT

      SELECT CASE(CONFIG%GETNAXI())
      CASE(0)
        VSFLUX%CALSKK => SKK_NOAXI
        VSFLUX%AXI = .FALSE.
      CASE(1)
        VSFLUX%CALSKK => SKK_AXI
        VSFLUX%AXI = .TRUE.
      END SELECT
      
      VSFLUX%NGRD = GRID%GETNGRD()
      
      VSFLUX%NPV = VARIABLE%GETNPV()
      VSFLUX%NDV = VARIABLE%GETNDV()
      VSFLUX%NTV = VARIABLE%GETNTV()
      
    END SUBROUTINE CONSTRUCT
    !CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC    
    SUBROUTINE DESTRUCT(VSFLUX)
      IMPLICIT NONE
      CLASS(T_VSFLUX), INTENT(INOUT) :: VSFLUX

      IF(ASSOCIATED(VSFLUX%NX))      NULLIFY(VSFLUX%NX)     
      IF(ASSOCIATED(VSFLUX%TX1))     NULLIFY(VSFLUX%TX1)    
      IF(ASSOCIATED(VSFLUX%TX2))     NULLIFY(VSFLUX%TX2)    
      IF(ASSOCIATED(VSFLUX%TX3))     NULLIFY(VSFLUX%TX3)    
      IF(ASSOCIATED(VSFLUX%TX4))     NULLIFY(VSFLUX%TX4)
      IF(ASSOCIATED(VSFLUX%R))       NULLIFY(VSFLUX%R)
      IF(ASSOCIATED(VSFLUX%GRDL))    NULLIFY(VSFLUX%GRDL)   
      IF(ASSOCIATED(VSFLUX%GRDR))    NULLIFY(VSFLUX%GRDR) 
      IF(ASSOCIATED(VSFLUX%PV))      NULLIFY(VSFLUX%PV)     
      IF(ASSOCIATED(VSFLUX%DVL))     NULLIFY(VSFLUX%DVL)    
      IF(ASSOCIATED(VSFLUX%DVR))     NULLIFY(VSFLUX%DVR)     
      IF(ASSOCIATED(VSFLUX%TVL))     NULLIFY(VSFLUX%TVL)    
      IF(ASSOCIATED(VSFLUX%TVR))     NULLIFY(VSFLUX%TVR)    
      IF(ASSOCIATED(VSFLUX%CALSKK))  NULLIFY(VSFLUX%CALSKK) 
      IF(ASSOCIATED(VSFLUX%CALBIGF)) NULLIFY(VSFLUX%CALBIGF)
      
    END SUBROUTINE DESTRUCT
    !CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
    SUBROUTINE SETNORM(VSFLUX,NX,TX1,TX2,TX3,TX4,R)
      IMPLICIT NONE
      CLASS(T_VSFLUX), INTENT(INOUT) :: VSFLUX
      REAL(8), INTENT(IN), TARGET :: NX(2),TX1(2),TX2(2),TX3(2),TX4(2),R
      
      VSFLUX%NX  => NX
      VSFLUX%TX1 => TX1
      VSFLUX%TX2 => TX2
      VSFLUX%TX3 => TX3
      VSFLUX%TX4 => TX4
      VSFLUX%R   => R
    
    END SUBROUTINE SETNORM 
    !CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
    SUBROUTINE SETGRD(VSFLUX,GRDL,GRDR)
      IMPLICIT NONE
      CLASS(T_VSFLUX), INTENT(INOUT) :: VSFLUX
      REAL(8), INTENT(IN), TARGET :: GRDL(VSFLUX%NGRD),GRDR(VSFLUX%NGRD)
      
      VSFLUX%GRDL => GRDL
      VSFLUX%GRDR => GRDR
      
    END SUBROUTINE SETGRD
    !CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
    SUBROUTINE SETPV(VSFLUX,PV)
      IMPLICIT NONE
      CLASS(T_VSFLUX), INTENT(INOUT) :: VSFLUX
      REAL(8), INTENT(IN), TARGET :: PV(14,VSFLUX%NPV) ! PV(7,:)~PV(14,:) ARE NOT USED !! PLZ CHECK
      
      VSFLUX%PV => PV
      
    END SUBROUTINE SETPV
    !CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC  
    SUBROUTINE SETDV(VSFLUX,DVL,DVR)
      IMPLICIT NONE
      CLASS(T_VSFLUX), INTENT(INOUT) :: VSFLUX
      REAL(8), INTENT(IN), TARGET :: DVL(VSFLUX%NDV),DVR(VSFLUX%NDV)
      
      VSFLUX%DVL => DVL
      VSFLUX%DVR => DVR
      
    END SUBROUTINE SETDV
    !CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
    SUBROUTINE SETTV(VSFLUX,TVL,TVR)
      IMPLICIT NONE
      CLASS(T_VSFLUX), INTENT(INOUT) :: VSFLUX
      REAL(8), INTENT(IN), TARGET :: TVL(VSFLUX%NTV),TVR(VSFLUX%NTV)
      
      VSFLUX%TVL => TVL
      VSFLUX%TVR => TVR
      
    END SUBROUTINE SETTV
    !CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
    SUBROUTINE VSFLUX_LAMINAR(VSFLUX,FX)
      IMPLICIT NONE
      CLASS(T_VSFLUX_LAMINAR), INTENT(IN) :: VSFLUX
      REAL(8), INTENT(OUT) :: FX(VSFLUX%NPV)
      REAL(8) :: TX,TY
      REAL(8) :: VOL,VIS,CON,UC,VC
      REAL(8) :: DUDN,DVDN,DTDN,DUDT,DVDT,DTDT
      REAL(8) :: DUDX,DVDX,DTDX,DUDY,DVDY,DTDY
      REAL(8) :: TAUXX,TAUYY,TAUXY
      
      
      TX = 0.25D0*(VSFLUX%TX1(1)+VSFLUX%TX2(1)+VSFLUX%TX3(1)+VSFLUX%TX4(1)) 
      TY = 0.25D0*(VSFLUX%TX1(2)+VSFLUX%TX2(2)+VSFLUX%TX3(2)+VSFLUX%TX4(2))
      
      VOL  = 0.5D0*(VSFLUX%GRDL(1) + VSFLUX%GRDR(1))
      VIS  = 0.5D0*(VSFLUX%TVL(1) + VSFLUX%TVR(1))
      CON  = 0.5D0*(VSFLUX%TVL(2) + VSFLUX%TVR(2))
      UC   = 0.5D0*(VSFLUX%PV(3,2) + VSFLUX%PV(4,2))
      VC   = 0.5D0*(VSFLUX%PV(3,3) + VSFLUX%PV(4,3))
      
      DUDN = VSFLUX%PV(4,2) - VSFLUX%PV(3,2)
      DVDN = VSFLUX%PV(4,3) - VSFLUX%PV(3,3)
      DTDN = VSFLUX%PV(4,4) - VSFLUX%PV(3,4)
      
      DUDT = 0.25D0*(VSFLUX%PV(5,2)+VSFLUX%PV(6,2)-VSFLUX%PV(2,2)-VSFLUX%PV(1,2))
      DVDT = 0.25D0*(VSFLUX%PV(5,3)+VSFLUX%PV(6,3)-VSFLUX%PV(2,3)-VSFLUX%PV(1,3))
      DTDT = 0.25D0*(VSFLUX%PV(5,4)+VSFLUX%PV(6,4)-VSFLUX%PV(2,4)-VSFLUX%PV(1,4))

      DUDX = (DUDN*VSFLUX%NX(1)+DUDT*TX)/VOL
      DVDX = (DVDN*VSFLUX%NX(1)+DVDT*TX)/VOL
      DTDX = (DTDN*VSFLUX%NX(1)+DTDT*TX)/VOL
 
      DUDY = (DUDN*VSFLUX%NX(2)+DUDT*TY)/VOL
      DVDY = (DVDN*VSFLUX%NX(2)+DVDT*TY)/VOL
      DTDY = (DTDN*VSFLUX%NX(2)+DTDT*TY)/VOL
    
      TAUXX = VIS*(2.D0*DUDX -2.D0/3.D0*VSFLUX%CALSKK(DUDX,DUDY,VC))
      TAUYY = VIS*(2.D0*DUDX -2.D0/3.D0*VSFLUX%CALSKK(DUDX,DUDY,VC))      
      TAUXY = VIS*(DUDY+DVDX)
      
      FX(1) = 0.D0
      FX(2) = (TAUXX*VSFLUX%NX(1)+TAUXY*VSFLUX%NX(2))
      FX(3) = (TAUXY*VSFLUX%NX(1)+TAUYY*VSFLUX%NX(2))
      FX(4) = (VSFLUX%NX(1)*(UC*TAUXX+VC*TAUXY+CON*DTDX)+VSFLUX%NX(2)*(UC*TAUXY+VC*TAUYY+CON*DTDY))
      FX(5) = 0.D0
      FX(6) = 0.D0

      IF(VSFLUX%AXI) FX = FX*VSFLUX%R
    END SUBROUTINE VSFLUX_LAMINAR
    !CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC 
    SUBROUTINE VSFLUX_TURBULENT(VSFLUX,FX)
      IMPLICIT NONE
      CLASS(T_VSFLUX_TURBULENT), INTENT(IN) :: VSFLUX
      REAL(8), INTENT(OUT) :: FX(VSFLUX%NPV)
      REAL(8) :: TX,TY
      REAL(8) :: VOL,VIS,CON,EMUT,TCON,BIGF,UC,VC
      REAL(8) :: DUDN,DVDN,DTDN,DKDN,DODN,DUDT,DVDT,DTDT,DKDT,DODT
      REAL(8) :: DUDX,DVDX,DTDX,DKDX,DODX,DUDY,DVDY,DTDY,DKDY,DODY
      REAL(8) :: TAUXX,TAUYY,TAUXY
      REAL(8) :: TDK,TDO
      
      
      TX = 0.25D0*(VSFLUX%TX1(1)+VSFLUX%TX2(1)+VSFLUX%TX3(1)+VSFLUX%TX4(1)) 
      TY = 0.25D0*(VSFLUX%TX1(2)+VSFLUX%TX2(2)+VSFLUX%TX3(2)+VSFLUX%TX4(2))
      
      VOL  = 0.5D0*(VSFLUX%GRDL(1) + VSFLUX%GRDR(1))
      VIS  = 0.5D0*(VSFLUX%TVL(1) + VSFLUX%TVR(1))
      CON  = 0.5D0*(VSFLUX%TVL(2) + VSFLUX%TVR(2))
      EMUT = 0.5D0*(VSFLUX%TVL(3) + VSFLUX%TVR(3))
      TCON = 0.5D0*(VSFLUX%TVL(3)*VSFLUX%DVL(12)+ VSFLUX%TVR(3)*VSFLUX%DVR(12))/VSFLUX%PR_T
      UC   = 0.5D0*(VSFLUX%PV(3,2) + VSFLUX%PV(4,2))
      VC   = 0.5D0*(VSFLUX%PV(3,3) + VSFLUX%PV(4,3))
     
      DUDN = VSFLUX%PV(4,2) - VSFLUX%PV(3,2)
      DVDN = VSFLUX%PV(4,3) - VSFLUX%PV(3,3)
      DTDN = VSFLUX%PV(4,4) - VSFLUX%PV(3,4)
      DKDN = VSFLUX%PV(4,7) - VSFLUX%PV(3,7)
      DODN = VSFLUX%PV(4,8) - VSFLUX%PV(3,8) 
      
      DUDT = 0.25D0*(VSFLUX%PV(5,2)+VSFLUX%PV(6,2)-VSFLUX%PV(2,2)-VSFLUX%PV(1,2))
      DVDT = 0.25D0*(VSFLUX%PV(5,3)+VSFLUX%PV(6,3)-VSFLUX%PV(2,3)-VSFLUX%PV(1,3))
      DTDT = 0.25D0*(VSFLUX%PV(5,4)+VSFLUX%PV(6,4)-VSFLUX%PV(2,4)-VSFLUX%PV(1,4))
      DKDT = 0.25D0*(VSFLUX%PV(5,7)+VSFLUX%PV(6,7)-VSFLUX%PV(2,7)-VSFLUX%PV(1,7))
      DODT = 0.25D0*(VSFLUX%PV(5,8)+VSFLUX%PV(6,8)-VSFLUX%PV(2,8)-VSFLUX%PV(1,8))
      
      DUDX = (DUDN*VSFLUX%NX(1)+DUDT*TX)/VOL
      DVDX = (DVDN*VSFLUX%NX(1)+DVDT*TX)/VOL
      DTDX = (DTDN*VSFLUX%NX(1)+DTDT*TX)/VOL
      DKDX = (DKDN*VSFLUX%NX(1)+DKDT*TX)/VOL
      DODX = (DODN*VSFLUX%NX(1)+DODT*TX)/VOL 
      
      DUDY = (DUDN*VSFLUX%NX(2)+DUDT*TY)/VOL
      DVDY = (DVDN*VSFLUX%NX(2)+DVDT*TY)/VOL
      DTDY = (DTDN*VSFLUX%NX(2)+DTDT*TY)/VOL
      DKDY = (DKDN*VSFLUX%NX(2)+DKDT*TY)/VOL
      DODY = (DODN*VSFLUX%NX(2)+DODT*TY)/VOL

      BIGF = VSFLUX%CALBIGF(DKDX*DODX+DKDY*DODY)
      
      TDK = BIGF*VSFLUX%TDK1 + (1.D0-BIGF)*VSFLUX%TDK2
      TDO = BIGF*VSFLUX%TDO1 + (1.D0-BIGF)*VSFLUX%TDO2
      
      TAUXX = (VIS+EMUT)*(2.D0*DUDX -2.D0/3.D0*VSFLUX%CALSKK(DUDX,DUDY,VC))
      TAUYY = (VIS+EMUT)*(2.D0*DVDY -2.D0/3.D0*VSFLUX%CALSKK(DUDX,DUDY,VC))  
      TAUXY = (VIS+EMUT)*(DUDY+DVDX)
      
      FX(1) = 0.D0
      FX(2) = (TAUXX*VSFLUX%NX(1)+TAUXY*VSFLUX%NX(2))
      FX(3) = (TAUXY*VSFLUX%NX(1)+TAUYY*VSFLUX%NX(2))
      FX(4) = (VSFLUX%NX(1)*(UC*TAUXX+VC*TAUXY+(CON+TCON)*DTDX)+VSFLUX%NX(2)*(UC*TAUXY+VC*TAUYY+(CON+TCON)*DTDY))
      FX(5) = 0.D0
      FX(6) = 0.D0
      FX(7) = (VIS+EMUT*TDK)*(DKDX*VSFLUX%NX(1)+DKDY*VSFLUX%NX(2))
      FX(8) = (VIS+EMUT*TDO)*(DODX*VSFLUX%NX(1)+DODY*VSFLUX%NX(2))
      
      IF(VSFLUX%AXI) FX = FX*VSFLUX%R
    END SUBROUTINE VSFLUX_TURBULENT
    !CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
    FUNCTION SKK_NOAXI(VSFLUX,DUDX,DVDY,V) RESULT(SKK)
      IMPLICIT NONE
      CLASS(T_VSFLUX), INTENT(IN) :: VSFLUX
      REAL(8), INTENT(IN) :: DUDX,DVDY,V
      REAL(8) :: SKK
      
      SKK = DUDX+DVDY
    END FUNCTION SKK_NOAXI
    !CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
    FUNCTION SKK_AXI(VSFLUX,DUDX,DVDY,V) RESULT(SKK)
      IMPLICIT NONE
      CLASS(T_VSFLUX), INTENT(IN) :: VSFLUX
      REAL(8), INTENT(IN) :: DUDX,DVDY,V
      REAL(8) :: SKK
      IF(VSFLUX%R.NE.0) THEN
        SKK = DUDX+DVDY+V/VSFLUX%R
      ELSE
        SKK = DUDX+DVDY
      END IF
    END FUNCTION SKK_AXI
    !CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
    FUNCTION KWSST(VSFLUX,PKW) RESULT(BIGF)
      IMPLICIT NONE
      CLASS(T_VSFLUX), INTENT(IN) :: VSFLUX
      REAL(8), INTENT(IN) :: PKW
      REAL(8) :: BIGF
      REAL(8) :: TERM0,TERM1,TERM2,TERM3,CDKW,ARG1,ARG2
      REAL(8) :: RHO,YDNS,VIS,K,O
      
      RHO = 0.5D0*(VSFLUX%DVL(1)+VSFLUX%DVR(1))
      YDNS = 0.5D0*(VSFLUX%GRDL(4)+VSFLUX%GRDR(4))
      VIS = 0.5D0*(VSFLUX%TVL(1)+VSFLUX%TVR(1))
      K = 0.5D0*(VSFLUX%PV(4,7)+VSFLUX%PV(3,7)) 
      O = 0.5D0*(VSFLUX%PV(4,8)+VSFLUX%PV(3,8))
      
      TERM0 = 1.712D0*RHO/O*PKW
      CDKW = DMAX1(TERM0,1.D-10)
      
      TERM1 = DSQRT(K)/(0.09D0*O*YDNS)
      TERM2 = 500.D0*VIS/RHO/(O*YDNS**2)
      TERM3 = (3.424D0*RHO*K)/(CDKW*YDNS**2)
      
      ARG2 = DMAX1(TERM1,TERM2)
      ARG1 = DMIN1(ARG2,TERM3)
      
      BIGF = DTANH(ARG1**4)
      
    END FUNCTION KWSST
    !CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
    FUNCTION KEPSILON(VSFLUX,PKW) RESULT(BIGF)
      IMPLICIT NONE
      CLASS(T_VSFLUX), INTENT(IN) :: VSFLUX
      REAL(8), INTENT(IN) :: PKW
      REAL(8) :: BIGF

      BIGF = 0.D0
      
    END FUNCTION KEPSILON
    !CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
END MODULE VSFLUX_MODULE
    