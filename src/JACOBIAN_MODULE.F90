MODULE JACOBIAN_MODULE
  USE CONFIG_MODULE
  USE GRID_MODULE
  USE VARIABLE_MODULE
  IMPLICIT NONE
  PRIVATE
  PUBLIC :: T_JAC,T_JAC_FLOWONLY,T_JAC_FLOWTURBALL
  
  TYPE, ABSTRACT :: T_JAC
    PRIVATE
    INTEGER :: NPV,NTV,NDV,NGRD
    REAL(8) :: UREF,STR
    REAL(8), POINTER :: NX(:)
    REAL(8), POINTER :: PV(:),TV(:),DV(:),GRD(:)
    REAL(8), DIMENSION(:,:), ALLOCATABLE :: A
    PROCEDURE(P_GETSNDP2), POINTER :: GETSNDP2
    PROCEDURE(P_GETEIGENVIS), POINTER :: GETEIGENVIS    
    CONTAINS
      PROCEDURE :: CONSTRUCT
      PROCEDURE :: DESTRUCT
      PROCEDURE :: SETNORM ! (NX)
      PROCEDURE :: SETGRD  ! VOL,XCEN,YCEN,ZCEN,YDNS
      PROCEDURE :: SETPV   ! P,U,V,W,T,Y1,Y2,K,O
      PROCEDURE :: SETDV   ! RHO,H,RHOL,RHOV,RHOG,SND2,DRDP,DRDT,DRDY1,DRDY2,DHDP,DHDT,DHDY1,DHDY2,DRDPV,DRDTV,DRDPL,DRDTL
      PROCEDURE :: SETTV   ! VIS,COND,EMUT
      PROCEDURE :: GETA
      PROCEDURE(P_CALJAC), DEFERRED :: CALJAC 
  END TYPE T_JAC
  
  TYPE, EXTENDS(T_JAC) :: T_JAC_FLOWONLY
    CONTAINS
      PROCEDURE :: CALJAC => FLOWONLY
  END TYPE T_JAC_FLOWONLY

  TYPE, EXTENDS(T_JAC) :: T_JAC_FLOWTURBALL
    CONTAINS
      PROCEDURE :: CALJAC => FLOWTURBALL
  END TYPE T_JAC_FLOWTURBALL
  
  ABSTRACT INTERFACE
    SUBROUTINE P_CALJAC(JAC,SIGN)
      IMPORT T_JAC
      IMPLICIT NONE
      CLASS(T_JAC), INTENT(INOUT) :: JAC
      INTEGER, INTENT(IN) :: SIGN
    END SUBROUTINE P_CALJAC
  END INTERFACE
  
  INTERFACE
    FUNCTION P_GETSNDP2(JAC,U2) RESULT(SNDP2)
      IMPORT T_JAC
      IMPLICIT NONE
      CLASS(T_JAC), INTENT(IN) :: JAC
      REAL(8), INTENT(IN) :: U2
      REAL(8) :: SNDP2
    END FUNCTION P_GETSNDP2
    
    FUNCTION P_GETEIGENVIS(JAC) RESULT(EIGENVIS)
      IMPORT T_JAC
      IMPLICIT NONE
      CLASS(T_JAC), INTENT(IN) :: JAC
      REAL(8) :: EIGENVIS
    END FUNCTION P_GETEIGENVIS
  END INTERFACE
  
  CONTAINS
    !CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
    SUBROUTINE CONSTRUCT(JAC,CONFIG,GRID,VARIABLE)
      IMPLICIT NONE
      CLASS(T_JAC), INTENT(OUT) :: JAC
      TYPE(T_CONFIG), INTENT(IN) :: CONFIG
      TYPE(T_GRID), INTENT(IN) :: GRID
      TYPE(T_VARIABLE), INTENT(IN) :: VARIABLE
            
      JAC%UREF = CONFIG%GETUREF()
      JAC%STR  = CONFIG%GETSTR()
      
      SELECT CASE(CONFIG%GETPREC())
      CASE(0)
        JAC%GETSNDP2 => NO_PREC
      CASE(1)
        JAC%GETSNDP2 => STEADY_PREC
      CASE(2)
        JAC%GETSNDP2 => UNSTEADY_PREC
      END SELECT
      
      SELECT CASE(CONFIG%GETITURB())
      CASE(-1,0)
        JAC%GETEIGENVIS => TURBULENT
      CASE(-2)
        JAC%GETEIGENVIS => LAMINAR
      CASE(-3)
        JAC%GETEIGENVIS => EULER
      END SELECT

      JAC%NGRD = GRID%GETNGRD()
      JAC%NPV  = VARIABLE%GETNPV()
      JAC%NTV  = VARIABLE%GETNTV()
      JAC%NDV  = VARIABLE%GETNDV()
      
      ALLOCATE(JAC%A(JAC%NPV,JAC%NPV))
    END SUBROUTINE CONSTRUCT
    !CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
    SUBROUTINE DESTRUCT(JAC)
      IMPLICIT NONE
      CLASS(T_JAC), INTENT(INOUT) :: JAC

      IF(ASSOCIATED(JAC%NX))           NULLIFY(JAC%NX)                    
      IF(ASSOCIATED(JAC%GRD))          NULLIFY(JAC%GRD)         
      IF(ASSOCIATED(JAC%PV))           NULLIFY(JAC%PV)
      IF(ASSOCIATED(JAC%DV))           NULLIFY(JAC%DV) 
      IF(ASSOCIATED(JAC%TV))           NULLIFY(JAC%TV)                   
      IF(ASSOCIATED(JAC%GETSNDP2))     NULLIFY(JAC%GETSNDP2)    
      IF(ASSOCIATED(JAC%GETEIGENVIS))  NULLIFY(JAC%GETEIGENVIS) 
      
      DEALLOCATE(JAC%A)
    END SUBROUTINE DESTRUCT
    !CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
    SUBROUTINE SETNORM(JAC,NX)
      IMPLICIT NONE
      CLASS(T_JAC), INTENT(INOUT) :: JAC
      REAL(8), INTENT(IN), TARGET :: NX(3)
      
      JAC%NX => NX

    END SUBROUTINE SETNORM
    !CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
    SUBROUTINE SETGRD(JAC,GRD)
      IMPLICIT NONE
      CLASS(T_JAC), INTENT(INOUT) :: JAC
      REAL(8), INTENT(IN), TARGET :: GRD(JAC%NGRD)
      
      JAC%GRD => GRD
      
    END SUBROUTINE SETGRD
    !CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
   SUBROUTINE SETPV(JAC,PV)
      IMPLICIT NONE
      CLASS(T_JAC), INTENT(INOUT) :: JAC
      REAL(8), INTENT(IN), TARGET :: PV(JAC%NPV)
      
      JAC%PV => PV
      
    END SUBROUTINE SETPV
    !CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
    SUBROUTINE SETDV(JAC,DV)
      IMPLICIT NONE
      CLASS(T_JAC), INTENT(INOUT) :: JAC
      REAL(8), INTENT(IN), TARGET :: DV(JAC%NDV)
      
      JAC%DV => DV
      
    END SUBROUTINE SETDV
    !CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
    SUBROUTINE SETTV(JAC,TV)
      IMPLICIT NONE
      CLASS(T_JAC), INTENT(INOUT) :: JAC
      REAL(8), INTENT(IN), TARGET :: TV(JAC%NTV)
      
      JAC%TV => TV
      
    END SUBROUTINE SETTV
    !CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
    SUBROUTINE FLOWONLY(JAC,SIGN)
      IMPLICIT NONE
      CLASS(T_JAC_FLOWONLY), INTENT(INOUT) :: JAC
      INTEGER, INTENT(IN) :: SIGN
      REAL(8) :: DS,H,U,UP,D,BETA,SNDP2,LAMDA,VIS
      REAL(8), PARAMETER :: CAPPA = 1.05D0
      REAL(8) :: A1,A2,A3,A4,A5,A6,A7
      REAL(8) :: G1,G5,G6,G7,GE1,GE5,GE6,GE7
      
      DS = JAC%NX(1)**2+JAC%NX(2)**2+JAC%NX(3)**2
      U = JAC%NX(1)*JAC%PV(2) + JAC%NX(2)*JAC%PV(3)+JAC%NX(3)*JAC%PV(4)
      H = JAC%DV(2) + 0.5D0*(JAC%PV(2)**2+JAC%PV(3)**2+JAC%PV(4)**2)
      SNDP2 = JAC%GETSNDP2(JAC%PV(2)**2+JAC%PV(3)**2+JAC%PV(4)**2)
      UP = (1.D0+SNDP2/JAC%DV(6))*U
      D  = DSQRT(U**2*(1.D0-SNDP2/JAC%DV(6))**2+4.D0*SNDP2*DS)
      LAMDA = DBLE(SIGN)*CAPPA*(DABS(UP)+D)
      VIS = DBLE(SIGN)*2.D0*JAC%GETEIGENVIS()*DS/JAC%GRD(1)
      BETA = 1.D0/SNDP2-1.D0/JAC%DV(6)+JAC%DV(7)
      
      A1 = U*JAC%DV(7)
      A2 = JAC%NX(1)*JAC%DV(1)
      A3 = JAC%NX(2)*JAC%DV(1)
      A4 = JAC%NX(3)*JAC%DV(1)
      A5 = U*JAC%DV(8)
      A6 = U*JAC%DV(9)
      A7 = U*JAC%DV(10)
      G1 = LAMDA*BETA
      G5 = LAMDA*JAC%DV(8)
      G6 = LAMDA*JAC%DV(9)
      G7 = LAMDA*JAC%DV(10)
      GE1 = VIS*JAC%DV(7)
      GE5 = VIS*JAC%DV(8)
      GE6 = VIS*JAC%DV(9)
      GE7 = VIS*JAC%DV(10)
      
      JAC%A(1,1) = A1+G1+GE1
      JAC%A(1,2) = A2
      JAC%A(1,3) = A3
      JAC%A(1,4) = A4
      JAC%A(1,5) = A5+G5+GE5
      JAC%A(1,6) = A6+G6+GE6
      JAC%A(1,7) = A7+G7+GE7

      JAC%A(2,1) = JAC%PV(2)*(A1+G1+GE1) + JAC%NX(1)
      JAC%A(2,2) = JAC%PV(2)*A2 + JAC%DV(1)*(U+LAMDA+VIS)
      JAC%A(2,3) = JAC%PV(2)*A3
      JAC%A(2,4) = JAC%PV(2)*A4
      JAC%A(2,5) = JAC%PV(2)*(A5+G5+GE5)
      JAC%A(2,6) = JAC%PV(2)*(A6+G6+GE6)
      JAC%A(2,7) = JAC%PV(2)*(A7+G7+GE7)
        
      JAC%A(3,1) = JAC%PV(3)*(A1+G1+GE1) + JAC%NX(2)
      JAC%A(3,2) = JAC%PV(3)*A2 
      JAC%A(3,3) = JAC%PV(3)*A3 + JAC%DV(1)*(U+LAMDA+VIS)
      JAC%A(3,4) = JAC%PV(3)*A4
      JAC%A(3,5) = JAC%PV(3)*(A5+G5+GE5)
      JAC%A(3,6) = JAC%PV(3)*(A6+G6+GE6)
      JAC%A(3,7) = JAC%PV(3)*(A7+G7+GE7)

      JAC%A(4,1) = JAC%PV(4)*(A1+G1+GE1) + JAC%NX(3)
      JAC%A(4,2) = JAC%PV(4)*A2 
      JAC%A(4,3) = JAC%PV(4)*A3 
      JAC%A(4,4) = JAC%PV(4)*A4 + JAC%DV(1)*(U+LAMDA+VIS)
      JAC%A(4,5) = JAC%PV(4)*(A5+G5+GE5)
      JAC%A(4,6) = JAC%PV(4)*(A6+G6+GE6)
      JAC%A(4,7) = JAC%PV(4)*(A7+G7+GE7)
      
      JAC%A(5,1) = JAC%DV(1)*(U+LAMDA+VIS)*JAC%DV(11)+H*(A1+G1+GE1)-LAMDA-VIS
      JAC%A(5,2) = JAC%DV(1)*(U+LAMDA+VIS)*JAC%PV(2) +H*A2
      JAC%A(5,3) = JAC%DV(1)*(U+LAMDA+VIS)*JAC%PV(3) +H*A3
      JAC%A(5,4) = JAC%DV(1)*(U+LAMDA+VIS)*JAC%PV(4) +H*A4
      JAC%A(5,5) = JAC%DV(1)*(U+LAMDA+VIS)*JAC%DV(12)+H*(A5+G5+GE5)
      JAC%A(5,6) = JAC%DV(1)*(U+LAMDA+VIS)*JAC%DV(13)+H*(A6+G6+GE6)
      JAC%A(5,7) = JAC%DV(1)*(U+LAMDA+VIS)*JAC%DV(14)+H*(A7+G7+GE7)
        
      JAC%A(6,1) = JAC%PV(6)*(A1+G1+GE1)
      JAC%A(6,2) = JAC%PV(6)*A2
      JAC%A(6,3) = JAC%PV(6)*A3
      JAC%A(6,4) = JAC%PV(6)*A4
      JAC%A(6,5) = JAC%PV(6)*(A5+G5+GE5)
      JAC%A(6,6) = JAC%PV(6)*(A6+G6+GE6)+JAC%DV(1)*(U+LAMDA+VIS)
      JAC%A(6,7) = JAC%PV(6)*(A7+G7+GE7)
        
      JAC%A(7,1) = JAC%PV(7)*(A1+G1+GE1)
      JAC%A(7,2) = JAC%PV(7)*A2
      JAC%A(7,3) = JAC%PV(7)*A3
      JAC%A(7,4) = JAC%PV(7)*A4
      JAC%A(7,5) = JAC%PV(7)*(A5+G5+GE5)
      JAC%A(7,6) = JAC%PV(7)*(A6+G6+GE6)
      JAC%A(7,7) = JAC%PV(7)*(A7+G7+GE7)+JAC%DV(1)*(U+LAMDA+VIS)

      JAC%A = JAC%A*0.5D0
      
    END SUBROUTINE FLOWONLY
    !CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
    SUBROUTINE FLOWTURBALL(JAC,SIGN)
      IMPLICIT NONE
      CLASS(T_JAC_FLOWTURBALL), INTENT(INOUT) :: JAC
      INTEGER, INTENT(IN) :: SIGN
      REAL(8) :: DS,H,U,UP,D,BETA,SNDP2,LAMDA,VIS
      REAL(8), PARAMETER :: CAPPA = 1.05D0
      REAL(8) :: A1,A2,A3,A4,A5,A6,A7
      REAL(8) :: G1,G5,G6,G7,GE1,GE5,GE6,GE7
      
      DS = JAC%NX(1)**2+JAC%NX(2)**2+JAC%NX(3)**2
      U = JAC%NX(1)*JAC%PV(2) + JAC%NX(2)*JAC%PV(3)+JAC%NX(3)*JAC%PV(4)
      H = JAC%DV(2) + 0.5D0*(JAC%PV(2)**2+JAC%PV(3)**2+JAC%PV(4)**2)
      SNDP2 = JAC%GETSNDP2(JAC%PV(2)**2+JAC%PV(3)**2+JAC%PV(4)**2)
      UP = (1.D0+SNDP2/JAC%DV(6))*U
      D  = DSQRT(U**2*(1.D0-SNDP2/JAC%DV(6))**2+4.D0*SNDP2*DS)
      LAMDA = DBLE(SIGN)*CAPPA*(DABS(UP)+D)
      VIS = DBLE(SIGN)*2.D0*JAC%GETEIGENVIS()*DS/JAC%GRD(1)
      BETA = 1.D0/SNDP2-1.D0/JAC%DV(6)+JAC%DV(7)
      
      A1 = U*JAC%DV(7)
      A2 = JAC%NX(1)*JAC%DV(1)
      A3 = JAC%NX(2)*JAC%DV(1)
      A4 = JAC%NX(3)*JAC%DV(1)
      A5 = U*JAC%DV(8)
      A6 = U*JAC%DV(9)
      A7 = U*JAC%DV(10)
      G1 = LAMDA*BETA
      G5 = LAMDA*JAC%DV(8)
      G6 = LAMDA*JAC%DV(9)
      G7 = LAMDA*JAC%DV(10)
      GE1 = VIS*JAC%DV(7)
      GE5 = VIS*JAC%DV(8)
      GE6 = VIS*JAC%DV(9)
      GE7 = VIS*JAC%DV(10)
      
      JAC%A(1,1) = A1+G1+GE1
      JAC%A(1,2) = A2
      JAC%A(1,3) = A3
      JAC%A(1,4) = A4
      JAC%A(1,5) = A5+G5+GE5
      JAC%A(1,6) = A6+G6+GE6
      JAC%A(1,7) = A7+G7+GE7
      JAC%A(1,8) = 0.D0
      JAC%A(1,9) = 0.D0
        
      JAC%A(2,1) = JAC%PV(2)*(A1+G1+GE1) + JAC%NX(1)
      JAC%A(2,2) = JAC%PV(2)*A2 + JAC%DV(1)*(U+LAMDA+VIS)
      JAC%A(2,3) = JAC%PV(2)*A3
      JAC%A(2,4) = JAC%PV(2)*A4
      JAC%A(2,5) = JAC%PV(2)*(A5+G5+GE5)
      JAC%A(2,6) = JAC%PV(2)*(A6+G6+GE6)
      JAC%A(2,7) = JAC%PV(2)*(A7+G7+GE7)
      JAC%A(2,8) = 0.D0
      JAC%A(2,9) = 0.D0
        
      JAC%A(3,1) = JAC%PV(3)*(A1+G1+GE1) + JAC%NX(2)
      JAC%A(3,2) = JAC%PV(3)*A2 
      JAC%A(3,3) = JAC%PV(3)*A3 + JAC%DV(1)*(U+LAMDA+VIS)
      JAC%A(3,4) = JAC%PV(3)*A4
      JAC%A(3,5) = JAC%PV(3)*(A5+G5+GE5)
      JAC%A(3,6) = JAC%PV(3)*(A6+G6+GE6)
      JAC%A(3,7) = JAC%PV(3)*(A7+G7+GE7)
      JAC%A(3,8) = 0.D0
      JAC%A(3,9) = 0.D0

      JAC%A(4,1) = JAC%PV(4)*(A1+G1+GE1) + JAC%NX(3)
      JAC%A(4,2) = JAC%PV(4)*A2 
      JAC%A(4,3) = JAC%PV(4)*A3 
      JAC%A(4,4) = JAC%PV(4)*A4 + JAC%DV(1)*(U+LAMDA+VIS)
      JAC%A(4,5) = JAC%PV(4)*(A5+G5+GE5)
      JAC%A(4,6) = JAC%PV(4)*(A6+G6+GE6)
      JAC%A(4,7) = JAC%PV(4)*(A7+G7+GE7)
      JAC%A(4,8) = 0.D0
      JAC%A(4,9) = 0.D0
      
      JAC%A(5,1) = JAC%DV(1)*(U+LAMDA+VIS)*JAC%DV(11)+H*(A1+G1+GE1)-LAMDA-VIS
      JAC%A(5,2) = JAC%DV(1)*(U+LAMDA+VIS)*JAC%PV(2) +H*A2
      JAC%A(5,3) = JAC%DV(1)*(U+LAMDA+VIS)*JAC%PV(3) +H*A3
      JAC%A(5,4) = JAC%DV(1)*(U+LAMDA+VIS)*JAC%PV(4) +H*A4
      JAC%A(5,5) = JAC%DV(1)*(U+LAMDA+VIS)*JAC%DV(12)+H*(A5+G5+GE5)
      JAC%A(5,6) = JAC%DV(1)*(U+LAMDA+VIS)*JAC%DV(13)+H*(A6+G6+GE6)
      JAC%A(5,7) = JAC%DV(1)*(U+LAMDA+VIS)*JAC%DV(14)+H*(A7+G7+GE7)
      JAC%A(5,8) = 0.D0
      JAC%A(5,9) = 0.D0
        
      JAC%A(6,1) = JAC%PV(6)*(A1+G1+GE1)
      JAC%A(6,2) = JAC%PV(6)*A2
      JAC%A(6,3) = JAC%PV(6)*A3
      JAC%A(6,4) = JAC%PV(6)*A4
      JAC%A(6,5) = JAC%PV(6)*(A5+G5+GE5)
      JAC%A(6,6) = JAC%PV(6)*(A6+G6+GE6)+JAC%DV(1)*(U+LAMDA+VIS)
      JAC%A(6,7) = JAC%PV(6)*(A7+G7+GE7)
      JAC%A(6,8) = 0.D0
      JAC%A(6,9) = 0.D0
        
      JAC%A(7,1) = JAC%PV(7)*(A1+G1+GE1)
      JAC%A(7,2) = JAC%PV(7)*A2
      JAC%A(7,3) = JAC%PV(7)*A3
      JAC%A(7,4) = JAC%PV(7)*A4
      JAC%A(7,5) = JAC%PV(7)*(A5+G5+GE5)
      JAC%A(7,6) = JAC%PV(7)*(A6+G6+GE6)
      JAC%A(7,7) = JAC%PV(7)*(A7+G7+GE7)+JAC%DV(1)*(U+LAMDA+VIS)
      JAC%A(7,8) = 0.D0
      JAC%A(7,9) = 0.D0

      JAC%A(8,1) = JAC%PV(8)*(A1+G1+GE1)
      JAC%A(8,2) = JAC%PV(8)*A2
      JAC%A(8,3) = JAC%PV(8)*A3
      JAC%A(8,4) = JAC%PV(8)*A4
      JAC%A(8,5) = JAC%PV(8)*(A5+G5+GE5)
      JAC%A(8,6) = JAC%PV(8)*(A6+G6+GE6)
      JAC%A(8,7) = JAC%PV(8)*(A7+G7+GE7)
      JAC%A(8,8) = (U+LAMDA+VIS)*JAC%DV(1)
      JAC%A(8,9) = 0.D0

      JAC%A(9,1) = JAC%PV(9)*(A1+G1+GE1)
      JAC%A(9,2) = JAC%PV(9)*A2
      JAC%A(9,3) = JAC%PV(9)*A3
      JAC%A(9,4) = JAC%PV(9)*A4
      JAC%A(9,5) = JAC%PV(9)*(A5+G5+GE5)
      JAC%A(9,6) = JAC%PV(9)*(A6+G6+GE6)
      JAC%A(9,7) = JAC%PV(9)*(A7+G7+GE7)
      JAC%A(9,8) = 0.D0
      JAC%A(9,9) = (U+LAMDA+VIS)*JAC%DV(1)
      
      JAC%A = JAC%A*0.5D0
    END SUBROUTINE FLOWTURBALL
    !CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
    FUNCTION EULER(JAC) RESULT(EIGENVIS)
      IMPLICIT NONE
      CLASS(T_JAC), INTENT(IN) :: JAC
      REAL(8) :: EIGENVIS
      
      EIGENVIS = 0.D0
      
    END FUNCTION EULER
    !CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
    FUNCTION LAMINAR(JAC) RESULT(EIGENVIS)
      IMPLICIT NONE
      CLASS(T_JAC), INTENT(IN) :: JAC
      REAL(8) :: EIGENVIS

      EIGENVIS = DMAX1(JAC%DV(7)*JAC%TV(2)/(JAC%DV(8)     &
                       +JAC%DV(12)*JAC%DV(7)*JAC%DV(1)    &
                       -JAC%DV(11)*JAC%DV(8)*JAC%DV(1)),  &
                       4.D0/3.D0*JAC%TV(1)/JAC%DV(1)) 
    END FUNCTION LAMINAR
    !CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
    FUNCTION TURBULENT(JAC) RESULT(EIGENVIS)
      IMPLICIT NONE
      CLASS(T_JAC), INTENT(IN) :: JAC
      REAL(8) :: EIGENVIS
      REAL(8), PARAMETER :: PR_T = 0.9D0
   
      EIGENVIS = DMAX1(JAC%DV(7)*(JAC%TV(2)+JAC%DV(12)*JAC%TV(3)/PR_T) &
                      /(JAC%DV(8)+JAC%DV(12)*JAC%DV(7)*JAC%DV(1)       &
                       -JAC%DV(11)*JAC%DV(8)*JAC%DV(1)),               &
                       4.D0/3.D0*(JAC%TV(1)+JAC%TV(3))/JAC%DV(1))
    END FUNCTION TURBULENT
    !CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
    FUNCTION NO_PREC(JAC,U2) RESULT(SNDP2)
      IMPLICIT NONE
      CLASS(T_JAC), INTENT(IN) :: JAC
      REAL(8), INTENT(IN) :: U2
      REAL(8) :: SNDP2
      
      SNDP2 = JAC%DV(6)
      
    END FUNCTION NO_PREC
    !CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
    FUNCTION STEADY_PREC(JAC,U2) RESULT(SNDP2)
      IMPLICIT NONE
      CLASS(T_JAC), INTENT(IN) :: JAC
      REAL(8), INTENT(IN) :: U2
      REAL(8) :: SNDP2  
      
      SNDP2 = DMIN1(JAC%DV(6),DMAX1(U2,JAC%UREF**2))
      
    END FUNCTION STEADY_PREC
    !CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
    FUNCTION UNSTEADY_PREC(JAC,U2) RESULT(SNDP2)
      IMPLICIT NONE
      CLASS(T_JAC), INTENT(IN) :: JAC
      REAL(8), INTENT(IN) :: U2
      REAL(8) :: SNDP2  
      
      SNDP2 = DMIN1(JAC%DV(6),DMAX1(U2,JAC%UREF**2,JAC%STR**2*U2))
      
    END FUNCTION UNSTEADY_PREC
    !CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
    FUNCTION GETA(JAC,I,J)
      IMPLICIT NONE
      CLASS(T_JAC), INTENT(IN) :: JAC
      INTEGER, INTENT(IN) :: I,J
      REAL(8) :: GETA
      
      GETA = JAC%A(I,J)
      
    END FUNCTION GETA
    !CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
END MODULE JACOBIAN_MODULE