MODULE PROP_MODULE
  IMPLICIT NONE
  PRIVATE
  PUBLIC :: T_PROP

  TYPE T_IAPWS_COEFF
    REAL(8), DIMENSION(:), ALLOCATABLE :: H,L
    REAL(8), DIMENSION(:,:), ALLOCATABLE :: HH,LL
  END TYPE T_IAPWS_COEFF
  
  TYPE T_PROP
    PRIVATE
    PROCEDURE(P_PROP_L), POINTER :: PROP_L
    PROCEDURE(P_PROP_V), POINTER :: PROP_V
    PROCEDURE(P_PROP_G), POINTER :: PROP_G
    TYPE(T_IAPWS_COEFF) :: PROP_COEFF
    CONTAINS
      PROCEDURE :: CONSTRUCT
      PROCEDURE, PRIVATE :: SET_IAPWS97
      PROCEDURE :: DESTRUCT
      PROCEDURE :: DETPROP    
  END TYPE T_PROP
 
  TYPE T_PROP2
    REAL(8) :: VIS,COND 
  END TYPE T_PROP2

  INTERFACE
    SUBROUTINE P_PROP_L(PROP,RHO,T,LIQUID)
      IMPORT T_PROP
      IMPORT T_PROP2
      IMPLICIT NONE
      CLASS(T_PROP), INTENT(IN) :: PROP
      REAL(8), INTENT(IN) :: RHO,T
      TYPE(T_PROP2), INTENT(OUT) :: LIQUID
    END SUBROUTINE P_PROP_L
    
    SUBROUTINE P_PROP_V(PROP,RHO,T,VAPOR)
      IMPORT T_PROP
      IMPORT T_PROP2
      IMPLICIT NONE
      CLASS(T_PROP), INTENT(IN) :: PROP
      REAL(8), INTENT(IN) :: RHO,T
      TYPE(T_PROP2), INTENT(OUT) :: VAPOR
    END SUBROUTINE P_PROP_V
    
    SUBROUTINE P_PROP_G(PROP,RHO,T,GAS)
      IMPORT T_PROP
      IMPORT T_PROP2
      IMPLICIT NONE
      CLASS(T_PROP), INTENT(IN) :: PROP
      REAL(8), INTENT(IN) :: RHO,T
      TYPE(T_PROP2), INTENT(OUT) :: GAS
    END SUBROUTINE P_PROP_G
  END INTERFACE

  CONTAINS
    !CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC    
    SUBROUTINE CONSTRUCT(PROP,FLUID,EOSTYPE,NGAS)
      IMPLICIT NONE
      CLASS(T_PROP), INTENT(OUT) :: PROP
      INTEGER, INTENT(IN) :: FLUID,EOSTYPE,NGAS
      
      SELECT CASE(NGAS)
      CASE(1)
        PROP%PROP_G => SUTHERLAND
      CASE DEFAULT
      END SELECT
      
      SELECT CASE(FLUID)
      CASE(1) ! WATER
        SELECT CASE(EOSTYPE)
        CASE(1) ! FITTING
          PROP%PROP_L => H2O_L_FIT
          PROP%PROP_V => H2O_V_FIT
        CASE(2) ! IAPWS97
          CALL PROP%SET_IAPWS97()
          PROP%PROP_L => IAPWS97
          PROP%PROP_V => IAPWS97
        CASE(3) ! STIFFENED
          PROP%PROP_L => STIFFENED
          PROP%PROP_V => SUTHERLAND
        END SELECT
      CASE(2) ! NITROGEN
        SELECT CASE(EOSTYPE)
        CASE(1) ! FITTING
          PROP%PROP_L => N2_L_FIT
          PROP%PROP_V => N2_V_FIT
        CASE DEFAULT
        END SELECT
      CASE(3) ! OXYGEN
        SELECT CASE(EOSTYPE)
        CASE(1) ! FITTING
          PROP%PROP_L => O2_L_FIT
          PROP%PROP_V => O2_V_FIT
        CASE DEFAULT
        END SELECT
      CASE(4) ! HYDROGEN
        SELECT CASE(EOSTYPE)
        CASE(1) ! FITTING
          PROP%PROP_L => H2_L_FIT
          PROP%PROP_V => H2_V_FIT
        CASE DEFAULT
        END SELECT
      END SELECT
    
    END SUBROUTINE CONSTRUCT
    !CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC    
    SUBROUTINE DESTRUCT(PROP)
      IMPLICIT NONE
      CLASS(T_PROP), INTENT(INOUT) :: PROP
      
      IF(ASSOCIATED(PROP%PROP_L)) NULLIFY(PROP%PROP_L)
      IF(ASSOCIATED(PROP%PROP_V)) NULLIFY(PROP%PROP_V)
      IF(ASSOCIATED(PROP%PROP_G)) NULLIFY(PROP%PROP_G)

      IF(ALLOCATED(PROP%PROP_COEFF%H)) DEALLOCATE(PROP%PROP_COEFF%H)
      IF(ALLOCATED(PROP%PROP_COEFF%L)) DEALLOCATE(PROP%PROP_COEFF%L)
      IF(ALLOCATED(PROP%PROP_COEFF%HH)) DEALLOCATE(PROP%PROP_COEFF%HH)
      IF(ALLOCATED(PROP%PROP_COEFF%LL)) DEALLOCATE(PROP%PROP_COEFF%LL)
    END SUBROUTINE DESTRUCT
    !CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
    SUBROUTINE DETPROP(PROP,RHOL,RHOV,RHOG,T,Y1,Y2,TV)
      IMPLICIT NONE
      CLASS(T_PROP), INTENT(IN) :: PROP
      REAL(8), INTENT(IN) :: RHOL,RHOV,RHOG,T,Y1,Y2
      REAL(8), INTENT(OUT) :: TV(2)
      TYPE(T_PROP2) :: LIQUID,VAPOR,GAS
      REAL(8) :: RHO
      
      CALL PROP%PROP_L(RHOL,T,LIQUID)
      CALL PROP%PROP_V(RHOV,T,VAPOR)
      CALL PROP%PROP_G(RHOG,T,GAS)
      
      RHO = 1.D0/((1.D0-Y1-Y2)/RHOL + Y1/RHOV + Y2/RHOG)
      TV(1)  = RHO*(LIQUID%VIS*(1.D0-Y1-Y2)/RHOL  + VAPOR%VIS*Y1/RHOV  &
                       + GAS%VIS*Y2/RHOG)
      TV(2) = RHO*(LIQUID%COND*(1.D0-Y1-Y2)/RHOL + VAPOR%COND*Y1/RHOV &
                       + GAS%COND*Y2/RHOG)
      
    
    END SUBROUTINE DETPROP
    !CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
    SUBROUTINE SET_IAPWS97(PROP)
      IMPLICIT NONE
      CLASS(T_PROP), INTENT(INOUT) :: PROP
      
      ALLOCATE(PROP%PROP_COEFF%H(0:3),PROP%PROP_COEFF%L(0:3))
      ALLOCATE(PROP%PROP_COEFF%HH(0:5,0:6),PROP%PROP_COEFF%LL(0:4,0:5))
      
      PROP%PROP_COEFF%H(0) =   1.D0          ;    PROP%PROP_COEFF%L(0) =   1.D0
      PROP%PROP_COEFF%H(1) =   0.978197D0    ;    PROP%PROP_COEFF%L(1) =   6.978267D0
      PROP%PROP_COEFF%H(2) =   0.579829D0    ;    PROP%PROP_COEFF%L(2) =   2.599096D0
      PROP%PROP_COEFF%H(3) = - 0.202354D0    ;    PROP%PROP_COEFF%L(3) = - 0.998254D0

      PROP%PROP_COEFF%HH(0,0) =   0.5132047D0;    PROP%PROP_COEFF%HH(1,0) =   0.3205656D0
      PROP%PROP_COEFF%HH(0,1) =   0.2151778D0;    PROP%PROP_COEFF%HH(1,1) =   0.7317883D0
      PROP%PROP_COEFF%HH(0,2) = - 0.2818107D0;    PROP%PROP_COEFF%HH(1,2) = - 1.070786D0
      PROP%PROP_COEFF%HH(0,3) =   0.1778064D0;    PROP%PROP_COEFF%HH(1,3) =   0.460504D0
      PROP%PROP_COEFF%HH(0,4) = - 0.0417661D0;    PROP%PROP_COEFF%HH(1,4) =   0.D0
      PROP%PROP_COEFF%HH(0,5) =   0.D0       ;    PROP%PROP_COEFF%HH(1,5) = - 0.01578386D0
      PROP%PROP_COEFF%HH(0,6) =   0.D0       ;    PROP%PROP_COEFF%HH(1,6) =   0.D0

      PROP%PROP_COEFF%HH(2,0) =   0.D0       ;    PROP%PROP_COEFF%HH(3,0) =   0.D0
      PROP%PROP_COEFF%HH(2,1) =   1.241044D0 ;    PROP%PROP_COEFF%HH(3,1) =   1.476783D0   
      PROP%PROP_COEFF%HH(2,2) = - 1.263184D0 ;    PROP%PROP_COEFF%HH(3,2) =   0.D0
      PROP%PROP_COEFF%HH(2,3) =   0.2340379D0;    PROP%PROP_COEFF%HH(3,3) = - 0.4924179D0
      PROP%PROP_COEFF%HH(2,4) =   0.D0       ;    PROP%PROP_COEFF%HH(3,4) =   0.1600435D0
      PROP%PROP_COEFF%HH(2,5) =   0.D0       ;    PROP%PROP_COEFF%HH(3,5) =   0.D0
      PROP%PROP_COEFF%HH(2,6) =   0.D0       ;    PROP%PROP_COEFF%HH(3,6) = - 0.003629481D0

      PROP%PROP_COEFF%HH(4,0) = - 0.7782567D0;    PROP%PROP_COEFF%HH(5,0) =   0.1885447D0
      PROP%PROP_COEFF%HH(4,1) =   0.D0       ;    PROP%PROP_COEFF%HH(5,1) =   0.D0
      PROP%PROP_COEFF%HH(4,2) =   0.D0       ;    PROP%PROP_COEFF%HH(5,2) =   0.D0
      PROP%PROP_COEFF%HH(4,3) =   0.D0       ;    PROP%PROP_COEFF%HH(5,3) =   0.D0
      PROP%PROP_COEFF%HH(4,4) =   0.D0       ;    PROP%PROP_COEFF%HH(5,4) =   0.D0
      PROP%PROP_COEFF%HH(4,5) =   0.D0       ;    PROP%PROP_COEFF%HH(5,5) =   0.D0
      PROP%PROP_COEFF%HH(4,6) =   0.D0       ;    PROP%PROP_COEFF%HH(5,6) =   0.D0

      PROP%PROP_COEFF%LL(0,0) =   1.3293046D0  ;    PROP%PROP_COEFF%LL(1,0) =   1.7018363D0
      PROP%PROP_COEFF%LL(0,1) = - 0.40452437D0 ;    PROP%PROP_COEFF%LL(1,1) = - 2.2156845D0
      PROP%PROP_COEFF%LL(0,2) =   0.24409490D0 ;    PROP%PROP_COEFF%LL(1,2) =   1.6511057D0
      PROP%PROP_COEFF%LL(0,3) =   0.018660751D0;    PROP%PROP_COEFF%LL(1,3) = - 0.76736002D0
      PROP%PROP_COEFF%LL(0,4) = - 0.12961068D0 ;    PROP%PROP_COEFF%LL(1,4) =   0.37283344D0
      PROP%PROP_COEFF%LL(0,5) =   0.044809953D0;    PROP%PROP_COEFF%LL(1,5) = - 0.11203160D0

      PROP%PROP_COEFF%LL(2,0) =   5.2246158D0  ;    PROP%PROP_COEFF%LL(3,0) =   8.7127675D0
      PROP%PROP_COEFF%LL(2,1) = - 10.124111D0  ;    PROP%PROP_COEFF%LL(3,1) = - 9.5000611D0 
      PROP%PROP_COEFF%LL(2,2) =   4.9874687D0  ;    PROP%PROP_COEFF%LL(3,2) =   4.3786606D0
      PROP%PROP_COEFF%LL(2,3) = - 0.27297694D0 ;    PROP%PROP_COEFF%LL(3,3) = - 0.91783782D0
      PROP%PROP_COEFF%LL(2,4) = - 0.43083393D0 ;    PROP%PROP_COEFF%LL(3,4) =   0.D0
      PROP%PROP_COEFF%LL(2,5) =   0.13333849D0 ;    PROP%PROP_COEFF%LL(3,5) =   0.D0

      PROP%PROP_COEFF%LL(4,0) = - 1.8525999D0
      PROP%PROP_COEFF%LL(4,1) =   0.93404690D0
      PROP%PROP_COEFF%LL(4,2) =   0.D0 
      PROP%PROP_COEFF%LL(4,3) =   0.D0  
      PROP%PROP_COEFF%LL(4,4) =   0.D0  
      PROP%PROP_COEFF%LL(4,5) =   0.D0      
    END SUBROUTINE SET_IAPWS97
    !CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
    SUBROUTINE SUTHERLAND(PROP,RHO,T,GAS)
      IMPLICIT NONE
      CLASS(T_PROP), INTENT(IN) :: PROP
      REAL(8), INTENT(IN) :: RHO,T
      TYPE(T_PROP2), INTENT(OUT) :: GAS
      
      GAS%VIS  = 1.716D-5*(T/273.D0)**1.5D0*384.D0/(T+111.D0)
      !GAS%COND = 0.0241D0*(T/273.d0)**1.5D0*467.D0/(T+194.D0)
      GAS%COND = 1004.64D0/0.72D0*GAS%VIS
      
    END SUBROUTINE SUTHERLAND
    !CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
    SUBROUTINE H2O_L_FIT(PROP,RHO,T,LIQUID)
      IMPLICIT NONE
      CLASS(T_PROP), INTENT(IN) :: PROP
      REAL(8), INTENT(IN) :: RHO,T
      TYPE(T_PROP2), INTENT(OUT) :: LIQUID
      LIQUID%VIS = 182.29652337358428D0 - 2.138464474127563D0*T + 0.008996951839771332D0*T**2 -                                            &
            0.00001603934897568428D0*T**3 + 1.0939578759971515D-8*T**4 - 2.4506596592636383D-12*T**5 -                             &
            0.29273617960175397D0*RHO+0.003279672826452764D0*T*RHO - 0.000012681282250878405D0*T**2*RHO +                       &
            1.8819010369235483D-8*T**3*RHO - 7.174526314439514D-12*T**4*RHO + 0.00011727665484754127D0*RHO**2 -                 &
            1.2515260581064558D-6*T*RHO**2 + 4.398491450268375D-9*T**2*RHO**2 -  5.096592868004483D-12*T**3*RHO**2

      LIQUID%COND = -2873.0920773919556D0 + 15.48896833182411D0*T + 0.023524601720546184D0*T**2 -                                          &
                0.00012515437912843214D0*T**3 + 1.2571696075836983D-7*T**4 - 3.65506256595383D-11*T**5 +                           &
                7.759665507709882D0*RHO - 0.049128575886152964D0*T*RHO + 1.253874327228879D-6*T**2*RHO +                        &
                1.1524510867042747D-7*T**3*RHO - 7.13949444766979D-11*T**4*RHO - 0.0064435874488161776D0*RHO**2 +               &
                0.00004365922518442514D0*T*RHO**2 - 1.8922275927041228D-8*T**2*RHO**2 -                                          &
                2.2527733573654497D-11*T**3*RHO**2 + 1.6453197045073728D-6*RHO**3 -                                              &
                1.1480302962074449D-8*T*RHO**3 + 3.8673838876035004D-12*T**2*RHO**3
    END SUBROUTINE H2O_L_FIT
    !CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
    SUBROUTINE H2O_V_FIT(PROP,RHO,T,VAPOR)
      IMPLICIT NONE
      CLASS(T_PROP), INTENT(IN) :: PROP
      REAL(8), INTENT(IN) :: RHO,T
      TYPE(T_PROP2), INTENT(OUT) :: VAPOR
      
      IF(T.LE.347.D0) THEN
        
    
        VAPOR%VIS = 0.00006818449331465641D0 - 8.671616545015754D-7*T + 4.88173425808012D-9*T**2 -                                      &
                1.3599835589740756D-11*T**3 + 1.9379102076160456D-14*T**4 - 1.1208856898634452D-17*T**5 -                          &
                4.622775716609354D-6*RHO + 3.38168423217193D-8*T*RHO - 8.953211921704184D-11*T**2*RHO +                         &
                8.761339390837146D-14*T**3*RHO - 1.1348488357421367D-17*T**4*RHO - 8.226703496364258D-6*RHO**2 +                &
                7.332470544669567D-8*T*RHO**2 - 2.1755598606357746D-10*T**2*RHO**2 +                                             &
                2.1494670148384862D-13*T**3*RHO**2       
    
        VAPOR%COND = 0.11886575771113059D0 - 0.0015105562801725884D0*T + 8.505634478148256D-6*T**2 -                                    &
                2.366884745998674D-8*T**3 + 3.3839656098631864D-11*T**4 - 1.9620891902741127D-14*T**5 +                            &
                0.14620233232119875D0*RHO - 0.0014524861710255087D0*T*RHO + 5.7372705663111265D-6*T**2*RHO -                    &
                1.044171880117295D-8*T**3*RHO + 7.295109646462301D-12*T**4*RHO + 0.02885120393186474D0*RHO**2 -                 &
                0.00023252426657073257D0*T*RHO**2 + 6.340942395897939D-7*T**2*RHO**2 -                                           &
                5.834854288655945D-10*T**3*RHO**2
    
      ELSE IF((T.GT.347.D0).AND.(T.LE.497.D0)) THEN
        
        VAPOR%VIS = 0.0000107258705777731D0 - 7.21490280880739D-08*T + 4.06640374383612D-10*T**2 - 8.23792620042554D-13*T**3 +          &
               9.25447858609362D-16*T**4 - 4.44448240737233D-19*T**5 - 9.08279871583265D-08*RHO - 5.61933547965227D-09*T*RHO +   &
               3.20992571868845D-11*T**2*RHO - 6.17485682146896D-14*T**3*RHO + 4.06271012748734D-17*T**4*RHO -                  &
               7.04507831064784D-09*RHO**2 + 7.506004218597D-11*T*RHO**2 - 2.03592074267208D-13*T**2*RHO**2 +                   &
               1.68256110210731D-16*T**3*RHO**2 - 2.8214178893372D-10*RHO**3 + 1.13313589486255D-12*T*RHO**3 -                  &
               1.14799089707064D-15*T**2*RHO**3
        
        VAPOR%COND = 0.037260605103079D0 - 0.000309862515077041D0*T + 1.42237484311934D-06*T**2 - 2.72685354163864D-09*T**3 +           &
                2.81019308084187D-12*T**4 - 1.18869002456954D-15*T**5 + 0.0497554818439424D0*RHO - 0.000344355978541611D0*T*RHO+ &
                9.4617453819575D-07*T**2*RHO - 1.20241981740753D-09*T**3*RHO + 5.89956268384747D-13*T**4*RHO +                  &
                0.0029213309201406D0*RHO**2 - 0.0000172026461736515D0*T*RHO**2 + 3.40364245522528D-08*T**2*RHO**2 -             &
                2.25906653399214D-11*T**3*RHO**2 + 0.0000132994306209143D0*RHO**3 - 5.37175064347615D-08*T*RHO**3 +             &
                5.4230558405697D-11*T**2*RHO**3 + 3.30349589454178D-08*RHO**4 - 6.13311964470895D-11*T*RHO**4
        
      ELSE !IF((T.GT.497.D0).AND.(T.LE.552.D0)) THEN
        
        VAPOR%VIS = 0.0000023502261679263D0 + 1.22881682908457D-08*T + 4.88348411442782D-11*T**2 - 2.72417906025321D-14*T**3 -          &
               9.05360803629602D-07*RHO + 3.68189722109223D-09*T*RHO - 5.35173691942562D-12*T**2*RHO +                          &
               2.76825488024515D-15*T**3*RHO + 1.09511339027305D-08*RHO**2 - 4.86043688227258D-11*T*RHO**2 +                    &
               7.67768900804663D-14*T**2*RHO**2 - 4.17985519045525D-17*T**3*RHO**2 - 3.14843417460143D-11*RHO**3 +              &
               9.35748491847235D-14*T*RHO**3 - 7.21198815398186D-17*T**2*RHO**3 + 4.91736078150471D-14*RHO**4 - &
               7.42494028104876D-17*T*RHO**4 - 2.45596742396709D-17*RHO**5
        VAPOR%COND =  -0.035413687557659D0 + 0.000243202319273188D0*T - 3.24154208932776D-07*T**2 + 2.43230121531957D-10*T**3 +         &
                  0.0309107955274621D0*RHO - 0.000155425773741589D0*T*RHO + 2.69487567888641D-07*T**2*RHO -                     &
                  1.59016538638875D-10*T**3*RHO + 0.000253858599042422D0*RHO**2 - 9.1317389636681D-07*T*RHO**2 +                &
                  8.30400668574453D-10*T**2*RHO**2 + 5.64534018741425D-07*RHO**3 - 1.05522723444757D-09*T*RHO**3 + &
                  9.89369240215337D-10*RHO**4
        
      END IF
    END SUBROUTINE H2O_V_FIT
    !CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
    SUBROUTINE IAPWS97(PROP,RHO,T,LIQUID)
      IMPLICIT NONE
      CLASS(T_PROP), INTENT(IN) :: PROP
      REAL(8), INTENT(IN) :: RHO,T
      TYPE(T_PROP2), INTENT(OUT) :: LIQUID
      
      REAL(8) :: TT,RR,MU0,MU1,MU2,LAM0,LAM1,LAM2,TT1
      INTEGER :: I,J
      
      TT = T/647.27D0
      RR = DMAX1(1.D-3,DMIN1(1332.4D0,RHO))/317.763D0
      TT1 = 647.27D0/T
      
      MU0 = DSQRT(TT)/(PROP%PROP_COEFF%H(0) + PROP%PROP_COEFF%H(1)*TT1 + PROP%PROP_COEFF%H(2)*TT1**2 &
                     + PROP%PROP_COEFF%H(3)*TT1**3)

      MU1 = 0.D0
      DO I = 0,5
        DO J = 0,6
          MU1 = MU1 + PROP%PROP_COEFF%HH(I,J)*(TT1-1.D0)**I*(RR-1.D0)**J
        END DO
      END DO
      MU1 = DEXP(RR*MU1)
      MU2 = 1.D0

      LIQUID%VIS = 55.071D-6*MU0*MU1*MU2


      LAM0 = DSQRT(TT)/(PROP%PROP_COEFF%L(0) + PROP%PROP_COEFF%L(1)*TT1 + PROP%PROP_COEFF%L(2)*TT1**2 &
                      + PROP%PROP_COEFF%L(3)*TT1**3)
      LAM1 = 0.D0
      DO I = 0,4
        DO J = 0,5
          LAM1 = LAM1 + PROP%PROP_COEFF%LL(I,J)*(TT1-1.D0)**I*(RR-1.D0)**J
        END DO
      END DO
      LAM1 = DEXP(RR*LAM1)
      LAM2 = 0.D0

      LIQUID%COND = 0.4945D0*(LAM0*LAM1 + LAM2)      
      
      
    END SUBROUTINE IAPWS97
    !CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
    SUBROUTINE STIFFENED(PROP,RHO,T,LIQUID)
      IMPLICIT NONE
      CLASS(T_PROP), INTENT(IN) :: PROP
      REAL(8), INTENT(IN) :: RHO,T
      TYPE(T_PROP2), INTENT(OUT) :: LIQUID
      
      LIQUID%VIS  = 1.788D-3*DEXP(-1.704D0 - 5.306D0*273.D0/T + 7.003*(273.D0/T)**2)
      LIQUID%COND = -0.000009438D0*T**2 + 0.007294D0*T - 0.7284D0
      
    END SUBROUTINE STIFFENED
    !CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
    SUBROUTINE N2_L_FIT(PROP,RHO,T,LIQUID)
      IMPLICIT NONE
      CLASS(T_PROP), INTENT(IN) :: PROP
      REAL(8), INTENT(IN) :: RHO,T
      TYPE(T_PROP2), INTENT(OUT) :: LIQUID
      LIQUID = T_PROP2(0.D0,0.D0)
    END SUBROUTINE N2_L_FIT
    !CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
    SUBROUTINE N2_V_FIT(PROP,RHO,T,VAPOR)
      IMPLICIT NONE
      CLASS(T_PROP), INTENT(IN) :: PROP
      REAL(8), INTENT(IN) :: RHO,T
      TYPE(T_PROP2), INTENT(OUT) :: VAPOR
      VAPOR = T_PROP2(0.D0,0.D0)
    END SUBROUTINE N2_V_FIT
    !CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
    SUBROUTINE O2_L_FIT(PROP,RHO,T,LIQUID)
      IMPLICIT NONE
      CLASS(T_PROP), INTENT(IN) :: PROP
      REAL(8), INTENT(IN) :: RHO,T
      TYPE(T_PROP2), INTENT(OUT) :: LIQUID
      LIQUID = T_PROP2(0.D0,0.D0)
    END SUBROUTINE O2_L_FIT
    !CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
    SUBROUTINE O2_V_FIT(PROP,RHO,T,VAPOR)
      IMPLICIT NONE
      CLASS(T_PROP), INTENT(IN) :: PROP
      REAL(8), INTENT(IN) :: RHO,T
      TYPE(T_PROP2), INTENT(OUT) :: VAPOR
      VAPOR = T_PROP2(0.D0,0.D0)
    END SUBROUTINE O2_V_FIT
    !CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
    SUBROUTINE H2_L_FIT(PROP,RHO,T,LIQUID)
      IMPLICIT NONE
      CLASS(T_PROP), INTENT(IN) :: PROP
      REAL(8), INTENT(IN) :: RHO,T
      TYPE(T_PROP2), INTENT(OUT) :: LIQUID
      LIQUID = T_PROP2(0.D0,0.D0)
    END SUBROUTINE H2_L_FIT
    !CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
    SUBROUTINE H2_V_FIT(PROP,RHO,T,VAPOR)
      IMPLICIT NONE
      CLASS(T_PROP), INTENT(IN) :: PROP
      REAL(8), INTENT(IN) :: RHO,T
      TYPE(T_PROP2), INTENT(OUT) :: VAPOR
      VAPOR = T_PROP2(0.D0,0.D0)
    END SUBROUTINE H2_V_FIT
    !CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
END MODULE PROP_MODULE
