MODULE LHS_MODULE
  USE CONFIG_MODULE
  USE GRID_MODULE
  USE VARIABLE_MODULE
  IMPLICIT NONE
  PRIVATE
  PUBLIC :: T_LHS,T_LHS_FLOWONLY,T_LHS_FLOWTURBALL
  
  TYPE, ABSTRACT :: T_LHS
    PRIVATE
    LOGICAL :: L_AXI
    INTEGER :: NPV,NDV,NTV,NGRD,NSTEADY
    REAL(8) :: UREF,STR,DT_PHY
    REAL(8), POINTER :: CX1(:),CX2(:),EX1(:),EX2(:)
    REAL(8), POINTER :: PV(:),TV(:),DV(:),GRD(:),DT
    REAL(8), POINTER :: C(:),T(:),C0(:),T0(:)
    REAL(8), DIMENSION(:,:), ALLOCATABLE :: X
    PROCEDURE(P_GETSNDP2), POINTER :: GETSNDP2
    PROCEDURE(P_GETEIGENVIS), POINTER :: GETEIGENVIS
    PROCEDURE(P_EIGEN), POINTER :: EIGEN
    CONTAINS
      PROCEDURE :: CONSTRUCT
      PROCEDURE :: DESTRUCT
      PROCEDURE :: SETNORM ! (CX1,CX2,EX1,EX2)
      PROCEDURE :: SETGRD  ! VOL,XCEN,YCEN,YDNS
      PROCEDURE :: SETPV   ! P,U,V,T,Y1,Y2,K,O
      PROCEDURE :: SETDV   ! RHO,H,RHOL,RHOV,RHOG,SND2,DRDP,DRDT,DRDY1,DRDY2,DHDP,DHDT,DHDY1,DHDY2,DRDPV,DRDTV,DRDPL,DRDTL
      PROCEDURE :: SETTV   ! VIS,COND,EMUT
      PROCEDURE :: SETDT   ! DT
      PROCEDURE :: SETC
      PROCEDURE :: SETT
      PROCEDURE :: GETX
      PROCEDURE(P_INVERSE), DEFERRED :: INVERSE
  END TYPE T_LHS
  
  TYPE, EXTENDS(T_LHS) :: T_LHS_FLOWONLY
    CONTAINS
      PROCEDURE :: INVERSE => FLOWONLY
  END TYPE T_LHS_FLOWONLY
  
  TYPE, EXTENDS(T_LHS) :: T_LHS_FLOWTURBALL
    CONTAINS
      PROCEDURE :: INVERSE => FLOWTURBALL
  END TYPE T_LHS_FLOWTURBALL
  
  ABSTRACT INTERFACE
    SUBROUTINE P_INVERSE(LHS)
      IMPORT T_LHS
      IMPLICIT NONE
      CLASS(T_LHS), INTENT(INOUT) :: LHS
    END SUBROUTINE P_INVERSE  
  END INTERFACE

  INTERFACE
    FUNCTION P_GETSNDP2(LHS,UUU2) RESULT(SNDP2)
      IMPORT T_LHS
      IMPLICIT NONE
      CLASS(T_LHS), INTENT(IN) :: LHS
      REAL(8), INTENT(IN) :: UUU2
      REAL(8) :: SNDP2
    END FUNCTION P_GETSNDP2
    
    FUNCTION P_GETEIGENVIS(LHS) RESULT(EIGENVIS)
      IMPORT T_LHS
      IMPLICIT NONE
      CLASS(T_LHS), INTENT(IN) :: LHS
      REAL(8) :: EIGENVIS
    END FUNCTION P_GETEIGENVIS
    
    FUNCTION P_EIGEN(LHS,NXL,NXR) RESULT(LAMDA)
      IMPORT T_LHS
      IMPLICIT NONE
      CLASS(T_LHS), INTENT(IN) :: LHS
      REAL(8), POINTER, INTENT(IN) :: NXL(:),NXR(:)
      REAL(8) :: LAMDA
    END FUNCTION P_EIGEN
    
  END INTERFACE
  
  CONTAINS
    !CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
    SUBROUTINE CONSTRUCT(LHS,CONFIG,GRID,VARIABLE)
      IMPLICIT NONE
      CLASS(T_LHS), INTENT(OUT) :: LHS
      TYPE(T_CONFIG), INTENT(IN) :: CONFIG
      TYPE(T_GRID), INTENT(IN) :: GRID
      TYPE(T_VARIABLE), INTENT(IN) :: VARIABLE

      LHS%NGRD = GRID%GETNGRD()
      LHS%NPV  = VARIABLE%GETNPV()
      LHS%NTV  = VARIABLE%GETNTV()
      LHS%NDV  = VARIABLE%GETNDV()
      
      LHS%UREF = CONFIG%GETUREF()
      LHS%STR  = CONFIG%GETSTR()
      LHS%DT_PHY = CONFIG%GETDT_PHY()
      LHS%NSTEADY = CONFIG%GETNSTEADY()
      
      SELECT CASE(CONFIG%GETPREC())
      CASE(0)
        LHS%GETSNDP2 => NO_PREC
      CASE(1)
        LHS%GETSNDP2 => STEADY_PREC
      CASE(2)
        LHS%GETSNDP2 => UNSTEADY_PREC
      END SELECT
      

      SELECT CASE(CONFIG%GETTIMEMETHOD())
      CASE(1,2)
        LHS%EIGEN => EIGEN_EXPLICIT
        LHS%GETEIGENVIS => EULER
      CASE(3)
        LHS%EIGEN => EIGEN_IMPLICIT
        SELECT CASE(CONFIG%GETITURB())
        CASE(-1,0)
          LHS%GETEIGENVIS => TURBULENT
        CASE(-2)
          LHS%GETEIGENVIS => LAMINAR
        CASE(-3)
          LHS%GETEIGENVIS => EULER
        END SELECT
      END SELECT
      
      SELECT CASE(CONFIG%GETNAXI())
      CASE(0)
        LHS%L_AXI = .FALSE.
      CASE(1)
        LHS%L_AXI = .TRUE.
      END SELECT
      
      ALLOCATE(LHS%X(LHS%NPV,LHS%NPV))
      ALLOCATE(LHS%C0(4))
      ALLOCATE(LHS%T0(4))
      LHS%C0 = 0.D0
      LHS%T0 = 0.D0
      LHS%C => LHS%C0
      LHS%T => LHS%T0
    END SUBROUTINE CONSTRUCT
    !CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
    SUBROUTINE DESTRUCT(LHS)
      IMPLICIT NONE
      CLASS(T_LHS), INTENT(INOUT) :: LHS

      IF(ASSOCIATED(LHS%CX1))         NULLIFY(LHS%CX1)          
      IF(ASSOCIATED(LHS%CX2))         NULLIFY(LHS%CX2)          
      IF(ASSOCIATED(LHS%EX1))         NULLIFY(LHS%EX1)          
      IF(ASSOCIATED(LHS%EX2))         NULLIFY(LHS%EX2)          
      IF(ASSOCIATED(LHS%GRD))         NULLIFY(LHS%GRD)          
      IF(ASSOCIATED(LHS%PV))          NULLIFY(LHS%PV)           
      IF(ASSOCIATED(LHS%DV))          NULLIFY(LHS%DV)           
      IF(ASSOCIATED(LHS%TV))          NULLIFY(LHS%TV)           
      IF(ASSOCIATED(LHS%DT))          NULLIFY(LHS%DT)           
      IF(ASSOCIATED(LHS%C))           NULLIFY(LHS%C)            
      IF(ASSOCIATED(LHS%T))           NULLIFY(LHS%T)            
      IF(ASSOCIATED(LHS%GETSNDP2))    NULLIFY(LHS%GETSNDP2)     
      IF(ASSOCIATED(LHS%GETEIGENVIS)) NULLIFY(LHS%GETEIGENVIS)  
      IF(ASSOCIATED(LHS%EIGEN))       NULLIFY(LHS%EIGEN)        

      DEALLOCATE(LHS%X,LHS%C0,LHS%T0)
    END SUBROUTINE DESTRUCT
    !CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
    SUBROUTINE SETNORM(LHS,CX1,CX2,EX1,EX2)
      IMPLICIT NONE
      CLASS(T_LHS), INTENT(INOUT) :: LHS
      REAL(8), INTENT(IN), TARGET :: CX1(2),CX2(2),EX1(2),EX2(2)
      
      LHS%CX1 => CX1
      LHS%CX2 => CX2
      LHS%EX1 => EX1
      LHS%EX2 => EX2
      
    END SUBROUTINE SETNORM
    !CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
    SUBROUTINE SETGRD(LHS,GRD)
      IMPLICIT NONE
      CLASS(T_LHS), INTENT(INOUT) :: LHS
      REAL(8), INTENT(IN), TARGET :: GRD(LHS%NGRD)
      
      LHS%GRD => GRD
      
    END SUBROUTINE SETGRD
    !CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
    SUBROUTINE SETPV(LHS,PV)
      IMPLICIT NONE
      CLASS(T_LHS), INTENT(INOUT) :: LHS
      REAL(8), INTENT(IN), TARGET :: PV(LHS%NPV)
      
      LHS%PV => PV
      
    END SUBROUTINE SETPV
    !CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
    SUBROUTINE SETDV(LHS,DV)
      IMPLICIT NONE
      CLASS(T_LHS), INTENT(INOUT) :: LHS
      REAL(8), INTENT(IN), TARGET :: DV(LHS%NDV)
      
      LHS%DV => DV
      
    END SUBROUTINE SETDV
    !CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
    SUBROUTINE SETTV(LHS,TV)
      IMPLICIT NONE
      CLASS(T_LHS), INTENT(INOUT) :: LHS
      REAL(8), INTENT(IN), TARGET :: TV(LHS%NTV)
      
      LHS%TV => TV
      
    END SUBROUTINE SETTV
    !CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
    SUBROUTINE SETDT(LHS,DT)
      IMPLICIT NONE
      CLASS(T_LHS), INTENT(INOUT) :: LHS
      REAL(8), INTENT(IN), TARGET :: DT
      
      LHS%DT => DT
      
    END SUBROUTINE SETDT
    !CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
    SUBROUTINE SETC(LHS,C)
      IMPLICIT NONE
      CLASS(T_LHS), INTENT(INOUT) :: LHS
      REAL(8), INTENT(IN), TARGET :: C(4)
      
      LHS%C => C
      
    END SUBROUTINE SETC
    !CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
    SUBROUTINE SETT(LHS,T)
      IMPLICIT NONE
      CLASS(T_LHS), INTENT(INOUT) :: LHS
      REAL(8), INTENT(IN), TARGET :: T(4)
      
      LHS%T => T
      
    END SUBROUTINE SETT
    !CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
    SUBROUTINE FLOWONLY(LHS)
      IMPLICIT NONE
      CLASS(T_LHS_FLOWONLY), INTENT(INOUT) :: LHS
      REAL(8) :: UV2,SS,RR,HST,B1,DR,DHDP1
      REAL(8) :: CROSS1,CROSS2,CROSS3,A,B
      REAL(8) :: MM,KY1,KY2,X14,X44,X54
      REAL(8) :: C(4)

      A = LHS%GRD(1)/LHS%DT + LHS%EIGEN(LHS%CX1,LHS%CX2) + LHS%EIGEN(LHS%EX1,LHS%EX2)
      B = (DBLE(LHS%NSTEADY)*1.5D0*LHS%GRD(1)/LHS%DT_PHY + 2.D0*LHS%GETEIGENVIS()/LHS%GRD(1))/A      
      C = LHS%C*LHS%GRD(1)/A
      
      B1 = 1.D0+B
      UV2 = LHS%PV(2)**2+LHS%PV(3)**2
      SS = LHS%DV(1)*LHS%DV(12)*(1.D0/LHS%GETSNDP2(UV2)+B/LHS%DV(6))
      RR = 1.D0/LHS%GETSNDP2(UV2)-1.D0/LHS%DV(6)+B1*LHS%DV(7)
      HST = LHS%DV(2)-0.5D0*(LHS%PV(2)**2+LHS%PV(3)**2)-LHS%DV(13)*LHS%PV(5)-LHS%DV(14)*LHS%PV(6)
      DR = LHS%DV(1)+LHS%DV(9)*LHS%PV(5)+LHS%DV(10)*LHS%PV(6)
      DHDP1 = -1.D0 + LHS%DV(11)*LHS%DV(1)
      CROSS1 = LHS%DV(14)*LHS%DV(8)-LHS%DV(12)*LHS%DV(10)
      CROSS2 = LHS%DV(13)*LHS%DV(10)-LHS%DV(14)*LHS%DV(9)
      CROSS3 = LHS%DV(13)*LHS%DV(8)-LHS%DV(12)*LHS%DV(9)
      KY1 = LHS%DV(13)*LHS%DV(1)*RR - LHS%DV(9)*B1*DHDP1
      KY2 = LHS%DV(14)*LHS%DV(1)*RR - LHS%DV(10)*B1*DHDP1
      MM = SS*B1*LHS%DV(1)+C(1)*B1*LHS%DV(1)*(LHS%DV(13)*LHS%DV(8)-LHS%DV(12)*LHS%DV(9))-C(2)*KY1+C(3)*SS
      X14 = ( C(2)*LHS%DV(9)-LHS%DV(8)*(C(3)+B1*LHS%DV(1)))/MM
      X44 = -(B1*C(1)*LHS%DV(9)-(C(3)+LHS%DV(1)*B1)*RR)/MM/B1
      X54 =  (B1*C(1)*LHS%DV(8)-C(2)*RR)/MM/B1
      
      LHS%X(1,1) = (B1*LHS%DV(1)*( LHS%DV(8)*HST + LHS%DV(12)*DR ) &
               - C(2)*( LHS%DV(9)*(HST+LHS%DV(13)*LHS%PV(5)) + LHS%DV(13)*(DR-LHS%DV(9)*LHS%PV(5)) )  &
               + C(3)*( LHS%DV(8)*(HST+LHS%DV(13)*LHS%PV(5)) + LHS%DV(12)*(DR-LHS%DV(9)*LHS%PV(5)) ) &
               + C(4)*CROSS3*LHS%PV(6) )/MM
      LHS%X(1,2) = -X14*LHS%PV(2)
      LHS%X(1,3) = -X14*LHS%PV(3)
      LHS%X(1,4) = X14
      LHS%X(1,5) = B1*CROSS3*LHS%DV(1)/MM
      LHS%X(1,6) = ( B1*LHS%DV(1)*CROSS1 + C(2)*CROSS2 + C(3)*CROSS1 - C(4)*CROSS3 )/MM

      LHS%X(2,1) = -LHS%PV(2)/LHS%DV(1)/B1
      LHS%X(2,2) = 1.D0/LHS%DV(1)/B1
      LHS%X(2,3) = 0.D0
      LHS%X(2,4) = 0.D0
      LHS%X(2,5) = 0.D0
      LHS%X(2,6) = 0.D0

      LHS%X(3,1) = -LHS%PV(3)/LHS%DV(1)/B1
      LHS%X(3,2) = 0.D0
      LHS%X(3,3) = 1.D0/LHS%DV(1)/B1
      LHS%X(3,4) = 0.D0
      LHS%X(3,5) = 0.D0
      LHS%X(3,6) = 0.D0  

      LHS%X(4,1) = ( - B1*LHS%DV(1)*( HST*LHS%DV(1)*RR+B1*DHDP1*DR )   &
                 + C(1)*B1*LHS%DV(1)*(LHS%DV(9)*(HST+LHS%DV(13)*LHS%PV(5)) + LHS%DV(13)*(LHS%DV(1)+LHS%DV(10)*LHS%PV(6))) &
                 - C(3)*(LHS%DV(1)*RR*(HST+LHS%DV(13)*LHS%PV(5))+B1*DHDP1*(LHS%DV(1)+LHS%DV(10)*LHS%PV(6)))               & 
                 - C(4)*KY1*LHS%PV(6) )/MM/LHS%DV(1)/B1
      LHS%X(4,2) = -X44*LHS%PV(2)
      LHS%X(4,3) = -X44*LHS%PV(3)
      LHS%X(4,4) = X44
      LHS%X(4,5) = -KY1/MM
      LHS%X(4,6) = ( -LHS%DV(1)*B1*(KY2 + C(1)*CROSS2) - C(3)*KY2 + C(4)*KY1 )/MM/LHS%DV(1)/B1

      LHS%X(5,1) = ( - B1*LHS%DV(1)*SS*LHS%PV(5) - C(1)*B1*LHS%DV(1)*(LHS%DV(8)*(HST+LHS%DV(13)*LHS%PV(5))+LHS%DV(12)*(LHS%DV(1)+LHS%DV(10)*LHS%PV(6))) &
                 + C(2)*(LHS%DV(1)*RR*(HST+LHS%DV(13)*LHS%PV(5)) + B1*DHDP1*(LHS%DV(1)+LHS%DV(10)*LHS%PV(6))) + C(4)*SS*LHS%PV(6) )/MM/LHS%DV(1)/B1
      LHS%X(5,2) = - X54*LHS%PV(2)
      LHS%X(5,3) = - X54*LHS%PV(3)
      LHS%X(5,4) = X54
      LHS%X(5,5) = SS/MM
      LHS%X(5,6) = ( -C(1)*B1*LHS%DV(1)*CROSS1 + C(2)*KY2 - C(4)*SS)/MM/LHS%DV(1)/B1

      LHS%X(6,1) = -LHS%PV(6)/LHS%DV(1)/B1
      LHS%X(6,2) = 0.D0
      LHS%X(6,3) = 0.D0
      LHS%X(6,4) = 0.D0
      LHS%X(6,5) = 0.D0
      LHS%X(6,6) = 1.D0/LHS%DV(1)/B1
      
      IF(LHS%L_AXI) A = A*LHS%GRD(3)
      LHS%X(:,:) = LHS%X(:,:)/A
    END SUBROUTINE FLOWONLY
    !CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
    SUBROUTINE FLOWTURBALL(LHS)
      IMPLICIT NONE
      CLASS(T_LHS_FLOWTURBALL), INTENT(INOUT) :: LHS
      REAL(8) :: UV2,SS,RR,HST,B1,DR,DHDP1
      REAL(8) :: CROSS1,CROSS2,CROSS3,A,B
      REAL(8) :: MM,KY1,KY2,TT,X14,X44,X54
      REAL(8) :: C(4),T(4)

      A = LHS%GRD(1)/LHS%DT + LHS%EIGEN(LHS%CX1,LHS%CX2) + LHS%EIGEN(LHS%EX1,LHS%EX2)
      B = (DBLE(LHS%NSTEADY)*1.5D0*LHS%GRD(1)/LHS%DT_PHY + 2.D0*LHS%GETEIGENVIS()/LHS%GRD(1))/A      
      C = LHS%C*LHS%GRD(1)/A
      T = LHS%T*LHS%GRD(1)/A

      B1 = 1.D0+B
      UV2 = LHS%PV(2)**2+LHS%PV(3)**2
      SS = LHS%DV(1)*LHS%DV(12)*(1.D0/LHS%GETSNDP2(UV2)+B/LHS%DV(6))
      RR = 1.D0/LHS%GETSNDP2(UV2)-1.D0/LHS%DV(6)+B1*LHS%DV(7)
      HST = LHS%DV(2)-0.5D0*(LHS%PV(2)**2+LHS%PV(3)**2)-LHS%DV(13)*LHS%PV(5)-LHS%DV(14)*LHS%PV(6)
      DR = LHS%DV(1)+LHS%DV(9)*LHS%PV(5)+LHS%DV(10)*LHS%PV(6)
      DHDP1 = -1.D0 + LHS%DV(11)*LHS%DV(1)
      CROSS1 = LHS%DV(14)*LHS%DV(8)-LHS%DV(12)*LHS%DV(10)
      CROSS2 = LHS%DV(13)*LHS%DV(10)-LHS%DV(14)*LHS%DV(9)
      CROSS3 = LHS%DV(13)*LHS%DV(8)-LHS%DV(12)*LHS%DV(9)
      KY1 = LHS%DV(13)*LHS%DV(1)*RR - LHS%DV(9)*B1*DHDP1
      KY2 = LHS%DV(14)*LHS%DV(1)*RR - LHS%DV(10)*B1*DHDP1
      MM = SS*B1*LHS%DV(1)+C(1)*B1*LHS%DV(1)*CROSS3-C(2)*KY1+C(3)*SS
      TT = T(2)*T(3)-(T(1)+B1*LHS%DV(1))*(T(4)+B1*LHS%DV(1)) 
      X14 = ( C(2)*LHS%DV(9)-LHS%DV(8)*(C(3)+B1*LHS%DV(1)))/MM
      X44 = -(B1*C(1)*LHS%DV(9)-(C(3)+LHS%DV(1)*B1)*RR)/MM/B1
      X54 =  (B1*C(1)*LHS%DV(8)-C(2)*RR)/MM/B1
      
      LHS%X(1,1) = (B1*LHS%DV(1)*( LHS%DV(8)*HST + LHS%DV(12)*DR ) &
               - C(2)*( LHS%DV(9)*(HST+LHS%DV(13)*LHS%PV(5)) + LHS%DV(13)*(DR-LHS%DV(9)*LHS%PV(5)) )  &
               + C(3)*( LHS%DV(8)*(HST+LHS%DV(13)*LHS%PV(5)) + LHS%DV(12)*(DR-LHS%DV(9)*LHS%PV(5)) ) &
               + C(4)*CROSS3*LHS%PV(6) )/MM
      LHS%X(1,2) = -X14*LHS%PV(2)
      LHS%X(1,3) = -X14*LHS%PV(3)
      LHS%X(1,4) = X14
      LHS%X(1,5) = B1*CROSS3*LHS%DV(1)/MM
      LHS%X(1,6) = ( B1*LHS%DV(1)*CROSS1 + C(2)*CROSS2 + C(3)*CROSS1 - C(4)*CROSS3 )/MM
      LHS%X(1,7) = 0.D0
      LHS%X(1,8) = 0.D0
      
      LHS%X(2,1) = -LHS%PV(2)/LHS%DV(1)/B1
      LHS%X(2,2) = 1.D0/LHS%DV(1)/B1
      LHS%X(2,3) = 0.D0
      LHS%X(2,4) = 0.D0
      LHS%X(2,5) = 0.D0
      LHS%X(2,6) = 0.D0
      LHS%X(2,7) = 0.D0
      LHS%X(2,8) = 0.D0
      
      LHS%X(3,1) = -LHS%PV(3)/LHS%DV(1)/B1
      LHS%X(3,2) = 0.D0
      LHS%X(3,3) = 1.D0/LHS%DV(1)/B1
      LHS%X(3,4) = 0.D0
      LHS%X(3,5) = 0.D0
      LHS%X(3,6) = 0.D0  
      LHS%X(3,7) = 0.D0
      LHS%X(3,8) = 0.D0
        
      LHS%X(4,1) = ( - B1*LHS%DV(1)*( HST*LHS%DV(1)*RR+B1*DHDP1*DR )   &
                 + C(1)*B1*LHS%DV(1)*(LHS%DV(9)*(HST+LHS%DV(13)*LHS%PV(5)) + LHS%DV(13)*(LHS%DV(1)+LHS%DV(10)*LHS%PV(6))) &
                 - C(3)*(LHS%DV(1)*RR*(HST+LHS%DV(13)*LHS%PV(5))+B1*DHDP1*(LHS%DV(1)+LHS%DV(10)*LHS%PV(6)))               & 
                 - C(4)*KY1*LHS%PV(6) )/MM/LHS%DV(1)/B1
      LHS%X(4,2) = -X44*LHS%PV(2)
      LHS%X(4,3) = -X44*LHS%PV(3)
      LHS%X(4,4) = X44
      LHS%X(4,5) = -KY1/MM
      LHS%X(4,6) = ( -LHS%DV(1)*B1*(KY2 + C(1)*CROSS2) - C(3)*KY2 + C(4)*KY1 )/MM/LHS%DV(1)/B1
      LHS%X(4,7) = 0.D0
      LHS%X(4,8) = 0.D0
        
      LHS%X(5,1) = ( - B1*LHS%DV(1)*SS*LHS%PV(5) - C(1)*B1*LHS%DV(1)*(LHS%DV(8)*(HST+LHS%DV(13)*LHS%PV(5))+LHS%DV(12)*(LHS%DV(1)+LHS%DV(10)*LHS%PV(6))) &
                 + C(2)*(LHS%DV(1)*RR*(HST+LHS%DV(13)*LHS%PV(5)) + B1*DHDP1*(LHS%DV(1)+LHS%DV(10)*LHS%PV(6)))+ C(4)*SS*LHS%PV(6) )/MM/LHS%DV(1)/B1
      LHS%X(5,2) = - X54*LHS%PV(2)
      LHS%X(5,3) = - X54*LHS%PV(3)
      LHS%X(5,4) = X54
      LHS%X(5,5) = SS/MM
      LHS%X(5,6) = ( -C(1)*B1*LHS%DV(1)*CROSS1 + C(2)*KY2 - C(4)*SS)/MM/LHS%DV(1)/B1
      LHS%X(5,7) = 0.D0
      LHS%X(5,8) = 0.D0
        
      LHS%X(6,1) = -LHS%PV(6)/LHS%DV(1)/B1
      LHS%X(6,2) = 0.D0
      LHS%X(6,3) = 0.D0
      LHS%X(6,4) = 0.D0
      LHS%X(6,5) = 0.D0
      LHS%X(6,6) = 1.D0/LHS%DV(1)/B1
      LHS%X(6,7) = 0.D0
      LHS%X(6,8) = 0.D0
       
      LHS%X(7,1) = ( - LHS%PV(8)*T(2) + LHS%PV(7)*(B1*LHS%DV(1)+T(4)) )/TT
      LHS%X(7,2) = 0.D0
      LHS%X(7,3) = 0.D0
      LHS%X(7,4) = 0.D0
      LHS%X(7,5) = 0.D0
      LHS%X(7,6) = 0.D0
      LHS%X(7,7) = -(B1*LHS%DV(1)+T(4))/TT
      LHS%X(7,8) = T(2)/TT

      LHS%X(8,1) = -( - LHS%PV(8)*(B1*LHS%DV(1)+T(1)) + LHS%PV(7)*T(3) )/TT
      LHS%X(8,2) = 0.D0
      LHS%X(8,3) = 0.D0
      LHS%X(8,4) = 0.D0
      LHS%X(8,5) = 0.D0
      LHS%X(8,6) = 0.D0
      LHS%X(8,7) = T(3)/TT
      LHS%X(8,8) = -(B1*LHS%DV(1)+T(1))/TT
      
      IF(LHS%L_AXI) A = A*LHS%GRD(3)
      LHS%X(:,:) = LHS%X(:,:)/A      
    END SUBROUTINE FLOWTURBALL
    !CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
    FUNCTION EIGEN_EXPLICIT(LHS,NXL,NXR) RESULT(LAMDA)
      IMPLICIT NONE
      CLASS(T_LHS), INTENT(IN) :: LHS
      REAL(8), POINTER, INTENT(IN) :: NXL(:),NXR(:)
      REAL(8) :: LAMDA

      LAMDA = 0.D0
      
    END FUNCTION EIGEN_EXPLICIT
    !CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
    FUNCTION EIGEN_IMPLICIT(LHS,NXL,NXR) RESULT(LAMDA)
      IMPLICIT NONE
      CLASS(T_LHS), INTENT(IN) :: LHS
      REAL(8), POINTER, INTENT(IN) :: NXL(:),NXR(:)
      REAL(8) :: LAMDA
      REAL(8) :: UV2,SNDP2,OX,OY,DS,U,UP,D
      REAL(8), PARAMETER :: CAPPA = 1.05D0
      
      UV2 = LHS%PV(2)**2+LHS%PV(3)**2
      SNDP2 = LHS%GETSNDP2(UV2)
      
      OX = 0.5D0*(NXL(1)+NXR(1))
      OY = 0.5D0*(NXL(2)+NXR(2))
      DS = OX**2+OY**2
      U  = OX*LHS%PV(2)+OY*LHS%PV(3)
      UP = (1.D0+SNDP2/LHS%DV(6))*U
      D  = DSQRT(U**2*(1.D0-SNDP2/LHS%DV(6))**2+4.D0*SNDP2*DS)
      LAMDA = CAPPA*(DABS(UP)+D)
      
    END FUNCTION EIGEN_IMPLICIT
    !CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
    FUNCTION EULER(LHS) RESULT(EIGENVIS)
      IMPLICIT NONE
      CLASS(T_LHS), INTENT(IN) :: LHS
      REAL(8) :: EIGENVIS
      
      EIGENVIS = 0.D0
      
    END FUNCTION EULER
    !CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
    FUNCTION LAMINAR(LHS) RESULT(EIGENVIS)
      IMPLICIT NONE
      CLASS(T_LHS), INTENT(IN) :: LHS
      REAL(8) :: EIGENVIS

      EIGENVIS = DMAX1(LHS%DV(7)*LHS%TV(2)/(LHS%DV(8)     &
                       +LHS%DV(12)*LHS%DV(7)*LHS%DV(1)    &
                       -LHS%DV(11)*LHS%DV(8)*LHS%DV(1)),  &
                       4.D0/3.D0*LHS%TV(1)/LHS%DV(1))
      EIGENVIS = EIGENVIS*( 0.25D0*(LHS%CX1(1)+LHS%CX2(1))**2 + 0.25D0*(LHS%CX1(2)+LHS%CX2(2))**2 &
                          + 0.25D0*(LHS%EX1(1)+LHS%EX2(1))**2 + 0.25D0*(LHS%EX1(2)+LHS%EX2(2))**2)
    END FUNCTION LAMINAR
    !CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
    FUNCTION TURBULENT(LHS) RESULT(EIGENVIS)
      IMPLICIT NONE
      CLASS(T_LHS), INTENT(IN) :: LHS
      REAL(8) :: EIGENVIS
      REAL(8), PARAMETER :: PR_T = 0.9D0
   
      EIGENVIS = DMAX1(LHS%DV(7)*(LHS%TV(2)+LHS%DV(12)*LHS%TV(3)/PR_T) &
                      /(LHS%DV(8)+LHS%DV(12)*LHS%DV(7)*LHS%DV(1)       &
                       -LHS%DV(11)*LHS%DV(8)*LHS%DV(1)),               &
                       4.D0/3.D0*(LHS%TV(1)+LHS%TV(3))/LHS%DV(1))
      EIGENVIS = EIGENVIS*( 0.25D0*(LHS%CX1(1)+LHS%CX2(1))**2 + 0.25D0*(LHS%CX1(2)+LHS%CX2(2))**2 &
                          + 0.25D0*(LHS%EX1(1)+LHS%EX2(1))**2 + 0.25D0*(LHS%EX1(2)+LHS%EX2(2))**2)
    END FUNCTION TURBULENT
    !CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
    FUNCTION NO_PREC(LHS,UUU2) RESULT(SNDP2)
      IMPLICIT NONE
      CLASS(T_LHS), INTENT(IN) :: LHS
      REAL(8), INTENT(IN) :: UUU2
      REAL(8) :: SNDP2
      
      SNDP2 = LHS%DV(6)
      
    END FUNCTION NO_PREC
    !CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
    FUNCTION STEADY_PREC(LHS,UUU2) RESULT(SNDP2)
      IMPLICIT NONE
      CLASS(T_LHS), INTENT(IN) :: LHS
      REAL(8), INTENT(IN) :: UUU2
      REAL(8) :: SNDP2  
      
      SNDP2 = DMIN1(LHS%DV(6),DMAX1(UUU2,LHS%UREF**2))
      
    END FUNCTION STEADY_PREC
    !CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
    FUNCTION UNSTEADY_PREC(LHS,UUU2) RESULT(SNDP2)
      IMPLICIT NONE
      CLASS(T_LHS), INTENT(IN) :: LHS
      REAL(8), INTENT(IN) :: UUU2
      REAL(8) :: SNDP2  
      
      SNDP2 = DMIN1(LHS%DV(6),DMAX1(UUU2,LHS%UREF**2,LHS%STR**2*UUU2))
      
    END FUNCTION UNSTEADY_PREC
    !CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
    FUNCTION GETX(LHS,I,J)
      IMPLICIT NONE
      CLASS(T_LHS), INTENT(IN) :: LHS
      INTEGER, INTENT(IN) :: I,J
      REAL(8) :: GETX
      
      GETX = LHS%X(I,J)
      
    END FUNCTION GETX
    !CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
END MODULE LHS_MODULE