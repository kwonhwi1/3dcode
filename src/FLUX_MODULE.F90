MODULE FLUX_MODULE
  USE CONFIG_MODULE
  USE VARIABLE_MODULE
  USE EOS_MODULE
  IMPLICIT NONE
  PRIVATE
  PUBLIC :: T_FLUX,T_ROE,T_ROEM,T_AUSMPWP,T_AUSMPUP

  TYPE, ABSTRACT :: T_FLUX
    PRIVATE
    INTEGER :: NPV,NDV
    LOGICAL :: AXI
    REAL(8) :: UREF,STR,PREF
    REAL(8), POINTER :: PVL(:),PVR(:),DVL(:),DVR(:),SDST(:)
    REAL(8), POINTER :: NX(:),R
    PROCEDURE(P_GETSNDP2), POINTER :: GETSNDP2
    CONTAINS
      PROCEDURE :: CONSTRUCT       ! (ITURB,AXI,PRECD,UREF,STR)
      PROCEDURE :: DESTRUCT
      PROCEDURE :: SETNORM         ! (NX,NY,R)
      PROCEDURE :: SETPV           ! (PVL,PVR) P,U,V,T,Y1,Y2,K,O
      PROCEDURE :: SETDV           ! (DVL,DVR) RHO,H,RHOL,RHOV,RHOG,SND2,DRDP,DRDT,DRDY1,DRDY2,DHDP,DHDT,DHDY1,DHDY2,DRDPV,DRDTV,DRDPL,DRDTL
      PROCEDURE :: SETSDST         ! (SDST)
      PROCEDURE(P_CALFLUX), DEFERRED :: CALFLUX
  END TYPE T_FLUX

  ABSTRACT INTERFACE
    SUBROUTINE P_CALFLUX(FLUX,EOS,FX)
      IMPORT T_FLUX
      IMPORT T_EOS
      IMPLICIT NONE
      CLASS(T_FLUX), INTENT(IN) :: FLUX
      TYPE(T_EOS), INTENT(IN) :: EOS
      REAL(8), INTENT(OUT) :: FX(FLUX%NPV)
    END SUBROUTINE P_CALFLUX
  END INTERFACE
  
  TYPE, EXTENDS(T_FLUX) :: T_ROE
    CONTAINS
      PROCEDURE :: CALFLUX => ROE
  END TYPE T_ROE

  TYPE, EXTENDS(T_FLUX) :: T_ROEM
    CONTAINS
      PROCEDURE :: CALFLUX => ROEM
  END TYPE T_ROEM
  
  TYPE, EXTENDS(T_FLUX) :: T_AUSMPWP
    CONTAINS
      PROCEDURE :: CALFLUX => AUSMPWP
  END TYPE T_AUSMPWP
  
  TYPE, EXTENDS(T_FLUX) :: T_AUSMPUP
    CONTAINS
      PROCEDURE :: CALFLUX => AUSMPUP
  END TYPE T_AUSMPUP
  
  INTERFACE
    FUNCTION P_GETSNDP2(FLUX,SND2,UUU2,CUT) RESULT(SNDP2)
      IMPORT T_FLUX
      IMPLICIT NONE
      CLASS(T_FLUX), INTENT(IN) :: FLUX
      REAL(8), INTENT(IN) :: SND2,UUU2
      INTEGER, INTENT(IN) :: CUT
      REAL(8) :: SNDP2
    END FUNCTION P_GETSNDP2
  END INTERFACE
        
  CONTAINS
    !CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC 
    SUBROUTINE CONSTRUCT(FLUX,CONFIG,VARIABLE)
      IMPLICIT NONE
      CLASS(T_FLUX), INTENT(OUT) :: FLUX
      TYPE(T_CONFIG), INTENT(IN) :: CONFIG
      TYPE(T_VARIABLE), INTENT(IN) :: VARIABLE

      SELECT CASE(CONFIG%GETNAXI())
      CASE(0)
        FLUX%AXI  = .FALSE.
      CASE(1)
        FLUX%AXI  = .TRUE.
      END SELECT
      
      FLUX%UREF = CONFIG%GETUREF()
      FLUX%STR  = CONFIG%GETSTR()
      FLUX%PREF = CONFIG%GETPREF()
            
      SELECT CASE(CONFIG%GETPRECD())
      CASE(0)
        FLUX%GETSNDP2 => NO_PREC
      CASE(1)
        FLUX%GETSNDP2 => STEADY_PREC
      CASE(2)
        FLUX%GETSNDP2 => UNSTEADY_PREC
      END SELECT

      FLUX%NPV = VARIABLE%GETNPV()
      FLUX%NDV = VARIABLE%GETNDV()
      
    END SUBROUTINE CONSTRUCT
    !CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC    
    SUBROUTINE DESTRUCT(FLUX)
      IMPLICIT NONE
      CLASS(T_FLUX), INTENT(INOUT) :: FLUX

      IF(ASSOCIATED(FLUX%NX))       NULLIFY(FLUX%NX)   
      IF(ASSOCIATED(FLUX%R))        NULLIFY(FLUX%R)    
      IF(ASSOCIATED(FLUX%PVL))      NULLIFY(FLUX%PVL)  
      IF(ASSOCIATED(FLUX%PVR))      NULLIFY(FLUX%PVR)  
      IF(ASSOCIATED(FLUX%DVL))      NULLIFY(FLUX%DVL)  
      IF(ASSOCIATED(FLUX%DVR))      NULLIFY(FLUX%DVR)  
      IF(ASSOCIATED(FLUX%SDST))     NULLIFY(FLUX%SDST) 
      IF(ASSOCIATED(FLUX%GETSNDP2)) NULLIFY(FLUX%GETSNDP2)    
    END SUBROUTINE DESTRUCT
    !CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
    SUBROUTINE SETNORM(FLUX,NX,R)
      IMPLICIT NONE
      CLASS(T_FLUX), INTENT(INOUT) :: FLUX
      REAL(8), INTENT(IN), TARGET :: NX(2),R
      
      FLUX%NX => NX
      FLUX%R  => R
    
    END SUBROUTINE SETNORM
    !CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
    SUBROUTINE SETPV(FLUX,PVL,PVR)
      IMPLICIT NONE
      CLASS(T_FLUX), INTENT(INOUT) :: FLUX
      REAL(8), INTENT(IN), TARGET :: PVL(FLUX%NPV),PVR(FLUX%NPV)
      
      FLUX%PVL => PVL
      FLUX%PVR => PVR
      
    END SUBROUTINE SETPV
    !CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
    SUBROUTINE SETDV(FLUX,DVL,DVR)
      IMPLICIT NONE
      CLASS(T_FLUX), INTENT(INOUT) :: FLUX
      REAL(8), INTENT(IN), TARGET :: DVL(FLUX%NDV),DVR(FLUX%NDV)
      
      FLUX%DVL => DVL
      FLUX%DVR => DVR
      
    END SUBROUTINE SETDV
    !CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
    SUBROUTINE SETSDST(FLUX,SDST)
      IMPLICIT NONE
      CLASS(T_FLUX), INTENT(INOUT) :: FLUX
      REAL(8), INTENT(IN), TARGET :: SDST(6)
      
      FLUX%SDST => SDST
      
    END SUBROUTINE SETSDST
    !CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
    SUBROUTINE ROE(FLUX,EOS,FX)
      IMPLICIT NONE
      CLASS(T_ROE), INTENT(IN) :: FLUX
      TYPE(T_EOS), INTENT(IN) :: EOS
      REAL(8), INTENT(OUT) :: FX(FLUX%NPV)
      INTEGER :: K
      REAL(8) :: NX,NY,DL
      REAL(8) :: UURR,UULL,HTL,HTR,UV2
      REAL(8) :: RAVG(FLUX%NPV),RAVG_HT,RDV(FLUX%NDV)
      REAL(8) :: SNDP2,SNDP2_CUT
      REAL(8) :: UUU,UUP,DDD,DDD_CUT,C_STAR,C_STAR_CUT,M_STAR,DU,DP
      REAL(8) :: DF(FLUX%NPV)
      
      DL = DSQRT(FLUX%NX(1)**2+FLUX%NX(2)**2)
      
      IF(DL.EQ.0.D0) THEN
        FX = 0.D0
        GO TO 1111
      END IF
      
      NX = FLUX%NX(1)/DL
      NY = FLUX%NX(2)/DL
      
      UULL = NX*FLUX%PVL(2) + NY*FLUX%PVL(3)
      UURR = NX*FLUX%PVR(2) + NY*FLUX%PVR(3)
      HTL = FLUX%DVL(2) + 0.5D0*(FLUX%PVL(2)**2 + FLUX%PVL(3)**2)
      HTR = FLUX%DVR(2) + 0.5D0*(FLUX%PVR(2)**2 + FLUX%PVR(3)**2)
    
      ! ROE AVERAGE - 1/2 VALUES
      RAVG(1) = 0.5D0*(FLUX%PVR(1)+FLUX%PVL(1))+FLUX%PREF
      DO K=2,FLUX%NPV
        RAVG(K) = (DSQRT(FLUX%DVL(1))*FLUX%PVL(K)+DSQRT(FLUX%DVR(1))*FLUX%PVR(K)) &
                 /(DSQRT(FLUX%DVL(1))+DSQRT(FLUX%DVR(1)))
      END DO

      CALL EOS%DETEOS(RAVG(1),RAVG(4),RAVG(5),RAVG(6),RDV)
      
      UV2 = RAVG(2)**2+RAVG(3)**2
      RAVG_HT = RDV(2)+0.5D0*UV2
      UUU = NX*RAVG(2) + NY*RAVG(3)
      
      SNDP2     = FLUX%GETSNDP2(RDV(6),UV2,0)
      SNDP2_CUT = FLUX%GETSNDP2(RDV(6),UV2,1)
      
      UUP = 0.5D0*(1.D0+SNDP2/RDV(6))*UUU
      DDD = 0.5D0*DSQRT((1.D0-SNDP2/RDV(6))**2*UUU**2+4.D0*SNDP2)
      DDD_CUT = 0.5D0*DSQRT((1.D0-SNDP2_CUT/RDV(6))**2*UUU**2+4.D0*SNDP2_CUT)

      C_STAR = (DABS(UUP+DDD)+DABS(UUP-DDD))/2.D0
      C_STAR_CUT = (DABS(UUP+DDD_CUT)+DABS(UUP-DDD_CUT))/2.D0
      M_STAR = (DABS(UUP+DDD_CUT)-DABS(UUP-DDD_CUT))/2.D0/DDD_CUT

      DU = M_STAR*(UURR-UULL)+(C_STAR_CUT-SNDP2/RDV(6)*DABS(UUU)-0.5D0*(1.D0-SNDP2/RDV(6))*UUU*M_STAR)*(FLUX%PVR(1)-FLUX%PVL(1))/RDV(1)/SNDP2_CUT
      DP = M_STAR*(FLUX%PVR(1)-FLUX%PVL(1))+(C_STAR-DABS(UUU)+0.5D0*(1.D0-SNDP2/RDV(6))*UUU*M_STAR)*RDV(1)*(UURR-UULL)
      
      DF(1) = DABS(UUU)*(FLUX%DVR(1)                 - FLUX%DVL(1)                ) + DU*RDV(1)
      DF(2) = DABS(UUU)*(FLUX%DVR(1)*FLUX%PVR(2)     - FLUX%DVL(1)*FLUX%PVL(2)    ) + DU*RDV(1)*RAVG(2)  + DP*NX
      DF(3) = DABS(UUU)*(FLUX%DVR(1)*FLUX%PVR(3)     - FLUX%DVL(1)*FLUX%PVL(3)    ) + DU*RDV(1)*RAVG(3)  + DP*NY
      DF(4) = DABS(UUU)*(FLUX%DVR(1)*HTR-(FLUX%PVR(1)+FLUX%PREF) - (FLUX%DVL(1)*HTL-(FLUX%PVL(1)+FLUX%PREF))) + DU*RDV(1)*RAVG_HT  + DP*UUU
      DO K=5,FLUX%NPV
        DF(K) = DABS(UUU)*(FLUX%DVR(1)*FLUX%PVR(K) - FLUX%DVL(1)*FLUX%PVL(K)) + DU*RDV(1)*RAVG(K)
      END DO
      
      FX(1) = 0.5D0*(FLUX%DVL(1)*UULL                            + FLUX%DVR(1)*UURR                            - DF(1))*DL
      FX(2) = 0.5D0*(FLUX%DVL(1)*UULL*FLUX%PVL(2)+NX*(FLUX%PVL(1)+FLUX%PREF) + FLUX%DVR(1)*UURR*FLUX%PVR(2)+NX*(FLUX%PVR(1)+FLUX%PREF) - DF(2))*DL
      FX(3) = 0.5D0*(FLUX%DVL(1)*UULL*FLUX%PVL(3)+NY*(FLUX%PVL(1)+FLUX%PREF) + FLUX%DVR(1)*UURR*FLUX%PVR(3)+NY*(FLUX%PVR(1)+FLUX%PREF) - DF(3))*DL
      FX(4) = 0.5D0*(FLUX%DVL(1)*UULL*HTL                        + FLUX%DVR(1)*UURR*HTR                        - DF(4))*DL
      DO K=5,FLUX%NPV
        FX(K) = 0.5D0*(FLUX%DVL(1)*UULL*FLUX%PVL(K) + FLUX%DVR(1)*UURR*FLUX%PVR(K) - DF(K))*DL
      END DO
      
      IF(FLUX%AXI) FX = FX*FLUX%R
1111 CONTINUE      
    END SUBROUTINE ROE
    !CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC    
    SUBROUTINE ROEM(FLUX,EOS,FX)
      IMPLICIT NONE
      CLASS(T_ROEM), INTENT(IN) :: FLUX
      TYPE(T_EOS), INTENT(IN) :: EOS
      REAL(8), INTENT(OUT) :: FX(FLUX%NPV)
      INTEGER :: K
      REAL(8) :: NX,NY,DL
      REAL(8) :: UURR,UULL,HTL,HTR,UV2
      REAL(8) :: RAVG(FLUX%NPV),RAVG_HT
      REAL(8) :: SNDP2,SNDP2_CUT
      REAL(8) :: UUU,UUP,DDD,DDD_CUT,C_STAR,C_STAR_CUT,M_STAR
      REAL(8) :: AAA,ADD,B1,B2,RRR,FF,GG,SDST(6),B1B2 
      REAL(8) :: DQP(FLUX%NPV),FL(FLUX%NPV),FR(FLUX%NPV),BDQ(FLUX%NPV),DQ(FLUX%NPV)
      REAL(8) :: RDV(FLUX%NDV)
      
      DL = DSQRT(FLUX%NX(1)**2+FLUX%NX(2)**2)
      
      IF(DL.EQ.0.D0) THEN
        FX = 0.D0
        GO TO 1111
      END IF
      
      NX = FLUX%NX(1)/DL
      NY = FLUX%NX(2)/DL
      
      UULL = NX*FLUX%PVL(2) + NY*FLUX%PVL(3)
      UURR = NX*FLUX%PVR(2) + NY*FLUX%PVR(3)
      HTL = FLUX%DVL(2) + 0.5D0*(FLUX%PVL(2)**2 + FLUX%PVL(3)**2)
      HTR = FLUX%DVR(2) + 0.5D0*(FLUX%PVR(2)**2 + FLUX%PVR(3)**2)
    
      ! ROE AVERAGE - 1/2 VALUES
      RAVG(1) = 0.5D0*(FLUX%PVR(1)+FLUX%PVL(1))+FLUX%PREF
      DO K=2,FLUX%NPV
        RAVG(K) = (DSQRT(FLUX%DVL(1))*FLUX%PVL(K)+DSQRT(FLUX%DVR(1))*FLUX%PVR(K)) &
                 /(DSQRT(FLUX%DVL(1))+DSQRT(FLUX%DVR(1)))
      END DO

      CALL EOS%DETEOS(RAVG(1),RAVG(4),RAVG(5),RAVG(6),RDV)

      UV2 = RAVG(2)**2+RAVG(3)**2
      RAVG_HT = RDV(2)+0.5D0*UV2
      UUU = NX*RAVG(2) + NY*RAVG(3)
      
      SNDP2     = FLUX%GETSNDP2(RDV(6),UV2,0)
      SNDP2_CUT = FLUX%GETSNDP2(RDV(6),UV2,1)
      
      UUP = 0.5D0*(1.D0+SNDP2/RDV(6))*UUU
      DDD = 0.5D0*DSQRT((1.D0-SNDP2/RDV(6))**2*UUU**2+4.D0*SNDP2)
      DDD_CUT = 0.5D0*DSQRT((1.D0-SNDP2_CUT/RDV(6))**2*UUU**2+4.D0*SNDP2_CUT)

      C_STAR = (DABS(UUP+DDD)+DABS(UUP-DDD))/2.D0
      C_STAR_CUT = (DABS(UUP+DDD_CUT)+DABS(UUP-DDD_CUT))/2.D0
      M_STAR = (DABS(UUP+DDD_CUT)-DABS(UUP-DDD_CUT))/2.D0/DDD_CUT

      B1 = DMAX1(0.D0,UUP + DDD_CUT,0.5D0*(1.D0+SNDP2/RDV(6))*UURR + DDD_CUT)
      B2 = DMIN1(0.D0,UUP - DDD_CUT,0.5D0*(1.D0+SNDP2/RDV(6))*UULL - DDD_CUT)
      
      DO K=1,FLUX%NPV
        DQP(K) = FLUX%PVR(K)-FLUX%PVL(K)
      END DO
      
      DQ(1) = FLUX%DVR(1)             - FLUX%DVL(1) 
      DQ(2) = FLUX%DVR(1)*FLUX%PVR(2) - FLUX%DVL(1)*FLUX%PVL(2)
      DQ(3) = FLUX%DVR(1)*FLUX%PVR(3) - FLUX%DVL(1)*FLUX%PVL(3)
      DQ(4) = FLUX%DVR(1)*HTR         - FLUX%DVL(1)*HTL
      DO K=5,FLUX%NPV
        DQ(K) = FLUX%DVR(1)*FLUX%PVR(K) - FLUX%DVL(1)*FLUX%PVL(K)
      END DO
      
      FL(1) = FLUX%DVL(1)*UULL
      FL(2) = FLUX%DVL(1)*UULL*FLUX%PVL(2)+NX*(FLUX%PVL(1)+FLUX%PREF)
      FL(3) = FLUX%DVL(1)*UULL*FLUX%PVL(3)+NY*(FLUX%PVL(1)+FLUX%PREF)
      FL(4) = FLUX%DVL(1)*UULL*HTL
      DO K=5,FLUX%NPV
        FL(K) = FLUX%DVL(1)*UULL*FLUX%PVL(K)
      END DO
      
      FR(1) = FLUX%DVR(1)*UURR
      FR(2) = FLUX%DVR(1)*UURR*FLUX%PVR(2)+NX*(FLUX%PVR(1)+FLUX%PREF)
      FR(3) = FLUX%DVR(1)*UURR*FLUX%PVR(3)+NY*(FLUX%PVR(1)+FLUX%PREF)
      FR(4) = FLUX%DVR(1)*UURR*HTR
      DO K=5,FLUX%NPV
        FR(K) = FLUX%DVR(1)*UURR*FLUX%PVR(K)
      END DO
      
      DO K=1,6
        SDST(K) = FLUX%SDST(K)+FLUX%PREF+0.5D0*RDV(1)*RDV(6)
      END DO
      
      FF = 1.D0 - DMIN1(SDST(3)/SDST(4),SDST(4)/SDST(3),SDST(1)/SDST(3),SDST(3)/SDST(1),SDST(5)/SDST(3),SDST(3)/SDST(5) &
                       ,SDST(4)/SDST(6),SDST(6)/SDST(4),SDST(4)/SDST(2),SDST(2)/SDST(4))
      
      IF(UUU .NE. 0.D0) THEN
        FF = (DABS(UUP)/DDD_CUT)**FF
      ELSE
        FF = 1.D0
      END IF
      
      GG = 1.D0 - DMIN1(SDST(3)/SDST(4),SDST(4)/SDST(3))
      
      IF(UUU .NE. 0.D0) THEN
        GG = (DABS(UUP)/DDD_CUT)**GG
      ELSE
        GG = 1.D0
      END IF
      
      ADD = C_STAR-DABS(UUU)+0.5D0*(1.D0-SNDP2/RDV(6))*UUU*M_STAR
      AAA = (C_STAR_CUT-SNDP2/RDV(6)*DABS(UUU)-0.5D0*(1.D0-SNDP2/RDV(6))*UUU*M_STAR)
      
      IF((M_STAR*UUP-C_STAR).EQ.0.D0) THEN
        RRR = 0.D0
      ELSE
        RRR = -1.D0/(M_STAR*UUP-C_STAR)
      END IF
      
      BDQ(1) = (ADD*DQ(1)-FF*AAA*DQP(1)/SNDP2_CUT) 
      BDQ(2) = BDQ(1)*RAVG(2)  + RDV(1)*ADD*(DQP(2)-NX*(UURR-UULL))
      BDQ(3) = BDQ(1)*RAVG(3)  + RDV(1)*ADD*(DQP(3)-NY*(UURR-UULL))
      BDQ(4) = BDQ(1)*RAVG_HT  + RDV(1)*ADD*(HTR-HTL)
      DO K=5,FLUX%NPV
        BDQ(K) = BDQ(1)*RAVG(K) + RDV(1)*ADD*DQP(K)
      END DO
      B1B2 = B1*B2 + DDD_CUT**2 - DDD*DDD_CUT
      DO K=1,FLUX%NPV
        FX(K) = (B1*FL(K)-B2*FR(K)+B1B2*DQ(K)-GG*B1B2*RRR*BDQ(K))/(B1-B2)*DL
      END DO
      IF(FLUX%AXI) FX = FX*FLUX%R
1111 CONTINUE    
    END SUBROUTINE ROEM
    !CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC     
    SUBROUTINE AUSMPWP(FLUX,EOS,FX)
      IMPLICIT NONE
      CLASS(T_AUSMPWP), INTENT(IN) :: FLUX
      TYPE(T_EOS), INTENT(IN) :: EOS
      REAL(8), INTENT(OUT) :: FX(FLUX%NPV)
      INTEGER :: K
      REAL(8) :: NX,NY,DL
      REAL(8) :: KU
      REAL(8) :: UURR,UULL,HTR,HTL
      REAL(8) :: RAVG(FLUX%NPV),RDV(FLUX%NDV)
      REAL(8) :: AMID,ZML,ZMR,AM2MID
      REAL(8) :: AM2RMID,AM2RMID1,FMID,FMID1,ALPHA
      REAL(8) :: ZMMR,PMR,ZMPL,PPL,PMID,ZMID
      REAL(8) :: WW1,WW2,WW,SDST(6)
      REAL(8) :: PMT,PWL,PWR,ZMPL1,ZMMR1
      REAL(8), PARAMETER :: BETA = 1.D0/8.D0
      
      KU = 0.75D0
      DL = DSQRT(FLUX%NX(1)**2+FLUX%NX(2)**2)
      
      IF(DL.EQ.0.D0) THEN
        FX = 0.D0
        GO TO 1111
      END IF
      
      NX = FLUX%NX(1)/DL
      NY = FLUX%NX(2)/DL
      
      UULL = NX*FLUX%PVL(2) + NY*FLUX%PVL(3)
      UURR = NX*FLUX%PVR(2) + NY*FLUX%PVR(3)
      HTL = FLUX%DVL(2) + 0.5D0*(FLUX%PVL(2)**2 + FLUX%PVL(3)**2)
      HTR = FLUX%DVR(2) + 0.5D0*(FLUX%PVR(2)**2 + FLUX%PVR(3)**2)
    
      ! ROE AVERAGE - 1/2 VALUES
      RAVG(1) = 0.5D0*(FLUX%PVR(1)+FLUX%PVL(1))+FLUX%PREF
      DO K=2,FLUX%NPV
        RAVG(K) = (DSQRT(FLUX%DVL(1))*FLUX%PVL(K)+DSQRT(FLUX%DVR(1))*FLUX%PVR(K)) &
                 /(DSQRT(FLUX%DVL(1))+DSQRT(FLUX%DVR(1)))
      END DO
      
      CALL EOS%DETEOS(RAVG(1),RAVG(4),RAVG(5),RAVG(6),RDV)
      
      AMID = DSQRT(RDV(6))
      
      ZMR = UURR/AMID
      ZML = UULL/AMID

      AM2MID = (RAVG(2)**2+RAVG(3)**2)/RDV(6)
      AM2RMID  = FLUX%GETSNDP2(RDV(6),AM2MID*RDV(6),1)/RDV(6)
      AM2MID = 0.5D0*(ZML**2+ZMR**2)
      AM2RMID1 = FLUX%GETSNDP2(RDV(6),AM2MID*RDV(6),0)/RDV(6)

            
      FMID = DSQRT(AM2RMID)*(2.D0-DSQRT(AM2RMID))
      FMID1 = DSQRT(AM2RMID1)*(2.D0-DSQRT(AM2RMID1))
      
      ALPHA = 3.D0/16.D0*(-4.D0+5.D0*FMID1**2)
      ALPHA = DMAX1(DMIN1(ALPHA,3.D0/16.D0),-3.D0/4.D0)
      
      IF(DABS(ZMR).GE.1.D0) THEN
        ZMMR = 0.5D0*(ZMR-DABS(ZMR))
        PMR = 0.5D0*(1.D0-ZMR/DABS(ZMR))
      ELSE
        ZMMR = -0.25D0*(ZMR-1.D0)**2 - BETA*(ZMR**2-1.D0)**2
        PMR = 0.25D0*(ZMR-1.D0)**2*(2.D0+ZMR) - ALPHA*ZMR*(ZMR**2-1.D0)**2
      END IF
      
      IF(DABS(ZML).GE.1.D0) THEN
        ZMPL = 0.5D0*(ZML+DABS(ZML))
        PPL = 0.5D0*(1.D0+ZML/DABS(ZML))
      ELSE
        ZMPL = 0.25D0*(ZML+1.D0)**2 + BETA*(ZML**2-1.D0)**2  
        PPL = 0.25D0*(ZML+1.D0)**2*(2.D0-ZML) + ALPHA*ZML*(ZML**2-1.D0)**2
      END IF
      
      ZMID = ZMPL + ZMMR
      PMID = PPL*(FLUX%PVL(1)+FLUX%PREF) + PMR*(FLUX%PVR(1)+FLUX%PREF) - KU*2.D0*PPL*PMR*RDV(1)*FMID1*AMID*(UURR-UULL)
      
      DO K=1,6
        SDST(K) = FLUX%SDST(K)+FLUX%PREF+0.5D0*RDV(1)*RDV(6)
      END DO
      
      WW1 = 1.D0-DMIN1(SDST(3)/SDST(4),SDST(4)/SDST(3))**3
      WW2 = 1.D0-DMIN1(1.D0,DMIN1(SDST(1),SDST(2),SDST(5),SDST(6))/DMAX1(SDST(1),SDST(2),SDST(5),SDST(6)))**2
      !WW2 = (1.D0-DMIN1(1.D0,4.D0*(SDST(4)-SDST(3))/(SDST(6)+SDST(5)-SDST(1)-SDST(2)+1.D-12)))**2*(1.D0-DMIN1(SDST(3)/SDST(4),SDST(4)/SDST(3)))**2
      WW = DMAX1(WW1,WW2)
      PMT = PMID + 0.5D0*RDV(1)*RDV(6)
      
      IF(PMT.NE.0.D0) THEN
        PWL = ((FLUX%PVL(1)+FLUX%PREF+0.5D0*RDV(1)*RDV(6))/PMT-1.D0)*(1.D0-WW2)/FMID
        PWR = ((FLUX%PVR(1)+FLUX%PREF+0.5D0*RDV(1)*RDV(6))/PMT-1.D0)*(1.D0-WW2)/FMID
      ELSE
        PWL = 0.D0
        PWR = 0.D0
      END IF
      
      IF( ZMID .GE. 0.D0) THEN
        ZMPL1 = ZMPL + ZMMR*((1.D0-WW)*(1.D0+PWR)-PWL)
        ZMMR1 = ZMMR*WW*(1.D0+PWR)
      ELSE
        ZMPL1 = ZMPL*WW*(1.D0+PWL)
        ZMMR1 = ZMMR + ZMPL*((1.D0-WW)*(1.D0+PWL)-PWR)
      END IF
      
      FX(1) = (ZMPL1*AMID*FLUX%DVL(1)              + ZMMR1*AMID*FLUX%DVR(1)                      )*DL
      FX(2) = (ZMPL1*AMID*FLUX%DVL(1)*FLUX%PVL(2)  + ZMMR1*AMID*FLUX%DVR(1)*FLUX%PVR(2) + PMID*NX)*DL
      FX(3) = (ZMPL1*AMID*FLUX%DVL(1)*FLUX%PVL(3)  + ZMMR1*AMID*FLUX%DVR(1)*FLUX%PVR(3) + PMID*NY)*DL
      FX(4) = (ZMPL1*AMID*FLUX%DVL(1)*HTL          + ZMMR1*AMID*FLUX%DVR(1)*HTR                  )*DL
      DO K=5,FLUX%NPV
        FX(K) = (ZMPL1*AMID*FLUX%DVL(1)*FLUX%PVL(K)+ ZMMR1*AMID*FLUX%DVR(1)*FLUX%PVR(K))*DL
      END DO
      
      IF(FLUX%AXI) FX = FX*FLUX%R
1111 CONTINUE        
    END SUBROUTINE AUSMPWP
    !CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC    
    SUBROUTINE AUSMPUP(FLUX,EOS,FX)
      IMPLICIT NONE
      CLASS(T_AUSMPUP), INTENT(IN) :: FLUX
      TYPE(T_EOS), INTENT(IN) :: EOS
      REAL(8), INTENT(OUT) :: FX(FLUX%NPV)
      INTEGER :: K
      REAL(8) :: NX,NY,DL
      REAL(8) :: KU,KP
      REAL(8) :: UURR,UULL,HTR,HTL
      REAL(8) :: RAVG(FLUX%NPV),RDV(FLUX%NDV)
      REAL(8) :: AMID,ZML,ZMR,AM2MID
      REAL(8) :: AM2RMID,AM2RMID1,FMID,FMID1,ALPHA
      REAL(8) :: ZMMR,PMR,ZMPL,PPL,PMID,ZMID
      REAL(8) :: ZMPL1,ZMMR1  
      REAL(8), PARAMETER :: BETA = 1.D0/8.D0  
      
      KP = 0.25D0
      KU = 0.75D0
      DL = DSQRT(FLUX%NX(1)**2+FLUX%NX(2)**2)
      
      IF(DL.EQ.0.D0) THEN
        FX = 0.D0
        GO TO 1111
      END IF
      
      NX = FLUX%NX(1)/DL
      NY = FLUX%NX(2)/DL
      
      UULL = NX*FLUX%PVL(2) + NY*FLUX%PVL(3)
      UURR = NX*FLUX%PVR(2) + NY*FLUX%PVR(3)
      HTL = FLUX%DVL(2) + 0.5D0*(FLUX%PVL(2)**2 + FLUX%PVL(3)**2)
      HTR = FLUX%DVR(2) + 0.5D0*(FLUX%PVR(2)**2 + FLUX%PVR(3)**2)
    
      ! ROE AVERAGE - 1/2 VALUES
      RAVG(1) = 0.5D0*(FLUX%PVR(1)+FLUX%PVL(1))+FLUX%PREF
      DO K=2,FLUX%NPV
        RAVG(K) = (DSQRT(FLUX%DVL(1))*FLUX%PVL(K)+DSQRT(FLUX%DVR(1))*FLUX%PVR(K)) &
                 /(DSQRT(FLUX%DVL(1))+DSQRT(FLUX%DVR(1)))
      END DO

      CALL EOS%DETEOS(RAVG(1),RAVG(4),RAVG(5),RAVG(6),RDV)
      
      AMID = DSQRT(RDV(6))
      
      ZMR = UURR/AMID
      ZML = UULL/AMID
      
      AM2MID = (RAVG(2)**2+RAVG(3)**2)/RDV(6)
      AM2RMID  = FLUX%GETSNDP2(RDV(6),AM2MID*RDV(6),1)/RDV(6)
      AM2MID = 0.5D0*(ZML**2+ZMR**2)
      AM2RMID1 = FLUX%GETSNDP2(RDV(6),AM2MID*RDV(6),0)/RDV(6)
            
      FMID = DSQRT(AM2RMID)*(2.D0-DSQRT(AM2RMID))
      FMID1 = DSQRT(AM2RMID1)*(2.D0-DSQRT(AM2RMID1))
      
      ALPHA = 3.D0/16.D0*(-4.D0+5.D0*FMID1**2)
      ALPHA = DMAX1(DMIN1(ALPHA,3.D0/16.D0),-3.D0/4.D0)
      
      IF(DABS(ZMR).GE.1.D0) THEN
        ZMMR = 0.5D0*(ZMR-DABS(ZMR))
        PMR = 0.5D0*(1.D0-ZMR/DABS(ZMR))
      ELSE
        ZMMR = -0.25D0*(ZMR-1.D0)**2 - BETA*(ZMR**2-1.D0)**2
        PMR = 0.25D0*(ZMR-1.D0)**2*(2.D0+ZMR) - ALPHA*ZMR*(ZMR**2-1.D0)**2
      END IF
      
      IF(DABS(ZML).GE.1.D0) THEN
        ZMPL = 0.5D0*(ZML+DABS(ZML))
        PPL = 0.5D0*(1.D0+ZML/DABS(ZML))
      ELSE
        ZMPL = 0.25D0*(ZML+1.D0)**2 + BETA*(ZML**2-1.D0)**2  
        PPL = 0.25D0*(ZML+1.D0)**2*(2.D0-ZML) + ALPHA*ZML*(ZML**2-1.D0)**2
      END IF
      
      ZMID = ZMPL + ZMMR - KP*DMAX1(1.D0-AM2MID,0.D0)*(FLUX%PVR(1)-FLUX%PVL(1))/RDV(1)/RDV(6)/FMID
      PMID = PPL*(FLUX%PVL(1)+FLUX%PREF) + PMR*(FLUX%PVR(1)+FLUX%PREF) - KU*2.D0*PPL*PMR*RDV(1)*FMID1*AMID*(UURR-UULL)
      
      IF(ZMID.GT.0.D0) THEN
        ZMPL1 = ZMID 
        ZMMR1 = 0.D0
      ELSE
        ZMMR1 = ZMID
        ZMPL1 = 0.D0
      END IF
      
      FX(1) = (ZMPL1*AMID*FLUX%DVL(1)              + ZMMR1*AMID*FLUX%DVR(1)                      )*DL
      FX(2) = (ZMPL1*AMID*FLUX%DVL(1)*FLUX%PVL(2)  + ZMMR1*AMID*FLUX%DVR(1)*FLUX%PVR(2) + PMID*NX)*DL
      FX(3) = (ZMPL1*AMID*FLUX%DVL(1)*FLUX%PVL(3)  + ZMMR1*AMID*FLUX%DVR(1)*FLUX%PVR(3) + PMID*NY)*DL
      FX(4) = (ZMPL1*AMID*FLUX%DVL(1)*HTL          + ZMMR1*AMID*FLUX%DVR(1)*HTR                  )*DL
      DO K=5,FLUX%NPV
        FX(K) = (ZMPL1*AMID*FLUX%DVL(1)*FLUX%PVL(K)+ ZMMR1*AMID*FLUX%DVR(1)*FLUX%PVR(K))*DL
      END DO
      
      IF(FLUX%AXI) FX = FX*FLUX%R
1111 CONTINUE          
    END SUBROUTINE AUSMPUP
    !CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
    FUNCTION NO_PREC(FLUX,SND2,UUU2,CUT) RESULT(SNDP2)
      IMPLICIT NONE
      CLASS(T_FLUX), INTENT(IN) :: FLUX
      REAL(8), INTENT(IN) :: SND2,UUU2
      INTEGER, INTENT(IN) :: CUT
      REAL(8) :: SNDP2
      
      SNDP2 = SND2
      
    END FUNCTION NO_PREC
    !CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
    FUNCTION STEADY_PREC(FLUX,SND2,UUU2,CUT) RESULT(SNDP2)
      IMPLICIT NONE
      CLASS(T_FLUX), INTENT(IN) :: FLUX
      REAL(8), INTENT(IN) :: SND2,UUU2
      INTEGER, INTENT(IN) :: CUT
      REAL(8) :: SNDP2  
      
      SNDP2 = DMIN1(SND2,DMAX1(UUU2,DBLE(CUT)*FLUX%UREF**2))
      
    END FUNCTION STEADY_PREC
    !CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
    FUNCTION UNSTEADY_PREC(FLUX,SND2,UUU2,CUT) RESULT(SNDP2)
      IMPLICIT NONE
      CLASS(T_FLUX), INTENT(IN) :: FLUX
      REAL(8), INTENT(IN) :: SND2,UUU2
      INTEGER, INTENT(IN) :: CUT
      REAL(8) :: SNDP2  
      
      SNDP2 = DMIN1(SND2,DMAX1(UUU2,DBLE(CUT)*FLUX%UREF**2,DBLE(CUT)*FLUX%STR**2*UUU2))
      
    END FUNCTION UNSTEADY_PREC
    !CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
END MODULE FLUX_MODULE